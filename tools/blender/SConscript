import os
import shutil
import fnmatch

env = Environment()

params = Variables(['custom.py']) 
params.Add(('releaseVersion', 'Release version (string)', '1.0'))
params.Update(env)

# blender addon
def globFiles(env, search, pattern, result, recursive=True):
	oldcwd = os.getcwd()
	os.chdir(env.Dir('.').srcnode().abspath)
	
	for root, dirs, files in os.walk(search):
		if recursive:
			if '.svn' in dirs:
				dirs.remove('.svn')
			if '.git' in dirs:
				dirs.remove('.git')
			if '__pycache__' in dirs:
				dirs.remove('__pycache__')
		else:
			del dirs[:]
		
		for s in fnmatch.filter(files, pattern):
			result.append(root + os.sep + s)
	
	os.chdir(oldcwd)

def createZipFile(env, target, source):
	shutil.make_archive(target[0].abspath, 'zip', source[0].abspath)

contentVersion = env['releaseVersion']

try:
	versionParts = [int(x) for x in contentVersion.split('.')]
	while len(versionParts) < 3:
		versionParts.append(0)
	blenderVersionString = '.'.join([str(x) for x in versionParts])
except:
	blenderVersionString = '9999.0.0'

content = []

dataFiles = []
globFiles(env, 'democap-tools', '*', dataFiles)

for f in dataFiles:
	path = f.split('/')
	content.append(env.InstallAs(os.path.join('content', 'democap-tools', *path[1:]), f))

def copyModifyFile(env, target, source):
	with open(source[0].abspath, 'r') as f:
		content = f.read()
	content = content.replace('{RELEASE_VERSION}', blenderVersionString)
	with open(target[0].abspath, 'w') as f:
		f.write(content)

dataFiles.extend(env.Command('content/democap-tools/blender_manifest.toml',
	'blender_manifest.toml.in', env.Action(copyModifyFile, 'Copy-Modify $TARGET')))

contentArchive = env.Command('blender-democap-tools-{}'.format(contentVersion),
	'content', env.Action(createZipFile, 'Archive Blender Addon'))
env.Depends(contentArchive, content)

Default(env.Alias('blenderAddon', contentArchive))

# default
