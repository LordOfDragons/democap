/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap

pin Democap.Characters
pin Democap.Elements

pin Dragengine.CameraDirectors
pin Dragengine.Gui
pin Dragengine.Scenery
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * Task ensuring active character is present in game world. Also stores the VR Menu and
 * other gadgets potentially used by the player.
 */
class WorldSpawnCharacter
	/** Listener. */
	public interface Listener
		/** Character configuration changed. */
		func void characterChanged(WorldSpawnCharacter wsc)
		
		/** Charatcer actor is ready to be used. */
		func void actorReady(WorldSpawnCharacter wsc)
		
		/** Charatcer actor is not ready to be used anymore. */
		func void actorNotReady(WorldSpawnCharacter wsc)
		
		/** Game world changed. */
		func void gameWorldChanged(WorldSpawnCharacter wsc)
	end
	
	/** Default implementation of listener. */
	public class DefaultListener implements Listener
		public func new()
		end
		
		/** Character configuration changed. */
		public func void characterChanged(WorldSpawnCharacter wsc)
		end
		
		/** Charatcer actor is ready to be used. */
		public func void actorReady(WorldSpawnCharacter wsc)
		end
		
		/** Charatcer actor is not ready to be used anymore. */
		public func void actorNotReady(WorldSpawnCharacter wsc)
		end
		
		/** Game world changed. */
		public func void gameWorldChanged(WorldSpawnCharacter wsc)
		end
	end
	
	/** Frame updater. */
	protected class UpdateCheck extends FrameUpdateListener
		private var WorldSpawnCharacter pOwner
		
		public func new(WorldSpawnCharacter owner)
			pOwner = owner
		end
		
		public func void frameUpdate(float elapsed)
			pOwner.updateCheck()
		end
	end
	
	/** Character listener. */
	protected class UpdateCharacter extends DefaultCharacterListener
		private var WorldSpawnCharacter pOwner
		
		public func new(WorldSpawnCharacter owner)
			pOwner = owner
		end
		
		public func void calibrated(CharacterProfile profile, CharacterConfiguration config)
			pOwner.recreateActor()
		end
	end
	
	
	
	private var GameWorld pGameWorld
	private var SpawnPoint pSpawnPoint
	private var CharacterConfiguration pCharacter
	private var BaseVRActor pActor
	private var UpdateCheck pUpdateCheck
	private var UpdateCharacter pUpdateCharacter
	private var DVector pLastActorPosition
	private var Quaternion pLastActorOrientation
	private var BehaviorElement pVRMenu
	private var ElementClass pMoCapActorClass
	private var Array pListeners
	
	
	
	/** Create. */
	public func new()
		pUpdateCharacter = UpdateCharacter.new(this)
		pLastActorPosition = DVector.new()
		pLastActorOrientation = Quaternion.new()
		pMoCapActorClass = GameApp.getGameApp().getElementClassList().getNamed("MoCapActor")
		pListeners = Array.new()
	end
	
	/** Dispose. */
	public func void dispose()
		if pVRMenu != null
			pVRMenu.safeDispose()
			pVRMenu = null
		end
		
		pListeners.removeAll()
		
		stopUpdateCheck()
		disposeActor()
		pUpdateCharacter = null
	end
	
	
	
	/** Game world or null. */
	public func GameWorld getGameWorld()
		return pGameWorld
	end
	
	/** Set game world or null. */
	public func void setGameWorld(GameWorld gameWorld)
		if gameWorld == pGameWorld
			return
		end
		
		// remove the VR menu from the game world. it will be reused
		if pVRMenu != null
			pVRMenu.removeFromGameWorldIfPresent()
		end
		
		// dispose of the actor. we could move the actor to the new game world but better
		// start with a fresh copy
		disposeActor()
		pSpawnPoint = null
		
		// set game world
		pGameWorld = gameWorld
		
		if gameWorld != null
			// find spawn point
			pSpawnPoint = gameWorld.getSpawnPoints().getNamed("player")
			
			// add VR menu creating it if required
			if pVRMenu == null
				pVRMenu = createVRMenu()
			end
			gameWorld.addElement(pVRMenu)
			
			// move the camera to a good starting position showing the character
			var VRCameraDirector director = GameApp.getGameApp().getVRCameraDirector()
			
			if director != null
				var DMatrix matrix = DMatrix.new()
				if pSpawnPoint != null
					matrix = pSpawnPoint.getMatrix()
				end
				
				var Vector angles = (Matrix.newRotation(-20, 180, 0) * matrix.getRotation().toMatrix()).getEulerAngles()
				director.setPivot(matrix * DVector.new(0, 1.4, 1))
				director.setAzimuth(angles.getY())
				director.setElevation(-angles.getX())
			end
		end
		
		pListeners.forEach(block Listener each
			each.gameWorldChanged(this)
		end)
		
		startUpdateCheck()
	end
	
	/** Spawn point or null. */
	public func SpawnPoint getSpawnPoint()
		return pSpawnPoint
	end
	
	/** Character or null. */
	public func CharacterConfiguration getCharacter()
		return pCharacter
	end
	
	/** Set character or null. */
	public func void setCharacter(CharacterConfiguration character)
		if character == pCharacter
			return
		end
		
		if pCharacter != null
			pCharacter.getProfile().removeListener(pUpdateCharacter)
		end
		disposeActor()
		
		pCharacter = character
		
		if character != null
			character.getProfile().addListener(pUpdateCharacter)
		end
		
		pListeners.forEach(block Listener each
			each.characterChanged(this)
		end)
		
		startUpdateCheck()
	end
	
	/** Recreate actor. */
	public func void recreateActor()
		disposeActor()
		startUpdateCheck()
	end
	
	/** Actor or null. */
	public func BaseVRActor getActor()
		return pActor
	end
	
	
	
	/** VR Menu or null if not created yet. */
	public func BehaviorElement getVRMenu()
		return pVRMenu
	end
	
	
	
	/** Add listener. */
	public func void addListener(Listener listener)
		if listener == null
			throw ENullPointer.new("listener")
		end
		pListeners.add(listener)
	end
	
	/** Remove listener. */
	public func void removeListener(Listener listener)
		pListeners.remove(listener)
	end
	
	
	
	/** Start update check if not running. */
	protected func void startUpdateCheck()
		if pUpdateCheck == null
			pUpdateCheck = UpdateCheck.new(this)
		end
	end
	
	/** Stop update check if running. */
	protected func void stopUpdateCheck()
		if pUpdateCheck != null
			pUpdateCheck.giveUp()
			pUpdateCheck = null
		end
	end
	
	/** Update check. */
	public func void updateCheck()
		if pGameWorld == null or pActor != null
			stopUpdateCheck()
			return
		end
		
		if pCharacter != null and (pCharacter.getElementClass() == null or pCharacter.isElementClassPreloading())
			return
		end
		
		stopUpdateCheck()
		
		try
			createActor()
			
		catch Exception e
			GameApp.getGameApp().getConsole().addError("creating actor failed", e)
			disposeActor()
		end
	end
	
	/** Dispose actor if present. */
	protected func void disposeActor()
		if pActor == null
			return
		end
		
		pLastActorPosition = pActor.getPosition()
		pLastActorOrientation = pActor.getOrientation()
		
		pActor.safeDispose()
		pActor = null
		
		pListeners.forEach(block Listener each
			each.actorNotReady(this)
		end)
	end
	
	/** Create actor. */
	protected func void createActor()
		// ensure spawn point is present
		if pSpawnPoint == null
			throw EInvalidParam.new("Spawn point 'player' not found in game world")
		end
		
		// spawn player actor
		var StubElement stub
		
		if pCharacter != null
			stub = pCharacter.createStubElement()
			
		else
			stub = StubElement.new(pMoCapActorClass, GameApp.getGameApp().getIDGenerator().nextID())
		end
		
		stub.setPosition(pLastActorPosition)
		stub.setRotation(pLastActorOrientation.getEulerAngles())
		
		pActor = stub.createElement() cast BaseVRActor
		pSpawnPoint.spawn(pGameWorld, pActor)
		
		// take control of actor
		pActor.getPlayerControllable().takeControl()
		
		pListeners.forEach(block Listener each
			each.actorReady(this)
		end)
	end
	
	/** Create VR menu. */
	protected func BehaviorElement createVRMenu()
		var ElementClass eclass = GameApp.getGameApp().getElementClassList().getNamed("VRMenu")
		var StubElement stub = StubElement.new(eclass, GameApp.getGameApp().getIDGenerator().nextID())
		
		// add properties if required
		stub.setPosition(DVector.new(0, 1, 0))
		
		stub.setPosition(DVector.new(-0.75, 1.6, 0.75))
		stub.setRotation(Vector.new(0, -135, 0))
		
		return stub.createElement() cast BehaviorElement
	end
end
