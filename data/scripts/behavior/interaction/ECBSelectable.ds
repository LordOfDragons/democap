/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Dragengine.Preloading
pin Dragengine.Scenery


/**
 * Behavior adding support for element to be selected. Selection tracker is present in the
 * game application. Only one element can be selected at each time or none at all.
 */
class ECBSelectable extends DefaultECBehavior
	/** Keeps track of the selected element. */
	class Tracker
		/** Listener for tracker events. */
		interface Listener
			/** Selected element changed. */
			func void selectedElementChanged(Tracker tracker)
		end
		
		/** Default implementation of tracker listener. */
		class DefaultListener implements Listener
			/** Create default listener. */
			public func new()
			end
			
			/** Selected element changed. */
			public func void selectedElementChanged(Tracker tracker)
			end
		end
		
		
		private var Instance pElement
		private var Array pListeners
		
		
		/** Create tracker. */
		public func new()
			pListeners = Array.new()
		end
		
		
		/** Selected element or null if not assigned. */
		public func Instance getElement()
			return pElement
		end
		
		/** Set selected element or null if not assigned. */
		public func void setElement(Instance element)
			if pElement == element
				return
			end
			
			var Instance previous = pElement
			
			pElement = element
			
			if previous != null
				previous.notifyDeselected()
			end
			if element != null
				pElement.notifySelected()
			end
			
			pListeners.forEach(block Listener each
				each.selectedElementChanged(this)
			end)
		end
		
		/** Clear selection. */
		public func void clear()
			setElement(null)
		end
		
		
		/** Add listener. */
		public func void addListener(Listener listener)
			if listener == null
				throw ENullPointer.new("listener")
			end
			pListeners.add(listener)
		end
		
		/** Remove listener. */
		public func void removeListener(Listener listener)
			pListeners.remove(listener)
		end
	end
	
	/** Listener for behavior instance events. */
	interface Listener
		/** Became selected element. */
		func void selected(Instance instance)
		
		/** Ceased being selected element. */
		func void deselected(Instance instance)
	end
	
	/** Default implementation of behavior instance listener. */
	class DefaultListener implements Listener
		/** Create default listener. */
		public func new()
		end
		
		/** Became selected element. */
		public func void selected(Instance instance)
		end
		
		/** Ceased being selected element. */
		public func void deselected(Instance instance)
		end
	end
	
	
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		private var ECBSelectable pECBehavior
		private var Array pListeners
		
		
		/** Create instance. */
		public func new(ECBSelectable ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pListeners = Array.new()
		end
		
		/** Dispose of instance. */
		public func void dispose()
			if GameApp.getGameApp().getSelectionTracker() != null
				if GameApp.getGameApp().getSelectionTracker().getElement() == this
					GameApp.getGameApp().getSelectionTracker().setElement(null)
				end
				GameApp.getGameApp().getSelectionTracker() = null
			end
			
			pListeners = null
			super.dispose()
		end
		
		
		/** Behavior. */
		public func ECBSelectable getECBehavior()
			return pECBehavior
		end
		
		
		/** Element is the selected element. */
		public func bool isSelected()
			return GameApp.getGameApp().getSelectionTracker().getElement() == this
		end
		
		/** Element is not the selected element. */
		public func bool isDeselected()
			return GameApp.getGameApp().getSelectionTracker().getElement() != this
		end
		
		/** Select element. */
		public func void selectElement()
			GameApp.getGameApp().getSelectionTracker().setElement(this)
		end
		
		/** Selected element. */
		public func Instance getSelected()
			return GameApp.getGameApp().getSelectionTracker().getElement()
		end
		
		/** Called by Tracker to notify behavior listeners. */
		public func void notifySelected()
			pListeners.forEach(block Listener each
				each.selected(this)
			end)
		end
		
		/** Called by Tracker to notify behavior listeners. */
		public func void notifyDeselected()
			pListeners.forEach(block Listener each
				each.deselected(this)
			end)
		end
		
		
		/** Add listener. */
		public func void addListener(Listener listener)
			if listener == null
				throw EInvalidParam.new()
			end
			pListeners.add(listener)
		end
		
		/** Remove listener. */
		public func void removeListener(Listener listener)
			pListeners.remove(listener)
		end
	end
	
	
	/** Create behavior element class. */
	public func new(BehaviorElementClass eclass) super(eclass)
		eclass.addBehavior(this)
	end
	
	
	/** Get instance in element from owner element class. */
	public func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	/** Get behavior in element class or null if absent. */
	static public func ECBSelectable getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBSelectable each
			return true
		end) cast ECBSelectable
	end
	
	/** Get instance in element or null if absent. */
	static public func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	
	/** Create Behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
