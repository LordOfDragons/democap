/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Dragengine.Scenery
pin Dragengine.Preloading
pin Dragengine.Utils


/**
 * Behavior adding support to rotate object using handle.
 */
class ECBInteractRotate extends ECBInteractHandle
	/** Behavior instance. */
	class Instance extends ECBInteractHandle.Instance
		protected var ECBInteractRotate pECBehavior
		protected var Vector pAxis
		protected var Quaternion pInteractOrientation
		protected var DVector pInteractOrigin
		protected var Vector pInteractAxis
		protected var DVector pPlanePosition
		protected var Vector pPlaneNormal
		protected var Vector pPlaneX
		protected var Vector pPlaneY
		
		
		
		/** Create behavior instance. */
		func new(ECBInteractRotate ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
		end
		
		/** Init behavior instance. */
		func void init(StubElement stub)
			var CodecPropertyString codec = getElement().getClass().getCodecPropertyString()
			pAxis = pECBehavior.getAxis().getVector(stub, codec)
		end
		
		
		
		/** Behavior. */
		func ECBInteractRotate getECBehavior()
			return pECBehavior
		end
		
		/** Rotatement axis. */
		func Vector getAxis()
			return pAxis
		end
		
		
		
		/** Start interacting. */
		func void startInteract(Matrix viewMatrix, DVector rayOrigin, Vector rayDirection, DVector hitPoint)
			var Element element = getElement().getParent()
			if element == null
				return
			end
			
			pInteractOrientation = element.getOrientation()
			pInteractOrigin = hitPoint
			pInteractAxis = Matrix.newFromQuaternion(pInteractOrientation).transformNormal(pAxis)
			
			var DVector position = element.getPosition()
			pPlanePosition = position + DVector.new(pInteractAxis)\
				* ((pInteractOrigin - position) * DVector.new(pInteractAxis))
			pPlaneNormal = pInteractAxis
			
			// the direction towards the interaction point is used as X axis. the Y axis is
			// then the X axis crossed with the plane normal. this allows to use the plane
			// normal as rotation axis and DEMath.atan2() to calculate the rotation angle
			
			// ensure the interaction point is on the plane. this prevents the rotation
			// jumping after the first mouse move due to the hit point being slightly
			// outside the plane
			var float denom = rayDirection * pPlaneNormal
			if denom != 0
				var float lambda = ((pPlanePosition - rayOrigin).toVector() * pPlaneNormal) / denom
				var DVector hp = rayOrigin + DVector.new(rayDirection) * lambda
				pPlaneX = (hp - pPlanePosition).toVector().normalize()
				
			else
				pPlaneX = (pInteractOrigin - pPlanePosition).toVector().normalize()
			end
			
			pPlaneY = (pPlaneNormal % pPlaneX).normalize()
		end
		
		/** Update interacting. */
		func void updateInteract(Matrix viewMatrix, DVector rayOrigin, Vector rayDirection)
			if pInteractOrigin == null
				return
			end
			
			// calculate intersecton of view ray with interaction plane
			var float denom = rayDirection * pPlaneNormal
			if denom == 0
				return
			end
			
			var float lambda = ((pPlanePosition - rayOrigin).toVector() * pPlaneNormal) / denom
			var DVector hp = rayOrigin + DVector.new(rayDirection) * lambda
			
			// apply interaction
			var Vector v = (hp - pPlanePosition).toVector()
			if v.getLength() < 0.001
				return
			end
			
			var float angle = DEMath.atan2(pPlaneY * v, pPlaneX * v)
			
			if lambda < 0
				// lambda is negative if the ray points away from the plane. in this case the
				// hit point is at the opposite direction causing angle to be 180 degrees off.
				// it does not matter in what direction the 180 degrees are corrected since
				// the angle is normalized anyway
				angle -= 180
			end
			
			angle = DEMath.normalize(angle, -180.0, 180.0)
			
			getElement().getParent().setOrientation(pInteractOrientation * Quaternion.newFromAxis(pPlaneNormal, angle))
		end
		
		/** Stop interacting. */
		func void stopInteract()
			pInteractOrientation = null
			pInteractOrigin = null
			pInteractAxis = null
			pPlanePosition = null
			pPlaneNormal = null
		end
		
		/** Cancel interacting. */
		func void cancelInteract()
			stopInteract()
		end
	end
	
	
	
	var ECPVector pAxis
	
	
	
	/** Create behavior and add it to element class. */
	func new(BehaviorElementClass eclass) super(eclass)
		var String prefix = "interactRotate."
		
		pAxis = ECPVector.new(prefix + "axis", Vector.new(1, 0, 0))
		eclass.addProperty(pAxis)
		
		eclass.addBehavior(this)
	end
	
	
	
	/** Rotatement axis. */
	func ECPVector getAxis()
		return pAxis
	end
	
	
	
	/** Create behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
