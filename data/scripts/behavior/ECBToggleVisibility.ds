/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.Characters

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Behavior adding support to toggle visibility of element and to reposition it. Positions
 * element in front of HMD, hand point-at or custom origin matrix at fixed distance facing
 * the actor. Optionally the up axis can be locked.
 */
class ECBToggleVisibility extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		private var ECBToggleVisibility pECBehavior
		private var Vector pLockUpAxis
		private var Vector pPivot
		private var Vector pRotation
		private var float pDistance
		private var bool pLockZRotation
		
		
		
		/** Create behavior instance. */
		public func new(ECBToggleVisibility ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
		end
		
		/** Init behavior instance. */
		public func void init(StubElement stub)
			var CodecPropertyString codec = getElement().getClass().getCodecPropertyString()
			pLockUpAxis = pECBehavior.getLockUpAxis().getVector(stub, codec)
			pPivot = pECBehavior.getPivot().getVector(stub, codec)
			pRotation = pECBehavior.getRotation().getVector(stub, codec)
			pDistance = pECBehavior.getDistance().getValue(stub)
			pLockZRotation = pECBehavior.getLockZRotation().getValue(stub)
		end
		
		
		
		/** Lock up axis or null value. */
		public func Vector getLockUpAxis()
			return pLockUpAxis
		end
		
		/** Pivot offset or null value. */
		public func Vector getPivot()
			return pPivot
		end
		
		/** Rotation to add. */
		public func Vector getRotation()
			return pRotation
		end
		
		/** Distance in front of camera to position element. */
		public func float getDistance()
			return pDistance
		end
		
		/** Lock Z rotation. */
		public func bool getLockZRotation()
			return pLockZRotation
		end
		
		
		
		/** Element is visible. */
		public func bool getVisible()
			return getElement().getVisible()
		end
		
		/** Show/hide element. */
		public func void setVisible(bool visible)
			var BehaviorElement element = getElement()
			element.setVisible(visible)
			element.setTouchable(visible)
		end
		
		/** Reposition element in front of VR camera. */
		public func void repositionHmd(BaseVRActor actor)
			repositionHmd(actor, Vector.new())
		end
		
		/** Reposition element relative to VR camera. */
		public func void repositionHmd(BaseVRActor actor, Vector position)
			var Camera camera = GameApp.getGameApp().getVRCamera()
			var DMatrix matrix = DMatrix.newTranslation(DVector.new(position))
			var Quaternion corient = camera.getOrientation()
			var DVector cpos = camera.getPosition()
			
			if pPivot != null
				matrix = matrix * DMatrix.newWorld(actor.getPosition().\
					combine(cpos, false, true, false) + DVector.new(pPivot), corient)
				
			else
				matrix = matrix * DMatrix.newWorld(cpos, corient)
			end
			
			// make object face HMD. keep in mind reposition() expects the matrix to point
			// away from the camera so "mpos - cpos" is used not "cpos - mpos".
			var DVector mpos = matrix.getPosition()
			var DVector view = (mpos - cpos).normalize()
			var DVector up = DMatrix.newFromQuaternion(corient).getUpVector()
			
			if pLockUpAxis != null
				var DVector lockUp = DVector.new(pLockUpAxis)
				
				view = mpos - (cpos + lockUp * (lockUp * (mpos - cpos)))
				
				if view.isZero()
					if DEMath.fabs(lockUp.getY()) > DEMath.fabs(lockUp.getX())\
					and DEMath.fabs(lockUp.getY()) > DEMath.fabs(lockUp.getZ())
						view = DVector.new(0, 0, 1)
						
					else
						view = DVector.new(0, 1, 0)
					end 
					
				else
					view = view.normalize()
				end
				
				up = lockUp
			end
			
			matrix = DMatrix.newWorld(mpos, view, up)
			
			reposition(matrix)
		end
		
		/** Reposition element along point-at direction. */
		public func void reposition(ECBehaviorVRHandPointAt.Instance pointAt)
			reposition(pointAt.calcTestMatrix())
		end
		
		/** Reposition element. */
		public func void reposition(DMatrix matrix)
			if pLockZRotation
				matrix = DMatrix.newRT(matrix.getEulerAngles().compSelect(true, true, false), matrix.getPosition())
			end
			
			matrix = DMatrix.newRotationY(180) * matrix
			
			if pLockUpAxis != null
				var DVector lockUp = DVector.new(pLockUpAxis)
				var DVector view = matrix.getViewVector()
				
				if DEMath.fabs(view * lockUp) > 0.707 // 45 degrees
					view = matrix.getRightVector() % lockUp
				end
				
				matrix = DMatrix.newWorld(matrix.getPosition(), view, lockUp)
			end
			
			matrix = DMatrix.newRotation(DVector.new(pRotation))\
				* DMatrix.newTranslation(0, 0, -pDistance) * matrix
			
			getElement().setGeometry(matrix.getPosition(), matrix.toQuaternion())
		end
	end
	
	
	
	private var ECPVector pLockUpAxis
	private var ECPVector pPivot
	private var ECPVector pRotation
	private var ECPFloat pDistance
	private var ECPBoolean pLockZRotation
	
	
	
	/** Create behavior and add it to element class. */
	public func new(BehaviorElementClass eclass) super(eclass)
		var String prefix = "toggleVisibility."
		
		pLockUpAxis = ECPVector.new(prefix + "lockUpAxis", null)
		eclass.addProperty(pLockUpAxis)
		
		pPivot = ECPVector.new(prefix + "pivot", null)
		eclass.addProperty(pPivot)
		
		pRotation = ECPVector.new(prefix + "rotation", Vector.new())
		eclass.addProperty(pRotation)
		
		pDistance = ECPFloat.new(prefix + "distance", 0.5, 0)
		eclass.addProperty(pDistance)
		
		pLockZRotation = ECPBoolean.new(prefix + "lockZRotation", false)
		eclass.addProperty(pLockZRotation)
		
		eclass.addBehavior(this)
	end
	
	
	
	/** Lock up axis or null value. */
	public func ECPVector getLockUpAxis()
		return pLockUpAxis
	end
	
	/** Pivot offset or null value. */
	public func ECPVector getPivot()
		return pPivot
	end
	
	/** Rotation to add. */
	public func ECPVector getRotation()
		return pRotation
	end
	
	/** Distance in front of camera to position element. */
	public func ECPFloat getDistance()
		return pDistance
	end
	
	/** Lock Z rotation. */
	public func ECPBoolean getLockZRotation()
		return pLockZRotation
	end
	
	
	
	/** Get instance in element from owner element class. */
	public func Instance instance(BehaviorElement element)
		return element.getInstanceAt(this.getInstanceIndex()) cast Instance
	end
	
	/** Get instance with slot name in element or null if absent. */
	static public func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	
	
	/** Create behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
