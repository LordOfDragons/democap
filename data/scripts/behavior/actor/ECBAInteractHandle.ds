/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.Characters

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Actor behavior using interact handle.
 */
class ECBAInteractHandle extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		var ECBAInteractHandle pECBehavior
		var ECBShowInteractHandles.Instance pShowInteractHandles
		var ECBInteractHandle.Instance pInteractHandle
		var ECBehaviorVRHandPointAt.Instance pPointAt
		var bool pTriggerPulled
		
		
		
		/** Create behavior instance. */
		func new(ECBAInteractHandle ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
		end
		
		/** Dispose of behavior instance. */
		func void dispose()
			pPointAt = null
			pInteractHandle = null
			pShowInteractHandles = null
			super.dispose()
		end
		
		
		
		/** Behavior instance show interact handles or null. */
		func ECBShowInteractHandles.Instance getShowInteractHandles()
			return pShowInteractHandles
		end
		
		/** Behavior instance interact handle or null. */
		func ECBInteractHandle.Instance getInteractHandle()
			return pInteractHandle
		end
		
		/** Behavior instance VR Hand Point At or null. */
		func ECBehaviorVRHandPointAt.Instance getPointAt()
			return pPointAt
		end
		
		/** Trigger is pulled. */
		func bool getTriggerPulled()
			return pTriggerPulled
		end
		
		
		
		/** Process pointing at element. Returns true if processed. */
		func bool pointAt(ECBehaviorVRHandPointAt.Instance behavior)
			var BehaviorElement element = behavior.getPointAtBehaviorElement()
			
			if pInteractHandle != null
				if behavior != pPointAt
					return false
				end
				
				if pTriggerPulled
					if pInteractHandle != null and pPointAt != null
						pInteractHandle.updateInteract(pPointAt)
					end
					return true
					
				elif element != null and not element.isDisposed()
					return true
					
				else
					pInteractHandle = null
					pShowInteractHandles = null
					pPointAt = null
					return false
				end
				
			else
				if element != null and not element.isDisposed()
					pInteractHandle = ECBInteractHandle.getInstanceIn(element)
				end
				
				if pInteractHandle != null
					pShowInteractHandles = ECBShowInteractHandles.getInstanceIn(element.getParent() cast BehaviorElement)
					pPointAt = behavior
					return true
					
				else
					pPointAt = null
					return false
				end
			end
		end
		
		/** Pull trigger on pointed at element. Returns true if processed. */
		func bool pointAtPull(ECBehaviorVRHandPointAt.Instance behavior)
			if not pointAt(behavior)
				return false
			end
			
			pInteractHandle.startInteract(pPointAt)
			if pShowInteractHandles != null
				pShowInteractHandles.notifyStartInteract(pInteractHandle)
			end
			
			pTriggerPulled = true
			return true
		end
		
		/** Release trigger on pointed at element. Returns true if processed. */
		func bool pointAtRelease(ECBehaviorVRHandPointAt.Instance behavior)
			if pInteractHandle == null or behavior != pPointAt
				return false
			end
			
			pInteractHandle.stopInteract()
			if pShowInteractHandles != null
				pShowInteractHandles.notifyStopInteract(pInteractHandle)
			end
			
			pInteractHandle = null
			pShowInteractHandles = null
			pPointAt = null
			pTriggerPulled = false
			return true
		end
		
		/** Cancel triggering pointAtRelease if point at is present. */
		func void cancel()
			if pInteractHandle == null
				return
			end
			
			pInteractHandle.cancelInteract()
			if pShowInteractHandles != null
				pShowInteractHandles.notifyStopInteract(pInteractHandle)
			end
			
			pInteractHandle = null
			pShowInteractHandles = null
			pPointAt = null
			pTriggerPulled = false
		end
	end
	
	
	
	/** Create behavior and add it to element class. */
	func new(BehaviorElementClass eclass) super(eclass)
		eclass.addBehavior(this)
	end
	
	
	
	/** Get instance in element from owner element class. */
	func Instance instance( BehaviorElement element )
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	/** Get instance in element or null if absent. */
	static func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/** Create behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
