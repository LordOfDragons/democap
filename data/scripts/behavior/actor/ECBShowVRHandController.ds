/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2022, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.Characters

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Behavior showing ECBehaviorVRHand model if user wants to.
 */
class ECBShowVRHandController extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		class ShowController extends DefaultToggleModelListener
			protected var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void toggledChanged(ToggleModel model)
				pInstance.updateVRHandVisible()
			end
		end
		
		
		var ECBShowVRHandController pECBehavior
		var ECBehaviorVRHand.Instance pVRHand
		var ShowController pShowController
		
		
		/** Create behavior instance. */
		func new(ECBShowVRHandController ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pVRHand = ecbehavior.getVRHand().instance(element)
			setRequiresThink(true)
		end
		
		/** Dispose of behavior instance. */
		func void dispose()
			pVRHand = null
			super.dispose()
		end
		
		
		/** Update VR hand model visibility. */
		func void updateVRHandVisible()
			var bool show = SessionSettings.get().getModelShowVRControllers().getToggled()
			var GameApp app = GameApp.getGameApp()
			var bool playbackStopped = app.getPlaybackScene().isStopped()
			var bool recording = app.getRecordScene().isRecording()
			var CharacterConfiguration config = app.getWorldSpawnCharacter().getCharacter()
			
			pVRHand.setShowHand(show or config == null or not config.isCalibrated()\
				or (not playbackStopped and not recording))
		end
		
		
		func void addToGameWorld()
			pShowController = ShowController.new(this)
			SessionSettings.get().getModelShowVRControllers().addListener(pShowController)
			updateVRHandVisible()
		end
		
		func void removeFromGameWorld()
			if pShowController != null
				SessionSettings.get().getModelShowVRControllers().removeListener(pShowController)
				pShowController = null
			end
		end
		
		func void think(float elapsed)
			updateVRHandVisible()
		end
	end
	
	
	protected var ECBehaviorVRHand pVRHand
	
	
	/** Create behavior and add it to element class. */
	func new(BehaviorElementClass eclass, ECBehaviorVRHand vrHand, String id) super(eclass, id)
		if vrHand == null
			throw ENullPointer.new("vrHand")
		end
		
		pVRHand = vrHand
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	func void dispose()
		pVRHand = null
		super.dispose()
	end
	
	
	func ECBehaviorVRHand getVRHand()
		return pVRHand
	end
	
	
	
	/** Get instance in element from owner element class. */
	func Instance instance( BehaviorElement element )
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	/** Create behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
