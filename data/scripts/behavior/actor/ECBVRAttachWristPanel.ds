/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2022, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.Gui

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Behavior adding support to attach wrist panel. Adds an attach slot and attaches matching
 * wrist menu upon being added to game world.
 */
class ECBVRAttachWristPanel extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		var ECBVRAttachWristPanel pECBehavior
		var ECBehaviorVRHandLaserPointer.Instance pVRHandLaserPointer
		var ECBehaviorAttachSlot.Instance pAttachSlot
		
		
		/** Create instance. */
		func new(ECBVRAttachWristPanel ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pVRHandLaserPointer = ecbehavior.getVRHandLaserPointer().instance(element)
			pAttachSlot = ecbehavior.getAttachSlot().instance(element)
		end
		
		/** Dispose of behavior instance. */
		func void dispose()
			pVRHandLaserPointer = null
			pAttachSlot = null
			super.dispose()
		end
		
		
		/** Behavior. */
		func ECBVRAttachWristPanel getECBehavior()
			return pECBehavior
		end
		
		
		/** Add to game world. */
		func void addToGameWorld()
			var ECBehaviorAttachable.Instance attachable =\
				ECBehaviorAttachable.getInstanceIn(pECBehavior.getRightHand()\
					if GameApp.getGameApp().getWorldSpawnCharacter().getWristMenuRight() \
					else GameApp.getGameApp().getWorldSpawnCharacter().getWristMenuLeft())
			attachable.reattach(pAttachSlot, Vector.new(), Quaternion.new())
			
			var Collider collider = attachable.getCollider().getCollider()
			pVRHandLaserPointer.getVRHandPointAt().getVRHand().getCollider().addIgnoreCollider(collider)
			pVRHandLaserPointer.getParticleEmitter().getParticleEmitterInstance().addIgnoreCollider(collider)
		end
		
		/** Remove from game world. */
		func void removeFromGameWorld()
			var ECBehaviorAttachable.Instance attachable = pAttachSlot.getAttachable()
			if attachable == null
				return
			end
			
			pAttachSlot.detach()
			
			var Collider collider = attachable.getCollider().getCollider()
			pVRHandLaserPointer.getVRHandPointAt().getVRHand().getCollider().removeIgnoreCollider(collider)
			pVRHandLaserPointer.getParticleEmitter().getParticleEmitterInstance().removeIgnoreCollider(collider)
		end
	end
	
	
	var ECBehaviorVRHandLaserPointer pVRHandLaserPointer
	var ECBehaviorAttachSlot pAttachSlot
	var bool pRightHand
	
	
	/** Create behavior element class. */
	func new(BehaviorElementClass eclass, ECBehaviorVRHandLaserPointer vrhandLaserPointer, bool rightHand) super(eclass)
		if vrhandLaserPointer == null
			throw ENullPointer.new("vrhandLaserPointer")
		end
		
		pVRHandLaserPointer = vrhandLaserPointer
		pRightHand = rightHand
		
		var ECBehaviorVRHand vrhand = vrhandLaserPointer.getVRHandPointAt().getVRHand()
		var Matrix matrix
		
		if rightHand
			matrix = Matrix.newRT(Vector.new(-20, -60, 0), Vector.new(0.06, 0.04, -0.05))
			
			pAttachSlot = ECBehaviorAttachSlot.new(eclass, null, vrhand,\
				BaseVRActorClass.idVRRightHand,\
				"attachSlot(" + BaseVRActorClass.idVRRightHand + ")")
			
		else
			matrix = Matrix.newRT(Vector.new(-20, 60, 0), Vector.new(-0.06, 0.04, -0.05))
			
			pAttachSlot = ECBehaviorAttachSlot.new(eclass, null, vrhand,\
				BaseVRActorClass.idVRLeftHand,\
				"attachSlot(" + BaseVRActorClass.idVRLeftHand + ")")
		end
		
		pAttachSlot.getPosition().setVector(matrix.getPosition())
		pAttachSlot.getOrientation().setVector(matrix.getEulerAngles())
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	func void dispose()
		pVRHandLaserPointer = null
		pAttachSlot = null
		super.dispose()
	end
	
	
	/** VR Hand Laser Pointer behavior. */
	func ECBehaviorVRHandLaserPointer getVRHandLaserPointer()
		return pVRHandLaserPointer
	end
	
	/** Attach slot behavior. */
	func ECBehaviorAttachSlot getAttachSlot()
		return pAttachSlot
	end
	
	/** Right hand. */
	func bool getRightHand()
		return pRightHand
	end
	
	
	/** Get instance in element from owner element class. */
	func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	/** Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
