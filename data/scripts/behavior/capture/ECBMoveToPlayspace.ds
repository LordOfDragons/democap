/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Behavior moving from calibrate space to playspace.
 */
class ECBMoveToPlayspace extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** Apply animator. */
		class ApplyAnimator extends ECBMoCapAnimator.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void animationApplied(ECBMoCapAnimator.Instance instance)
				pInstance.applyAnimator()
			end
		end
		
		
		
		protected var ECBMoveToPlayspace pECBehavior
		protected var ECBMoCapAnimator.Instance pMoCapAnimator
		var Component pComponent
		var AnimatorInstance pAnimatorInstance
		
		
		
		/** Create behavior instance. */
		func new(ECBMoveToPlayspace ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pMoCapAnimator = ecbehavior.getMoCapAnimator().instance(element)
		end
		
		/** Dispose of instance. */
		func void dispose()
			pMoCapAnimator = null
			super.dispose()
		end
		
		/** Init behavior instance. */
		func void init(StubElement stub)
			pComponent = pMoCapAnimator.getActorAnimated().getComponent().getComponent()
			
			var Animator animator = pECBehavior.getAnimator()
			if animator == null
				return
			end
			
			animator.setRig(pComponent.getRig())
			
			pAnimatorInstance = AnimatorInstance.new()
			pAnimatorInstance.setAnimator(animator)
			pAnimatorInstance.setComponent(pComponent)
			
			pMoCapAnimator.addListener(ApplyAnimator.new(this))
		end
		
		
		
		/** Apply animator. Called after ECBMoCapAnimator has been applied. */
		func void applyAnimator()
			if pAnimatorInstance != null
				pAnimatorInstance.apply()
			end
		end
	end
	
	
	
	protected var ECBMoCapAnimator pMoCapAnimator
	protected var Animator pAnimator
	
	
	
	/** Create behavior and add it to element class. */
	func new(BaseVRActorClass eclass, Animator animator) super(eclass)
		pMoCapAnimator = ECBMoCapAnimator.getBehaviorIn(eclass)
		pAnimator = animator
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	func void dispose()
		pMoCapAnimator = null
		super.dispose()
	end
	
	
	
	/** Motion capture animator behavior. */
	func ECBMoCapAnimator getMoCapAnimator()
		return pMoCapAnimator
	end
	
	/** Animator. */
	func Animator getAnimator()
		return pAnimator
	end
	
	
	
	/** Get instance in element from owner element class. */
	func Instance instance( BehaviorElement element )
		return element.getInstanceAt( this.getInstanceIndex() ) cast Instance
	end
	
	
	
	/** Create behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
