/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.Characters

pin Dragengine.Scenery


/**
 * Behavior hide component while playing back if not recording.
 */
class ECBHideWhilePlaybackNotRecording extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		class RecordingStateChanged extends ECBRecordAnimation.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void prepareRecording(ECBRecordAnimation.Instance instance)
				pInstance.updateVisibility()
			end
			
			func void stopRecording(ECBRecordAnimation.Instance instance)
				pInstance.updateVisibility()
			end
		end
		
		class PlaybackChanged extends PlaybackScene.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void startPlayback(PlaybackScene playbackScene)
				pInstance.updateVisibility()
			end
			
			func void stopPlayback(PlaybackScene playbackScene)
				pInstance.updateVisibility()
			end
			
			func void pausePlayback(PlaybackScene playbackScene)
				pInstance.updateVisibility()
			end
		end
		
		
		
		var ECBHideWhilePlaybackNotRecording pECBehavior
		var ECBRecordAnimation.Instance pRecordAnimation
		var PlaybackChanged pPlaybackChanged
		var Boolean pRestoreVisibility
		
		
		
		/** Create behavior instance. */
		func new(ECBHideWhilePlaybackNotRecording ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			
			pRecordAnimation = ecbehavior.getRecordAnimation().instance(element)
			pRecordAnimation.addListener(RecordingStateChanged.new(this))
		end
		
		/** Dispose of behavior. */
		func void dispose()
			pRecordAnimation = null
			super.dispose()
		end
		
		
		
		/** Update visibility. */
		func void updateVisibility()
			if pRecordAnimation.isPreparingOrRecording() or GameApp.getGameApp().getPlaybackScene().isStopped()
				show()
				
			else
				hide()
			end
		end
		
		/** Hide element. */
		func void hide()
			if pRestoreVisibility != null
				return
			end
			
			var Component component = pRecordAnimation.getComponent().getComponent()
			pRestoreVisibility = Boolean.new(component.getVisible())
			component.setVisible(false)
		end
		
		/** Show element. */
		func void show()
			if pRestoreVisibility == null
				return
			end
			
			pRecordAnimation.getComponent().getComponent().setVisible(pRestoreVisibility.value())
			pRestoreVisibility = null
		end
		
		
		
		/** Added to game world. */
		func void addToGameWorld()
			pPlaybackChanged = PlaybackChanged.new(this)
			GameApp.getGameApp().getPlaybackScene().addListener(pPlaybackChanged)
		end
		
		/** Remove from game world. */
		func void removeFromGameWorld()
			if pPlaybackChanged != null
				GameApp.getGameApp().getPlaybackScene().removeListener(pPlaybackChanged)
				pPlaybackChanged = null
			end
		end
	end
	
	
	
	var ECBRecordAnimation pRecordAnimation
	
	
	
	/** Create behavior and add it to element class. */
	func new(BehaviorElementClass eclass, ECBRecordAnimation recordAnimation) super(eclass)
		pRecordAnimation = recordAnimation
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	func void dispose()
		pRecordAnimation = null
		super.dispose()
	end
	
	
	
	func ECBRecordAnimation getRecordAnimation()
		return pRecordAnimation
	end
	
	
	
	/** Get instance in element from owner element class. */
	func Instance instance( BehaviorElement element )
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	/** Create behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
