/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2023, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.MotionTransferSystem

pin Dragengine.Commands
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Behavior for MotionTransferFace.
 */
class ECBMotionTransferFace extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** Controller attach/detach listener. */
		class AttachDetachListener extends DefaultBindingManagerListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void deviceAttached(BindingManager manager, InputDevice device)
				if pInstance.getInputDevice() == null and device.getType() == InputDeviceType.vrHMD
					pInstance.setInputDevice(device)
				end
			end
			
			func void deviceDetached(BindingManager manager, InputDevice device)
				if pInstance.getInputDevice() == device
					pInstance.setInputDevice(null)
				end
			end
			
			func void devicesReloaded(BindingManager manager)
				pInstance.findInputDevice()
			end
		end
		
		/** Update controllers. */
		class UpdateControllers extends ECBMoCapAnimator.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void updateControllers(ECBMoCapAnimator.Instance instance, float elapsed, int phase)
				pInstance.updateControllers()
			end
		end
		
		/** Tracked expression. */
		class TrackExpression
			var int pExpression
			var AnimatorController pController
			
			func new(int expression, AnimatorController controller)
				pExpression = expression
				pController = controller
			end
			
			func void update(InputDevice device)
				pController.setValue(device.getDeviceFaceExpressionAt(pExpression))
			end
		end
		
		
		
		var ECBMotionTransferFace pECBehavior
		var ECBehaviorVRPlayspace.Instance pVRPlayspace
		var ECBMoCapAnimator.Instance pMoCapAnimator
		var Array pTrackedExpressions
		var InputDevice pInputDevice
		var AttachDetachListener pAttachDetachListener
		
		
		
		/** Create instance. */
		func new(ECBMotionTransferFace ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			
			pTrackedExpressions = Array.new()
			pAttachDetachListener = AttachDetachListener.new(this)
			
			pVRPlayspace = ecbehavior.getVRPlayspace().instance(element)
			pMoCapAnimator = ecbehavior.getMoCapAnimator().instance(element)
		end
		
		/** Dispose of instance. */
		func void dispose()
			pAttachDetachListener = null
			pVRPlayspace = null
			pMoCapAnimator = null
			super.dispose()
		end
		
		
		
		/** Init behavior instance. */
		func void init(StubElement stub)
			var ECBehaviorActorAnimated.Instance actorAnimated = pMoCapAnimator.getActorAnimated()
			var String baseControllerName = pECBehavior.getBaseControllerName()
			var MotionTransferFace transfer = pECBehavior.getTransfer()
			
			MotionTransferFace.expressionInfos.forEach(block MotionTransferFace.ExpressionInfo each
				if not transfer.getExpressionVPSAt(each.getExpression()).empty()
					pTrackedExpressions.add(TrackExpression.new(each.getExpression(),\
						actorAnimated.getControllerNamed(baseControllerName + each.getId())))
				end
			end)
			
			// add listener
			pMoCapAnimator.addListener(UpdateControllers.new(this))
		end
		
		
		/** Behavior. */
		func ECBMotionTransferFace getECBehavior()
			return pECBehavior
		end
		
		/** VR Playspace. */
		func ECBehaviorVRPlayspace.Instance getVRPlayspace()
			return pVRPlayspace
		end
		
		/** Motion capture animator. */
		func ECBMoCapAnimator.Instance getMoCapAnimator()
			return pMoCapAnimator
		end
		
		/** Input device or null if detached. */
		func InputDevice getInputDevice()
			return pInputDevice
		end
		
		/** Set input device or null if detached. */
		func void setInputDevice(InputDevice device)
			pInputDevice = device
		end
		
		
		
		/** Element has been added to game world. Start tracking device. */
		func void addToGameWorld()
			GameApp.getGameApp().getBindingManager().addListener(pAttachDetachListener)
			findInputDevice()
		end
		
		/** Element has been removed from game world. Stop tracking device. */
		func void removeFromGameWorld()
			GameApp.getGameApp().getBindingManager().removeListener(pAttachDetachListener)
			setInputDevice(null)
		end
		
		
		
		/** Update controllers. */
		func void updateControllers()
			if pInputDevice == null
				return
			end
			
			pTrackedExpressions.forEach(block TrackExpression each
				each.update(pInputDevice)
			end)
		end
		
		/** Find input device. */
		func void findInputDevice()
			setInputDevice(GameApp.getApp().getBindingManager().findDevice(block InputDevice each
				return each.getType() == InputDeviceType.vrHMD
			end))
		end
	end
	
	
	
	var MotionTransferFace pTransfer
	var ECBehaviorVRPlayspace pVRPlayspace
	var ECBMoCapAnimator pMoCapAnimator
	var String pBaseControllerName
	
	
	
	/** Create behavior element class. */
	func new(BaseVRActorClass eclass, MotionTransferFace transfer) super(eclass, transfer.getName())
		if transfer == null
			throw ENullPointer.new("transfer")
		end
		
		pTransfer = transfer
		pVRPlayspace = eclass.getVRPlayspace()
		pMoCapAnimator = ECBMoCapAnimator.getBehaviorIn(eclass)
		
		pBaseControllerName = transfer.getName() + ".expr."
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	func void dispose()
		pVRPlayspace = null
		pTransfer = null
		super.dispose()
	end
	
	
	
	/** Motion transfer. */
	func MotionTransferFace getTransfer()
		return pTransfer
	end
	
	/** VR Playspace behavior. */
	func ECBehaviorVRPlayspace getVRPlayspace()
		return pVRPlayspace
	end
	
	/** Motion capture animator behavior. */
	func ECBMoCapAnimator getMoCapAnimator()
		return pMoCapAnimator
	end
	
	/** Base animator controller name. */
	func String getBaseControllerName()
		return pBaseControllerName
	end
	
	
	
	/** Get instance in element from owner element class. */
	func Instance instance( BehaviorElement element )
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/** Get behavior with slot name in element class or null if absent. */
	static func ECBMotionTransferFace getBehaviorIn(BehaviorElementClass eclass, String slotName)
		return eclass.findBehavior(block ECBMotionTransferFace each
			return each.getTransfer().getName().equals(slotName)
		end) cast ECBMotionTransferFace
	end
	
	/** Get list of all behaviors in element. */
	static func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBMotionTransferFace each
			return true
		end)
	end
	
	/** Get instance with slot name in element or null if absent. */
	static func Instance getInstanceIn(BehaviorElement element, String slotName)
		return element.findInstance(block Instance each
			return each.getECBehavior().getTransfer().getName().equals(slotName)
		end) cast Instance
	end
	
	/** Get list of all instances in element. */
	static func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
