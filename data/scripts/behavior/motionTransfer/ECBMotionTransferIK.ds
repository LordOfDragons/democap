/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Behaviors

pin Democap.MotionTransferSystem

pin Dragengine.Commands
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Behavior for MotionTransferIK.
 */
class ECBMotionTransferIK extends DefaultECBehavior
	/** Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** Update controllers. */
		class UpdateControllers extends ECBMoCapAnimator.DefaultListener
			private var Instance pInstance
			
			public func new(Instance instance)
				pInstance = instance
			end
			
			public func int requiredPhaseCount(ECBMoCapAnimator.Instance instance)
				return pInstance.requiredPhaseCount()
			end
			
			public func void updateControllers(ECBMoCapAnimator.Instance instance, float elapsed, int phase)
				select phase
				case 0
					pInstance.updateControllersPhase1()
					
				case 1
					pInstance.updateControllersPhase2()
					
				case 2
					pInstance.updateControllersPhase3()
				end
			end
		end
		
		
		
		private var ECBMotionTransferIK pECBehavior
		private var ECBehaviorVRPlayspace.Instance pVRPlayspace
		private var ECBMoCapAnimator.Instance pMoCapAnimator
		private var ECBTrackerSlot.Instance pSlotTip
		private var ECBTrackerSlot.Instance pSlotGuide
		private var AnimatorController pControllerTipPosition
		private var AnimatorController pControllerTipOrientation
		private var AnimatorController pControllerGuideRotation
		private var AnimatorController pControllerTwist
		private var AnimatorController pControllerGuideTwistAxis
		private var AnimatorController pControllerRetracted
		private var AnimatorController pControllerBlend
		private var Matrix pTransformTip
		private var Quaternion pTransformTipQuat
		private var Matrix pTransformGuide
		private var Vector pTipOffset
		private var int pBaseBone
		private var int pGuideBone
		private var int pTipBone
		private var AnimatorInstance pAnimatorInstance
		private var Component pComponent
		private var int pTwistBone
		private var Vector pScaleReach
		private var Vector pScaleReachBack
		private var Matrix pReachMatrix
		private var Quaternion pTwistQuat, pTwistQuatInv
		private var bool pEnableScaleReach
		private var bool pEnableTipOffset
		private var bool pEnableTwist
		private var Matrix pMatrixAdjustSlotComponent
		
		
		
		/** Create instance. */
		public func new(ECBMotionTransferIK ecbehavior, BehaviorElement element) super(element)
			var MotionTransferIK transfer = ecbehavior.getTransfer()
			pECBehavior = ecbehavior
			
			pVRPlayspace = ecbehavior.getVRPlayspace().instance(element)
			pMoCapAnimator = ecbehavior.getMoCapAnimator().instance(element)
			
			pTipOffset = transfer.getCalibrateTipOffset()
			pScaleReach = transfer.getCalibrateReachScaleDirection() * transfer.getCalibrateReachScale()
			pScaleReachBack = transfer.getCalibrateReachScaleDirectionBack() * transfer.getCalibrateReachScale()
			pReachMatrix = Matrix.newRotation(transfer.getCalibrateReachRotation())
			
			pTwistQuat = Quaternion.newFromEuler(transfer.getCalibrateTwistBoneAxisRotation())
			pTwistQuatInv = pTwistQuat.conjugate()
			
			if ecbehavior.getSlotTip() != null
				pSlotTip = ecbehavior.getSlotTip().instance(element)
				pTransformTip = transfer.getTipTracker().getTransformMatrix()
				pTransformTipQuat = pTransformTip.normalize().toQuaternion()
			end
			
			if ecbehavior.getSlotGuide() != null
				pSlotGuide = ecbehavior.getSlotGuide().instance(element)
				pTransformGuide = transfer.getGuideTracker().getTransformMatrix()
			end
		end
		
		/** Dispose of instance. */
		public func void dispose()
			pSlotGuide = null
			pSlotTip = null
			pVRPlayspace = null
			pMoCapAnimator = null
			super.dispose()
		end
		
		
		
		/** Init behavior instance. */
		public func void init(StubElement stub)
			var ECBehaviorActorAnimated.Instance actorAnimated = pMoCapAnimator.getActorAnimated()
			
			pComponent = actorAnimated.getComponent().getComponent()
			
			pControllerTipPosition = actorAnimated.getControllerNamed(pECBehavior.getControllerNameTipPosition())
			pControllerTipOrientation = actorAnimated.getControllerNamed(pECBehavior.getControllerNameTipOrientation())
			pControllerGuideRotation = actorAnimated.getControllerNamed(pECBehavior.getControllerNameGuideRotation())
			pControllerRetracted = actorAnimated.getControllerNamed(pECBehavior.getControllerNameRetracted())
			pControllerTwist = actorAnimated.getControllerNamed(pECBehavior.getControllerNameTwist())
			pControllerGuideTwistAxis = actorAnimated.getControllerNamed(pECBehavior.getControllerNameGuideTwistAxis())
			pControllerBlend = actorAnimated.getControllerNamed(pECBehavior.getControllerNameBlend())
			
			pTwistBone = -1
			if not pECBehavior.getTransfer().getTipBone().empty()
				pTwistBone = pComponent.indexOfBoneNamed(pECBehavior.getTransfer().getTipBone())
			end
			
			pEnableTwist = pSlotTip != null and pControllerTwist != null and pTwistBone != -1
			
			pBaseBone = pComponent.indexOfBoneNamed(pECBehavior.getTransfer().getBaseBone())
			pGuideBone = pComponent.indexOfBoneNamed(pECBehavior.getTransfer().getGuideBone())
			pTipBone = pComponent.indexOfBoneNamed(pECBehavior.getTransfer().getTipBone())
			
			pEnableScaleReach = pSlotTip != null and pBaseBone != -1 \
				and pControllerTipPosition != null and pControllerBlend != null
			
			pEnableTipOffset = pEnableScaleReach and not pTipOffset.isZero()
			
			pMoCapAnimator.addListener(UpdateControllers.new(this))
		end
		
		
		/** Behavior. */
		public func ECBMotionTransferIK getECBehavior()
			return pECBehavior
		end
		
		/** VR Playspace. */
		public func ECBehaviorVRPlayspace.Instance getVRPlayspace()
			return pVRPlayspace
		end
		
		/** Motion capture animator behavior. */
		public func ECBMoCapAnimator.Instance getMoCapAnimator()
			return pMoCapAnimator
		end
		
		/** Tip slot behavior instance or null. */
		public func ECBTrackerSlot.Instance getSlotTip()
			return pSlotTip
		end
		
		/** Guide slot behavior instance or null. */
		public func ECBTrackerSlot.Instance getSlotGuide()
			return pSlotGuide
		end
		
		
		
		/** Internal use. */
		public func int requiredPhaseCount()
			var int count = 1
			
			if pEnableScaleReach
				count++
			end
			
			if pEnableTwist
				count++
			end
			
			return count
		end
		
		/** Update controllers. */
		public func void updateControllersPhase1()
			// support tweaking
			var MotionTransferIK transfer = pECBehavior.getTransfer()
			pScaleReach = transfer.getCalibrateReachScaleDirection() * transfer.getCalibrateReachScale()
			pScaleReachBack = transfer.getCalibrateReachScaleDirectionBack() * transfer.getCalibrateReachScale()
			
			if pControllerBlend != null
				pControllerBlend.setValue(1)
			end
			if pControllerTwist != null
				pControllerTwist.setValue(0)
			end
			
			if pEnableScaleReach
				pControllerBlend.setValue(0)
				
			else
				updateControllerIK()
				updateControllerGuide()
			end
		end
		
		public func void updateControllersPhase2()
			if pControllerBlend != null
				pControllerBlend.setValue(1)
			end
			
			if pEnableScaleReach
				updateControllerIK()
				updateControllerGuide()
				
			elif pEnableTwist
				updateControllersTwist()
			end
		end
		
		public func void updateControllersPhase3()
			if pEnableScaleReach and pEnableTwist
				updateControllersTwist()
			end
		end
		
		protected func void updateControllerIK()
			if pControllerTipPosition == null or pControllerTipOrientation == null
				return
			end
			
			var Matrix matrix = pTransformTip * pSlotTip.getMatrixCharacter()
			
			// position, retracted length
			var Vector targetPosition = matrix.getPosition()
			var float retractedLength = 1
			
			if pEnableScaleReach
				var Matrix reachMatrix = pReachMatrix * pComponent.boneGetMatrix(pBaseBone)
				
				var Vector targetInReach = reachMatrix.getInverse() * targetPosition
				
				if pEnableTipOffset
					targetInReach = targetInReach + pTipOffset
				end
				
				targetInReach = targetInReach.compMultiply(pScaleReach).combine(\
					targetInReach.compMultiply(pScaleReachBack),\
					targetInReach.getX() > 0,\
					targetInReach.getY() > 0,\
					targetInReach.getZ() > 0)
				
				targetPosition = reachMatrix * targetInReach
				
				retractedLength = targetInReach.getLength()
			end
			
			// orientation
			var Vector targetRotation = matrix.normalize().getEulerAngles()
			
			// update controllers
			if pControllerTipPosition != null
				pControllerTipPosition.setVector(targetPosition)
			end
			if pControllerTipOrientation != null
				pControllerTipOrientation.setRotation(targetRotation)
			end
			if pControllerRetracted != null
				pControllerRetracted.setValue(retractedLength)
			end
			
			// adjust slot component if required
			adjustSlotComponentPosition(targetPosition, targetRotation)
		end
		
		protected func void updateControllerGuide()
			if pSlotGuide == null
				return
			end
			
			if pControllerGuideRotation != null
				// NOTE actually this is not fully correct. the rotation is affected by
				//      reach scaling if the direction scaling is not uniform. doing this
				//      would require to calculate the rotation matrix then apply a scale
				//      matrix using the direction scaling. the view and up vector can
				//      be obtained and normalized. this can be used to calculate a new
				//      coordinate system which is then the new rotation. the difference
				//      is neglectable for reach scalings close to 1 but become noticable
				//      for large reach scaling differences
				pControllerGuideRotation.setRotation((pTransformGuide\
					* pSlotGuide.getMatrixCharacter()).normalize().getEulerAngles())
			end
		end
		
		protected func void updateControllersTwist()
			//pControllerTwist.setValue(pComponent.boneGetRotation(pTwistBone).getEulerAngles().getZ())
			
			var Quaternion orientation = pTwistQuat * pComponent.boneGetRotation(pTwistBone) * pTwistQuatInv
//			pControllerTwist.setValue(orientation.getEulerAngles().getZ())
			
			var float factor = 0.5
			var float rotation = (orientation.slerp(Quaternion.new(), factor)).getEulerAngles().getZ() / factor
			
			pControllerTwist.setValue(rotation)
			
//			GameApp.getGameApp().getConsole().addMessage("Phase2 " + pECBehavior.getTransfer().getName() \
//				+ " " + pControllerTwist.getValue() + " " + pComponent.boneGetName(pTwistBone))
			
			if pControllerGuideTwistAxis != null and pTipBone != -1 and pGuideBone != -1
				var Vector axis = pComponent.boneGetMatrix(pTipBone).getPosition()\
					- pComponent.boneGetMatrix(pGuideBone).getPosition()
				if not axis.isZero()
					var Vector view = axis.normalize()
					var Vector up
					if DEMath.fabs(view.getY()) > DEMath.max(DEMath.fabs(view.getX()), DEMath.fabs(view.getZ()))
						up = Vector.new(0, 0, 1)
					else
						up = Vector.new(0, 1, 0)
					end
					pControllerGuideTwistAxis.setRotation(Matrix.newVU(view, up).getEulerAngles())
				end
			end
		end
		
		protected func void adjustSlotComponentPosition(Vector position, Vector rotation)
			if pMatrixAdjustSlotComponent != null
				pSlotTip.setComponentFromMatrixCharacter(\
					pMatrixAdjustSlotComponent * Matrix.newRT(rotation, position))
			end
		end
		
		
		
		/** Element added to game world. */
		public func void addToGameWorld()
			// slot input device is valid only once the tracker slot processed addToGameWorld
			if pSlotTip != null\
			and pSlotTip.getInputDevice() != null\
			and pSlotTip.getComponent() != null\
			and (pSlotTip.getInputDevice().getType() == InputDeviceType.vrRightHand\
			or pSlotTip.getInputDevice().getType() == InputDeviceType.vrLeftHand)
				pMatrixAdjustSlotComponent = pECBehavior.getTransfer().getTipTracker().getReverseTransformMatrix()
			end
		end
	end
	
	
	
	private var MotionTransferIK pTransfer
	private var ECBehaviorVRPlayspace pVRPlayspace
	private var ECBMoCapAnimator pMoCapAnimator
	private var ECBTrackerSlot pSlotTip
	private var ECBTrackerSlot pSlotGuide
	private var String pControllerNameTipPosition
	private var String pControllerNameTipOrientation
	private var String pControllerNameGuideRotation
	private var String pControllerNameTwist
	private var String pControllerNameGuideTwistAxis
	private var String pControllerNameRetracted
	private var String pControllerNameBlend
	
	
	
	/** Create behavior element class. */
	public func new(BaseVRActorClass eclass, MotionTransferIK transfer) \
	super(eclass, transfer.getName())
		if transfer == null
			throw ENullPointer.new("transfer")
		end
		
		var String subId = "motionTransferIK(" + transfer.getName() + ")"
		var String prefix = subId + "."
		
		pTransfer = transfer
		pVRPlayspace = eclass.getVRPlayspace()
		pMoCapAnimator = ECBMoCapAnimator.getBehaviorIn(eclass)
		if transfer.getTipTracker() != null
			pSlotTip = ECBTrackerSlot.getBehaviorIn(eclass, transfer.getTipTracker().getSlot())
		end
		if transfer.getGuideTracker() != null
			pSlotGuide = ECBTrackerSlot.getBehaviorIn(eclass, transfer.getGuideTracker().getSlot())
		end
		
		pControllerNameTipPosition = transfer.getName() + ".targetPosition"
		pControllerNameTipOrientation = transfer.getName() + ".targetOrientation"
		pControllerNameGuideRotation = transfer.getName() + ".guideRotation"
		pControllerNameTwist = transfer.getName() + ".twist"
		pControllerNameGuideTwistAxis = transfer.getName() + ".guideTwistAxis"
		pControllerNameRetracted = transfer.getName() + ".retracted"
		pControllerNameBlend = transfer.getName() + ".blend"
		
		eclass.addBehavior(this)
	end
	
	/** Dispose of behavior. */
	public func void dispose()
		pVRPlayspace = null
		pMoCapAnimator = null
		pTransfer = null
		pSlotGuide = null
		pSlotTip = null
		super.dispose()
	end
	
	
	
	/** Motion transfer. */
	public func MotionTransferIK getTransfer()
		return pTransfer
	end
	
	/** VR Playspace behavior. */
	public func ECBehaviorVRPlayspace getVRPlayspace()
		return pVRPlayspace
	end
	
	/** Motion capture animator behavior. */
	public func ECBMoCapAnimator getMoCapAnimator()
		return pMoCapAnimator
	end
	
	/** Tip slot behavior or null. */
	public func ECBTrackerSlot getSlotTip()
		return pSlotTip
	end
	
	/** Guide slot behavior or null. */
	public func ECBTrackerSlot getSlotGuide()
		return pSlotGuide
	end
	
	/** Animator controller name target position. */
	public func String getControllerNameTipPosition()
		return pControllerNameTipPosition
	end
	
	/** Animator controller name target orientation. */
	public func String getControllerNameTipOrientation()
		return pControllerNameTipOrientation
	end
	
	/** Animator controller name guide rotation. */
	public func String getControllerNameGuideRotation()
		return pControllerNameGuideRotation
	end
	
	/** Animator controller name twist. */
	public func String getControllerNameTwist()
		return pControllerNameTwist
	end
	
	/** Animator controller name guide twist axis. */
	public func String getControllerNameGuideTwistAxis()
		return pControllerNameGuideTwistAxis
	end
	
	/** Animator controller name retracted. */
	public func String getControllerNameRetracted()
		return pControllerNameRetracted
	end
	
	/** Animator controller name blend. */
	public func String getControllerNameBlend()
		return pControllerNameBlend
	end
	
	
	
	/** Get instance in element from owner element class. */
	public func Instance instance( BehaviorElement element )
		return element.getInstanceAt( this.getInstanceIndex() ) cast Instance
	end
	
	
	
	/** Get behavior with slot name in element class or null if absent. */
	static public func ECBMotionTransferIK getBehaviorIn(BehaviorElementClass eclass, String slotName)
		return eclass.findBehavior(block ECBMotionTransferIK each
			return each.getTransfer().getName().equals(slotName)
		end) cast ECBMotionTransferIK
	end
	
	/** Get list of all behaviors in element. */
	static public func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBMotionTransferIK each
			return true
		end)
	end
	
	/** Get instance with slot name in element or null if absent. */
	static public func Instance getInstanceIn(BehaviorElement element, String slotName)
		return element.findInstance(block Instance each
			return each.getECBehavior().getTransfer().getName().equals(slotName)
		end) cast Instance
	end
	
	/** Get list of all instances in element. */
	static public func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** Create Behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
