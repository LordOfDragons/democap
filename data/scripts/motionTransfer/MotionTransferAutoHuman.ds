/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.MotionTransferSystem

pin Democap.Actors
pin Democap.Behaviors
pin Democap.Characters

pin Dragengine.Animators
pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.XML


/**
 * Motion transfer automatic human.
 * 
 * Uses various child motion transfers configured by a few key parameters for simplicity.
 */
class MotionTransferAutoHuman extends DefaultMotionTransfer
	/** Factory. */
	class Factory implements MotionTransferFactory
		public func new()
		end
		
		public func String factoryID()
			return "autoHuman"
		end
		
		public func MotionTransfer createMotionTransfer()
			return MotionTransferAutoHuman.new(factoryID())
		end
		
		public func String displayName()
			return "Automatic Human"
		end
	end
	
	
	
	private var String pBoneHead
	private var String pBoneChest
	private var String pBoneHandRight
	private var String pBoneHandLeft
	private var String pBoneHip
	private var String pBoneFootRight
	private var String pBoneFootLeft
	private var String pMoveBaseIK
	private var String pMoveCrouch
	
	private var MotionTransferChild pTransferWaist
	//private var MotionTransferIK pTransferSpineHead
	private var MotionTransferIK pTransferHead
	private var MotionTransferBend pTransferSpine
	private var MotionTransferIK pTransferArmRight
	private var MotionTransferIK pTransferArmLeft
	private var MotionTransferIK pTransferLegRight
	private var MotionTransferIK pTransferLegLeft
	
	
	
	/** Create motion transfer. */
	public func new(String factoryID) super(factoryID)
		pBoneHead = ""
		pBoneChest = ""
		pBoneHandRight = ""
		pBoneHandLeft = ""
		pBoneHip = ""
		pBoneFootRight = ""
		pBoneFootLeft = ""
		pMoveBaseIK = ""
		pMoveCrouch = ""
		
		// create child motion transfers
		var MotionTransferChild.Factory factoryChild = MotionTransferChild.Factory.new()
		var MotionTransferIK.Factory factoryIK = MotionTransferIK.Factory.new()
		var MotionTransferBend.Factory factoryBend = MotionTransferBend.Factory.new()
		
		pTransferWaist = MotionTransferChild.new(factoryChild.factoryID())
		pTransferWaist.setName("AutoHuman: Waist")
		
		/*
		pTransferSpineHead = MotionTransferIK.new(factoryIK.factoryID())
		pTransferSpineHead.setName("AutoHuman: SpineHead")
		pTransferSpineHead.setTwistPropagation(1)
		pTransferSpineHead.setTwistAxisRotation(Vector.new(90, 0, 0))
		*/
		
		pTransferHead = MotionTransferIK.new(factoryIK.factoryID())
		pTransferHead.setName("AutoHuman: Head")
		
		pTransferSpine = MotionTransferBend.new(factoryBend.factoryID())
		pTransferSpine.setName("AutoHuman: Spine")
		pTransferSpine.setTwistPropagation(0.5)
		pTransferSpine.setTwistAxisRotation(Vector.new(90, 0, 0))
		
		pTransferArmRight = MotionTransferIK.new(factoryIK.factoryID())
		pTransferArmRight.setName("AutoHuman: ArmRight")
		pTransferArmRight.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferArmRight.setActorReach(MotionTransferIK.ActorReach.arm)
		pTransferArmRight.setTwistPropagation(0.5)
		pTransferArmRight.setTwistAxisRotation(Vector.new(0, -90, 0))
		pTransferArmRight.setTwistBoneCount(1)
		
		pTransferArmLeft = MotionTransferIK.new(factoryIK.factoryID())
		pTransferArmLeft.setName("AutoHuman: ArmLeft")
		pTransferArmLeft.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferArmLeft.setActorReach(MotionTransferIK.ActorReach.arm)
		pTransferArmLeft.setTwistPropagation(0.5)
		pTransferArmLeft.setTwistAxisRotation(Vector.new(0, 90, 0))
		pTransferArmLeft.setTwistBoneCount(1)
		
		pTransferLegRight = MotionTransferIK.new(factoryIK.factoryID())
		pTransferLegRight.setName("AutoHuman: LegRight")
		pTransferLegRight.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferLegRight.setActorReach(MotionTransferIK.ActorReach.leg)
		pTransferLegRight.setTwistPropagation(0.5)
		
		pTransferLegLeft = MotionTransferIK.new(factoryIK.factoryID())
		pTransferLegLeft.setName("AutoHuman: LegLeft")
		pTransferLegLeft.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferLegLeft.setActorReach(MotionTransferIK.ActorReach.leg)
		pTransferLegLeft.setTwistPropagation(0.5)
	end
	
	/** Create copy of motion transfer. */
	public func new(MotionTransferAutoHuman transfer) super(transfer)
		pBoneHead = transfer.pBoneHead
		pBoneChest = transfer.pBoneChest
		pBoneHandRight = transfer.pBoneHandRight
		pBoneHandLeft = transfer.pBoneHandLeft
		pBoneHip = transfer.pBoneHip
		pBoneFootRight = transfer.pBoneFootRight
		pBoneFootLeft = transfer.pBoneFootLeft
		pMoveBaseIK = transfer.pMoveBaseIK
		pMoveCrouch = transfer.pMoveCrouch
		
		pTransferWaist = MotionTransferChild.new(transfer.pTransferWaist)
		//pTransferSpineHead = MotionTransferIK.new(transfer.pTransferSpineHead)
		pTransferSpine = MotionTransferBend.new(transfer.pTransferSpine)
		pTransferHead = MotionTransferIK.new(transfer.pTransferHead)
		pTransferArmRight = MotionTransferIK.new(transfer.pTransferArmRight)
		pTransferArmLeft = MotionTransferIK.new(transfer.pTransferArmLeft)
		pTransferLegRight = MotionTransferIK.new(transfer.pTransferLegRight)
		pTransferLegLeft = MotionTransferIK.new(transfer.pTransferLegLeft)
	end
	
	
	
	/** Head bone name. */
	public func String getBoneHead()
		return pBoneHead
	end
	
	/** Set head bone name. */
	public func void setBoneHead(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneHead = bone
	end
	
	/** Chest bone name. */
	public func String getBoneChest()
		return pBoneChest
	end
	
	/** Set chest bone name. */
	public func void setBoneChest(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneChest = bone
	end
	
	/** Right hand bone name. */
	public func String getBoneHandRight()
		return pBoneHandRight
	end
	
	/** Set right hand bone name. */
	public func void setBoneHandRight(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneHandRight = bone
	end
	
	/** Left hand bone name. */
	public func String getBoneHandLeft()
		return pBoneHandLeft
	end
	
	/** Set left hand bone name. */
	public func void setBoneHandLeft(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneHandLeft = bone
	end
	
	/** Hip bone name. */
	public func String getBoneHip()
		return pBoneHip
	end
	
	/** Set hip bone name. */
	public func void setBoneHip(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneHip = bone
	end
	
	/** Right foot bone name. */
	public func String getBoneFootRight()
		return pBoneFootRight
	end
	
	/** Set right foot bone name. */
	public func void setBoneFootRight(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneFootRight = bone
	end
	
	/** Left foot bone name. */
	public func String getBoneFootLeft()
		return pBoneFootLeft
	end
	
	/** Set left foot bone name. */
	public func void setBoneFootLeft(String bone)
		if bone == null
			throw ENullPointer.new("bone")
		end
		pBoneFootLeft = bone
	end
	
	/** Name of move to use to init base IK state for better results. */
	public func String getMoveBaseIK()
		return pMoveBaseIK
	end
	
	/** Set name of move to use to init base IK state for better results. */
	public func void setMoveBaseIK(String move)
		if move == null
			throw ENullPointer.new("move")
		end
		pMoveBaseIK = move
	end
	
	/** Name of move to use to init crouch pose for better results. */
	public func String getMoveCrouch()
		return pMoveCrouch
	end
	
	/** Set name of move to use to init crouch pose for better results. */
	public func void setMoveCrouch(String move)
		if move == null
			throw ENullPointer.new("move")
		end
		pMoveCrouch = move
	end
	
	
	
	/** Child motion transfer. */
	public func MotionTransferChild getTransferWaist()
		return pTransferWaist
	end
	
	/*
	public func MotionTransferIK getTransferSpineHead()
		return pTransferSpineHead
	end
	*/
	
	public func MotionTransferIK getTransferHead()
		return pTransferHead
	end
	
	public func MotionTransferBend getTransferSpine()
		return pTransferSpine
	end
	
	public func MotionTransferIK getTransferArmRight()
		return pTransferArmRight
	end
	
	public func MotionTransferIK getTransferArmLeft()
		return pTransferArmLeft
	end
	
	public func MotionTransferIK getTransferLegRight()
		return pTransferLegRight
	end
	
	public func MotionTransferIK getTransferLegLeft()
		return pTransferLegLeft
	end
	
	
	
	/** Init element class. */
	public func void initElementClass(CharacterConfiguration config, BaseVRActorClass eclass)
		pTransferWaist.initElementClass(config, eclass)
		//pTransferSpineHead.initElementClass(config, eclass)
		pTransferSpine.initElementClass(config, eclass)
		pTransferHead.initElementClass(config, eclass)
		pTransferArmRight.initElementClass(config, eclass)
		pTransferArmLeft.initElementClass(config, eclass)
		pTransferLegRight.initElementClass(config, eclass)
		pTransferLegLeft.initElementClass(config, eclass)
	end
	
	/** Reset calibration. */
	public func void resetCalibration()
		pTransferWaist.resetCalibration()
		//pTransferSpineHead.resetCalibration()
		pTransferSpine.resetCalibration()
		pTransferHead.resetCalibration()
		pTransferArmRight.resetCalibration()
		pTransferArmLeft.resetCalibration()
		pTransferLegRight.resetCalibration()
		pTransferLegLeft.resetCalibration()
	end
	
	/** Calibrate character. */
	public func void calibrate(CharacterConfiguration config, ActorProfile actorProfile, \
	BaseVRActor actor, Component component)
		initChildTransferParams(config, actorProfile, actor, component)
		
		pTransferWaist.calibrate(config, actorProfile, actor, component)
		//pTransferSpineHead.calibrate(config, actorProfile, actor, component)
		pTransferSpine.calibrate(config, actorProfile, actor, component)
		pTransferHead.calibrate(config, actorProfile, actor, component)
		pTransferArmRight.calibrate(config, actorProfile, actor, component)
		pTransferArmLeft.calibrate(config, actorProfile, actor, component)
		pTransferLegRight.calibrate(config, actorProfile, actor, component)
		pTransferLegLeft.calibrate(config, actorProfile, actor, component)
	end
	
	/** Init child transfer parameters. */
	protected func void initChildTransferParams(CharacterConfiguration config,\
	ActorProfile actorProfile, BaseVRActor actor, Component component)
		var Rig rig = component.getRig()
		var Array chain = Array.new()
		var float bendScale
		var int index
		
		assignSlots(config)
		
		// waist
		pTransferWaist.setBone(pBoneHip)
		
		// spine, head. base is hip which we use to find chains but it should not be
		// included in anything but the waist. start one bone down the chain
		var Array chainSpine = Array.new()
		findBoneChain(rig, pBoneChest, pBoneHip, chainSpine)
		
		/*
		pTransferSpineHead.setTipBone(pBoneHead)
		pTransferSpineHead.setGuideBone(pBoneChest)
		pTransferSpineHead.setMoveBaseIK(pMoveBaseIK)
		pTransferSpineHead.setTwistPropagation(1)
		pTransferSpineHead.setTwistAxisRotation(Vector.new(90, 0, 0))
		
		if chainSpine.getCount() > 1
			pTransferSpineHead.setBaseBone(chainSpine.getAt(-2) cast String)
			pTransferSpineHead.setTwistBoneCount(DEMath.max(chainSpine.getCount() - 2, 0))
			
		else
			pTransferSpineHead.setBaseBone("")
			pTransferSpineHead.setTwistBoneCount(0)
		end
		*/
		
		pTransferSpine.setTipBone(pBoneChest)
		pTransferSpine.setTargetBone(pBoneHead)
		//pTransferSpine.getTargetTracker().setOffsetPosition(Vector.new(0, 0, -0.05))
		pTransferSpine.getTargetTracker().setOffsetPosition(Vector.new(0, 0, 0))
		pTransferSpine.setTwistPropagation(0.5)
		pTransferSpine.setTwistAxisRotation(Vector.new(90, 0, 0))
		
		if chainSpine.getCount() > 1
			pTransferSpine.setBaseBone(chainSpine.getAt(-2) cast String)
			pTransferSpine.setTwistBoneCount(DEMath.max(chainSpine.getCount() - 1, 0))
			//bendScale = (chainSpine.getCount() cast float) / ((chainSpine.getCount() - 1) cast float)
			//bendScale = (chainSpine.getCount() cast float) / ((chainSpine.getCount() - 1) cast float + 0.5)
			bendScale = 1.33
			
		else
			pTransferSpine.setBaseBone("")
			pTransferSpine.setTwistBoneCount(0)
			bendScale = 1
		end
		pTransferSpine.setScaleRotation(Vector2.new(bendScale, bendScale))
		
		var Array chainHead = Array.new()
		findBoneChain(rig, pBoneHead, pBoneChest, chainHead)
		
		pTransferHead.setTipBone(pBoneHead)
		
		if chainHead.getCount() > 1
			pTransferHead.setBaseBone(chainHead.getAt(-2) cast String)
			
		else
			pTransferHead.setBaseBone("")
		end
		
		// right arm. separate guide bone is used if chain has at least 4 bones:
		// hand, lower arm, upper arm, shoulder. with 3 bones no shoulder is assumed
		findBoneChain(rig, pBoneHandRight, pBoneHip, chain)
		index = firstBoneNotInChain(chain, chainSpine)
		
		pTransferArmRight.setTipBone(pBoneHandRight)
		pTransferArmRight.setMoveBaseIK(pMoveBaseIK)
		pTransferArmRight.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferArmRight.setActorReach(MotionTransferIK.ActorReach.arm)
		pTransferArmRight.setTwistBoneCount(1)
		pTransferArmRight.setTwistPropagation(0.5)
		pTransferArmRight.setTwistAxisRotation(Vector.new(0, -90, 0))
		
		if index > 1 // at least 3 bones
			pTransferArmRight.setBaseBone(chain.getAt(index - 1) cast String)
			
		else
			pTransferArmRight.setBaseBone("")
		end
		
		if index > 2 // at least 4 bones
			pTransferArmRight.setGuideBone(chain.getAt(index - 2) cast String)
			
		else
			pTransferArmRight.setGuideBone(pTransferArmRight.getBaseBone())
		end
		
		// left arm. separate guide bone is used if chain has at least 4 bones:
		// hand, lower arm, upper arm, shoulder. with 3 bones no shoulder is assumed
		findBoneChain(rig, pBoneHandLeft, pBoneHip, chain)
		index = firstBoneNotInChain(chain, chainSpine)
		
		pTransferArmLeft.setTipBone(pBoneHandLeft)
		pTransferArmLeft.setMoveBaseIK(pMoveBaseIK)
		pTransferArmLeft.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferArmLeft.setActorReach(MotionTransferIK.ActorReach.arm)
		pTransferArmLeft.setTwistBoneCount(1)
		pTransferArmLeft.setTwistPropagation(0.5)
		pTransferArmLeft.setTwistAxisRotation(Vector.new(0, 90, 0))
		
		if index > 1 // at least 3 bones
			pTransferArmLeft.setBaseBone(chain.getAt(index - 1) cast String)
			
		else
			pTransferArmLeft.setBaseBone("")
		end
		
		if index > 2 // at least 4 bones
			pTransferArmLeft.setGuideBone(chain.getAt(index - 2) cast String)
			
		else
			pTransferArmLeft.setGuideBone(pTransferArmLeft.getBaseBone())
		end
		
		// right leg
		findBoneChain(rig, pBoneFootRight, pBoneHip, chain)
		index = firstBoneNotInChain(chain, chainSpine)
		
		pTransferLegRight.setTipBone(pBoneFootRight)
		pTransferLegRight.setMoveBaseIK(pMoveBaseIK)
		pTransferLegRight.setMoveRetracted(pMoveCrouch)
		pTransferLegRight.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferLegRight.setActorReach(MotionTransferIK.ActorReach.leg)
		pTransferLegRight.setTwistBoneCount(2)
		pTransferLegRight.setTwistPropagation(0.5)
		
		if index > 1
			pTransferLegRight.setBaseBone(chain.getAt(index - 1) cast String)
			
		else
			pTransferLegRight.setBaseBone("")
		end
		
		pTransferLegRight.setGuideBone(pTransferLegRight.getBaseBone())
		
		// left leg
		findBoneChain(rig, pBoneFootLeft, pBoneHip, chain)
		index = firstBoneNotInChain(chain, chainSpine)
		
		pTransferLegLeft.setTipBone(pBoneFootLeft)
		pTransferLegLeft.setMoveBaseIK(pMoveBaseIK)
		pTransferLegLeft.setMoveRetracted(pMoveCrouch)
		pTransferLegLeft.getTipTracker().setAutoTransform(MotionTransferTracker.AutoTransform.orientation)
		pTransferLegLeft.setActorReach(MotionTransferIK.ActorReach.leg)
		pTransferLegLeft.setTwistBoneCount(2)
		pTransferLegLeft.setTwistPropagation(0.5)
		
		if index > 1
			pTransferLegLeft.setBaseBone(chain.getAt(index - 1) cast String)
			
		else
			pTransferLegLeft.setBaseBone("")
		end
		
		pTransferLegLeft.setGuideBone(pTransferLegLeft.getBaseBone())
	end
	
	/** Create copy of motion transfer. */
	public func MotionTransfer createCopy()
		return MotionTransferAutoHuman.new(this)
	end
	
	/** Decompose motion transfer. Returns Array of MotionTranfer or empty list if not supported. */
	public func Array decompose()
		var Array list = Array.new()
		list.add(pTransferWaist.createCopy())
		//list.add(pTransferSpineHead.createCopy())
		list.add(pTransferSpine.createCopy())
		list.add(pTransferHead.createCopy())
		list.add(pTransferArmRight.createCopy())
		list.add(pTransferArmLeft.createCopy())
		list.add(pTransferLegRight.createCopy())
		list.add(pTransferLegLeft.createCopy())
		return list
	end
	
	/** Load from XML. */
	public func void load(BaseXmlLoader loader, EasyXMLElement root)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("name")
				setName(element.getFirstCData())
				
			elif tagName.equals("boneHead")
				setBoneHead(element.getFirstCData())
				
			elif tagName.equals("boneChest")
				setBoneChest(element.getFirstCData())
				
			elif tagName.equals("boneHandRight")
				setBoneHandRight(element.getFirstCData())
				
			elif tagName.equals("boneHandLeft")
				setBoneHandLeft(element.getFirstCData())
				
			elif tagName.equals("boneHip")
				setBoneHip(element.getFirstCData())
				
			elif tagName.equals("boneFootRight")
				setBoneFootRight(element.getFirstCData())
				
			elif tagName.equals("boneFootLeft")
				setBoneFootLeft(element.getFirstCData())
				
			elif tagName.equals("moveBaseIK")
				setMoveBaseIK(element.getFirstCData())
				
			elif tagName.equals("moveCrouch")
				setMoveCrouch(element.getFirstCData())
				
			elif tagName.equals("transfers")
				loadTransfers(loader, element)
				
			elif tagName.equals("calibrated")
				loadCalibrated(loader, element)
			end
		end)
	end
	
	protected func void loadTransfers(BaseXmlLoader loader, EasyXMLElement root)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("waist")
				pTransferWaist.load(loader, element)
				
			/*
			elif tagName.equals("spineHead")
				pTransferSpineHead.load(loader, element)
			*/
				
			elif tagName.equals("head")
				pTransferHead.load(loader, element)
				
			elif tagName.equals("spine")
				pTransferSpine.load(loader, element)
				
			elif tagName.equals("armRight")
				pTransferArmRight.load(loader, element)
				
			elif tagName.equals("armLeft")
				pTransferArmLeft.load(loader, element)
				
			elif tagName.equals("legRight")
				pTransferLegRight.load(loader, element)
				
			elif tagName.equals("legLeft")
				pTransferLegLeft.load(loader, element)
			end
		end)
	end
	
	public func void loadCalibrated(BaseXmlLoader loader, EasyXMLElement root)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("transfers")
				loadCalibratedTransfers(loader, element)
			end
		end)
	end
	
	protected func void loadCalibratedTransfers(BaseXmlLoader loader, EasyXMLElement root)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("waist")
				pTransferWaist.loadCalibrated(loader, element)
				
			/*
			elif tagName.equals("spineHead")
				pTransferSpineHead.loadCalibrated(loader, element)
			*/
				
			elif tagName.equals("head")
				pTransferHead.loadCalibrated(loader, element)
				
			elif tagName.equals("spine")
				pTransferSpine.loadCalibrated(loader, element)
				
			elif tagName.equals("armRight")
				pTransferArmRight.loadCalibrated(loader, element)
				
			elif tagName.equals("armLeft")
				pTransferArmLeft.loadCalibrated(loader, element)
				
			elif tagName.equals("legRight")
				pTransferLegRight.loadCalibrated(loader, element)
				
			elif tagName.equals("legLeft")
				pTransferLegLeft.loadCalibrated(loader, element)
			end
		end)
	end
	
	/** Save to XML. */
	public func void save(BaseXmlLoader loader, EasyXMLElement tag)
		tag.addDataTag("name", getName())
		tag.addDataTag("boneHead", pBoneHead)
		tag.addDataTag("boneChest", pBoneChest)
		tag.addDataTag("boneHandRight", pBoneHandRight)
		tag.addDataTag("boneHandLeft", pBoneHandLeft)
		tag.addDataTag("boneHip", pBoneHip)
		tag.addDataTag("boneFootRight", pBoneFootRight)
		tag.addDataTag("boneFootLeft", pBoneFootLeft)
		tag.addDataTag("moveBaseIK", pMoveBaseIK)
		tag.addDataTag("moveCrouch", pMoveCrouch)
		
		var EasyXMLElement tagTransfers = tag.addTag("transfers")
		pTransferWaist.save(loader, tagTransfers.addTag("waist"))
		//pTransferSpineHead.save(loader, tagTransfers.addTag("spineHead"))
		pTransferSpine.save(loader, tagTransfers.addTag("spine"))
		pTransferHead.save(loader, tagTransfers.addTag("head"))
		pTransferArmRight.save(loader, tagTransfers.addTag("armRight"))
		pTransferArmLeft.save(loader, tagTransfers.addTag("armLeft"))
		pTransferLegRight.save(loader, tagTransfers.addTag("legRight"))
		pTransferLegLeft.save(loader, tagTransfers.addTag("legLeft"))
		
		saveCalibrated(loader, tag.addTag("calibrated"))
	end
	
	public func void saveCalibrated(BaseXmlLoader loader, EasyXMLElement tag)
		var EasyXMLElement tagTransfers = tag.addTag("transfers")
		pTransferWaist.saveCalibrated(loader, tagTransfers.addTag("waist"))
		//pTransferSpineHead.saveCalibrated(loader, tagTransfers.addTag("spineHead"))
		pTransferSpine.saveCalibrated(loader, tagTransfers.addTag("spine"))
		pTransferHead.saveCalibrated(loader, tagTransfers.addTag("head"))
		pTransferArmRight.saveCalibrated(loader, tagTransfers.addTag("armRight"))
		pTransferArmLeft.saveCalibrated(loader, tagTransfers.addTag("armLeft"))
		pTransferLegRight.saveCalibrated(loader, tagTransfers.addTag("legRight"))
		pTransferLegLeft.saveCalibrated(loader, tagTransfers.addTag("legLeft"))
	end
	
	
	
	/** Find guide bone or empty string. */
	protected func String findBoneBeforeBase(Rig rig, String tipBone, String baseBone)
		if tipBone.empty() or baseBone.empty()
			return ""
		end
		
		var Array chain = Array.new()
		findBoneChain(rig, tipBone, baseBone, chain)
		if chain.getCount() < 3
			return ""
		end
		
		return chain.getAt( -2 ) cast String
	end
	
	/** Index of first bone not in another chain or chain.getCount(). */
	protected func int firstBoneNotInChain(Array chain, Array otherChain)
		var int index, count = chain.getCount()
		for index = 0 to count
			if otherChain.has(chain.getAt(index))
				break
			end
		end
		return index
	end
	
	/** Find slots and assign them to child transfers. */
	protected func void assignSlots(CharacterConfiguration config)
		var String slotWaist = firstSlotWithType(config, CharacterTrackerSlot.Type.waist)
		
		pTransferWaist.getTracker().setSlot(slotWaist)
		
		/*
		pTransferSpineHead.getTipTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.headHmd))
		pTransferSpineHead.getBaseTracker().setSlot(slotWaist)
		*/
		
		pTransferHead.getTipTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.headHmd))
		
		pTransferSpine.getTargetTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.headHmd))
		pTransferSpine.getBaseTracker().setSlot(slotWaist)
		
		pTransferArmRight.getTipTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.rightHandController))
		pTransferArmRight.getGuideTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.rightElbow))
		pTransferArmRight.getBaseTracker().setSlot(slotWaist)
		
		pTransferArmLeft.getTipTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.leftHandController))
		pTransferArmLeft.getGuideTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.leftElbow))
		pTransferArmLeft.getBaseTracker().setSlot(slotWaist)
		
		pTransferLegRight.getTipTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.rightFoot))
		pTransferLegRight.getGuideTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.rightKnee))
		pTransferLegRight.getBaseTracker().setSlot(slotWaist)
		
		pTransferLegLeft.getTipTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.leftFoot))
		pTransferLegLeft.getGuideTracker().setSlot(firstSlotWithType(config, CharacterTrackerSlot.Type.leftKnee))
		pTransferLegLeft.getBaseTracker().setSlot(slotWaist)
	end
	
	/** First slot name matching type or empty string. */
	protected func String firstSlotWithType(CharacterConfiguration config, CharacterTrackerSlot.Type type)
		var CharacterTrackerSlot slot = config.getTrackerSlots().firstWithType(type)
		return slot != null if slot.getName() else ""
	end
end
