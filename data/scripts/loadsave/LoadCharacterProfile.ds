/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap

pin Democap.Characters
pin Democap.MotionTransferSystem

pin Dragengine.Animators
pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.XML



/**
 * Load/Save character profile from/to XML file.
 */
class LoadCharacterProfile extends BaseXmlLoader
	/** Create loader. */
	public func new(Console console) this(console, "LoadCharacterProfile")
	end
	
	/** Create loader. */
	public func new(Console console, String name) super(console, name)
	end
	
	
	
	/** Load character profile from file. */
	public func CharacterProfile load(String filename)
		logLoadingFromFile(filename)
		
		var EasyXML document = EasyXML.new(filename)
		if document.hasParseFailed()
			logParsingFailed(document.getParseLog())
			throw EInvalidParam.new("parsing document failed")
		end
		
		var EasyXMLElement root = document.getRootElement()
		if not root.getTagName().equals("characterProfile")
			logUnknownTag(root)
			throw EInvalidParam.new("root tag name mismatch")
		end
		
		// name of profile is the file name without extension
		var CharacterProfile profile = CharacterProfile.new(File.new(filename).getName().substring(0, -7))
		
		readCharacterProfile(root, profile)
		return profile
	end
	
	/** Save character profile to file. */
	public func void save(String filename, CharacterProfile profile)
		if profile == null
			throw ENullPointer.new("profile")
		end
		
		logSavingFromFile(filename)
		
		var EasyXML document = EasyXML.new()
		var EasyXMLElement root = document.getRootElement()
		
		root.setTagName("characterProfile")
		
		writeCharacterProfile(root, profile)
		
		document.writeToFile(FileWriter.new(filename), false)
	end
	
	
	
	/** Read character profile tag. */
	public func void readCharacterProfile(EasyXMLElement root, CharacterProfile profile)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("appearance")
				readAppearance(element, profile.getAppearance())
				
			elif tagName.equals("configuration")
				readConfiguration(element, profile)
				
			elif tagName.equals("originBone")
				profile.setOriginBone(element.getFirstCData())
			end
		end)
	end
	
	/** Read appearance tag. */
	public func void readAppearance(EasyXMLElement root, CharacterAppearance appearance)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("model")
				appearance.setPathModel(element.getFirstCData())
				
			elif tagName.equals("skin")
				appearance.setPathSkin(element.getFirstCData())
				
			elif tagName.equals("rig")
				appearance.setPathRig(element.getFirstCData())
				
			elif tagName.equals("animation")
				appearance.setPathAnimation(element.getFirstCData())
			end
		end)
	end
	
	/** Read configuration tag. */
	public func void readConfiguration(EasyXMLElement root, CharacterProfile profile)
		var CharacterConfiguration configuration = CharacterConfiguration.new(profile, root.getAttributeNamed("name"))
		var MotionTransferFactories motionTransferFactories = GameApp.getGameApp().getMotionTransferFactories()
		profile.getConfigurations().add(configuration)
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("scaleMode")
				configuration.setScaleMode(CharacterConfiguration.ScaleMode.named(element.getFirstCData()))
				
			elif tagName.equals("shoulderLevel")
				configuration.setShoulderLevel(element.getFirstCDataFloat())
				
			elif tagName.equals("cameraBone")
				configuration.setCameraBone(element.getFirstCData())
				
			elif tagName.equals("cameraPosition")
				configuration.setCameraPosition(readVector(element))
				
			elif tagName.equals("cameraRotation")
				configuration.setCameraRotation(readVector(element))
				
			elif tagName.equals("moveCalibrate")
				configuration.setMoveCalibrate(element.getFirstCData())
				
			elif tagName.equals("calibrate")
				readConfigurationCalibrate(element, configuration)
				
			elif tagName.equals("trackerSlot")
				readTrackerSlot(element, configuration)
				
			elif tagName.equals("motionTransfer")
				var String type = element.getAttributeNamed("type")
				var MotionTransferFactory factory = motionTransferFactories.getAt(type)
				if factory == null
					logInvalidAttributeValue(element, "type", type)
					throw EInvalidParam.new("Invalid motion transfer type '" + type + "'")
				end
				
				var MotionTransfer transfer = factory.createMotionTransfer()
				transfer.load(this, element)
				
				configuration.getMotionTransfers().add(transfer)
			end
		end)
	end
	
	/** Read configuration calibration tag. */
	public func void readConfigurationCalibrate(EasyXMLElement root, CharacterConfiguration configuration)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("scale")
				configuration.setCalibrateScale(element.getFirstCDataFloat())
				
			elif tagName.equals("matrix")
				configuration.setMatrixCalibrate(Matrix.new(\
					element.getAttributeFloatNamed("a11"), element.getAttributeFloatNamed("a12"), \
					element.getAttributeFloatNamed("a13"), element.getAttributeFloatNamed("a14"), \
					element.getAttributeFloatNamed("a21"), element.getAttributeFloatNamed("a22"), \
					element.getAttributeFloatNamed("a23"), element.getAttributeFloatNamed("a24"), \
					element.getAttributeFloatNamed("a31"), element.getAttributeFloatNamed("a32"), \
					element.getAttributeFloatNamed("a33"), element.getAttributeFloatNamed("a34")))
			end
		end)
	end
	
	/** Read tracker slot tag. */
	public func void readTrackerSlot(EasyXMLElement root, CharacterConfiguration configuration)
		var CharacterTrackerSlot slot = CharacterTrackerSlot.new(configuration, root.getAttributeNamed("name"))
		configuration.getTrackerSlots().add(slot)
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("position")
				slot.setPosition(readVector(element))
				
			elif tagName.equals("rotation")
				slot.setRotation(readVector(element))
				
			elif tagName.equals("inputDevice")
				slot.setInputDeviceID(element.getFirstCData())
				
			elif tagName.equals("offsetPosition")
				slot.setOffsetPosition(readVector(element))
				
			elif tagName.equals("offsetRotation")
				slot.setOffsetRotation(readVector(element))
				
			elif tagName.equals("calibrate")
				readTrackerSlotCalibrate(element, slot)
			end
		end)
	end
	
	/** Read tracker slot calibration tag. */
	public func void readTrackerSlotCalibrate(EasyXMLElement root, CharacterTrackerSlot slot)
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("position")
				slot.setCalibratePosition(readVector(element))
				
			elif tagName.equals("rotation")
				slot.setCalibrateRotation(readVector(element))
			end
		end)
	end
	
	
	
	
	/** Write character profile. */
	public func void writeCharacterProfile(EasyXMLElement root, CharacterProfile profile)
		writeAppearance(root, profile.getAppearance())
		
		profile.getConfigurations().forEach(block CharacterConfiguration each
			writeConfiguration(root, each)
		end)
		
		root.addDataTag("originBone", profile.getOriginBone())
	end
	
	/** Write appearance. */
	public func void writeAppearance(EasyXMLElement root, CharacterAppearance appearance)
		var EasyXMLElement tag = root.addTag("appearance")
		tag.addDataTag("model", appearance.getPathModel())
		tag.addDataTag("skin", appearance.getPathSkin())
		tag.addDataTag("rig", appearance.getPathRig())
		tag.addDataTag("animation", appearance.getPathAnimation())
	end
	
	/** Write configuration. */
	public func void writeConfiguration(EasyXMLElement root, CharacterConfiguration configuration)
		var EasyXMLElement tag = root.addTag("configuration")
		tag.setAttribute("name", configuration.getName())
		
		tag.addDataTag("scaleMode", configuration.getScaleMode().name())
		tag.addDataTag("shoulderLevel", configuration.getShoulderLevel())
		tag.addDataTag("moveCalibrate", configuration.getMoveCalibrate())
		tag.addDataTag("cameraBone", configuration.getCameraBone())
		writeVector(tag, "cameraPosition", configuration.getCameraPosition())
		writeVector(tag, "cameraRotation", configuration.getCameraRotation())
		
		configuration.getTrackerSlots().forEach(block CharacterTrackerSlot each
			writeTrackerSlot(tag, each)
		end)
		
		configuration.getMotionTransfers().forEach(block MotionTransfer each
			var EasyXMLElement childTag = tag.addTag("motionTransfer")
			childTag.setAttribute("type", each.factoryID())
			each.save(this, childTag)
		end)
		
		writeConfigurationCalibrate(tag, configuration)
	end
	
	/** Write configuration calibration. */
	public func void writeConfigurationCalibrate(EasyXMLElement root, CharacterConfiguration configuration)
		var EasyXMLElement tag = root.addTag("calibrate")
		
		tag.addDataTag("scale", configuration.getCalibrateScale())
		
		var EasyXMLElement tag2 = tag.addTag("matrix")
		tag2.setAttribute("a11", configuration.getMatrixCalibrate().get11())
		tag2.setAttribute("a12", configuration.getMatrixCalibrate().get12())
		tag2.setAttribute("a13", configuration.getMatrixCalibrate().get13())
		tag2.setAttribute("a14", configuration.getMatrixCalibrate().get14())
		tag2.setAttribute("a21", configuration.getMatrixCalibrate().get21())
		tag2.setAttribute("a22", configuration.getMatrixCalibrate().get22())
		tag2.setAttribute("a23", configuration.getMatrixCalibrate().get23())
		tag2.setAttribute("a24", configuration.getMatrixCalibrate().get24())
		tag2.setAttribute("a31", configuration.getMatrixCalibrate().get31())
		tag2.setAttribute("a32", configuration.getMatrixCalibrate().get32())
		tag2.setAttribute("a33", configuration.getMatrixCalibrate().get33())
		tag2.setAttribute("a34", configuration.getMatrixCalibrate().get34())
	end
	
	/** Write tracker slot. */
	public func void writeTrackerSlot(EasyXMLElement root, CharacterTrackerSlot slot)
		var EasyXMLElement tag = root.addTag("trackerSlot")
		tag.setAttribute("name", slot.getName())
		
		writeVector(tag, "position", slot.getPosition())
		writeVector(tag, "rotation", slot.getRotation())
		tag.addDataTag("inputDevice", slot.getInputDeviceID())
		writeVector(tag, "offsetPosition", slot.getOffsetPosition())
		writeVector(tag, "offsetRotation", slot.getOffsetRotation())
	end
	
	/** Write tracker slot calibrate. */
	public func void writeTrackerSlotCalibrate(EasyXMLElement root, CharacterTrackerSlot slot)
		var EasyXMLElement tag = root.addTag("calibrate")
		writeVector(tag, "position", slot.getCalibratePosition())
		writeVector(tag, "rotation", slot.getCalibrateRotation())
	end
end
