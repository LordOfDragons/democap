namespace Democap

pin Dragengine.CameraDirectors
pin Dragengine.Gui
pin Dragengine.Scenery
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * Start game loader class.
 */
class StartGame extends WindowGameWorld.Loader
	/**
	 * Create start game loader.
	 */
	public func new(WindowGameWorld window, String pathWorld) super(window)
		setPathWorld(pathWorld)
	end
	
	/**
	 * Loading world finished successfully.
	 */
	public func void onLoadWorldFinished()
		var GameApp app = BaseGameApp.getApp() cast GameApp
		var GameWorld gameWorld = getStubWorld().getGameWorld()
		var ElementClass eclass
		var UniqueID id
		
		// spawn player actor
		eclass = app.getElementClassList().getNamed("VRActor")
		id = app.getIDGenerator().nextID()
		var BaseVRActor player = StubElement.new(eclass, id).createElement() cast BaseVRActor
		
		gameWorld.getSpawnPoints().getNamed("player").spawn(gameWorld, player)
		
		player.getPlayerControllable().takeControl()
		
		// assign camera director attaching the camera to the player controlled actor
		var VRCameraDirector director = VRCameraDirector.new()
		director.setViewDistance(300)
		director.setLowestIntensity(20)
		director.setHighestIntensity(20)
		director.setAdaptionTime(30)
		getWindow().setCameraDirector(director)
		
		// finish loading
		super.onLoadWorldFinished()
		
		app.setInputProcessorBindings()
		app.finishLoadGameWorld()
	end
	
	/**
	 * Loading world failed.
	 */
	public func void onLoadWorldFailed()
		super.onLoadWorldFailed()
		
		BaseGameApp.getApp().cancelLoadGameWorld()
		
		Engine.quit()
	end
end
