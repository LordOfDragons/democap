/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Elements

pin Democap.Behaviors

pin Dragengine.Commands
pin Dragengine.Gui
pin Dragengine.Scenery



/**
 * VR Menu element class.
 */
class VRMenuClass extends BehaviorElementClass
	public var ECBehaviorComponent component
	public var ECBehaviorCollider collider
	public var ECBehaviorDynamicSkin dynamicSkin
	public var ECBehaviorRenderableDesktop renderableDesktop
	public var ECBVRWindowMain windowMain
	public var ECBehaviorVRHandPointedAt vrHandPointedAt
	public var ECBehaviorControlDesktop controlDesktop
	public var ECBToggleVisibility toggleVisibility
	public var ECBHideWhileRecording hideWhileRecording
	
	
	
	/** Create element class. */
	public func new() this("VRMenu")
	end
	
	public func new(String name) super(name)
		setPersistable(false)
		
		component = ECBehaviorComponent.new(this, null)
		component.setCommonPath("/content/models/vrmenu", "vrmenu.demodel", "vrmenu.deskin", "vrmenu.derig")
		
		collider = ECBehaviorCollider.new(this, component)
		collider.getCollider().getPhysicsType().setEnumeration(CollisionResponse.kinematic)
		collider.getCollider().getLocalGravity().setVector(Vector.new())
		
		collider.getCollider().setCollisionFilter(CollisionFilter.new(\
			LayerMask.newWith(GameApp.CollisionFilterBit.interaction)))
		
		dynamicSkin = ECBehaviorDynamicSkin.new(this, component)
		
		renderableDesktop = ECBehaviorRenderableDesktop.new(this, dynamicSkin)
		renderableDesktop.getGuiTheme().setPath("/content/gui/vrmain.guitheme.xml")
		renderableDesktop.getRenderable().setValue("screen.content")
		renderableDesktop.getRenderableSolidity().setValue("screen.solidity")
		renderableDesktop.getSize().setPoint(Point.new(1024, 1024))
		
		windowMain = ECBVRWindowMain.new(this, renderableDesktop)
		
		vrHandPointedAt = ECBehaviorVRHandPointedAt.new(this)
		
		controlDesktop = ECBehaviorControlDesktop.new(this, renderableDesktop)
		controlDesktop.getLowerLeft().setVector(Vector.new(0.484, -0.484, 0))
		controlDesktop.getLowerRight().setVector(Vector.new(-0.484, -0.484, 0))
		controlDesktop.getUpperLeft().setVector(Vector.new(0.484, 0.484, 0))
		
		toggleVisibility = ECBToggleVisibility.new(this)
		toggleVisibility.getDistance().setValue(0.2)
		toggleVisibility.getLockZRotation().setValue(true)
		
		hideWhileRecording = ECBHideWhileRecording.new(this)
	end
end
