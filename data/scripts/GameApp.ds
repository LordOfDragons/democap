/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap

pin Democap.Actors
pin Democap.Characters
pin Democap.Commands
pin Democap.MotionTransferSystem
pin Democap.Gui

pin Dragengine.Commands
pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * Game Application.
 */
class GameApp extends BaseGameApp
	private var ActorProfiles pActorProfiles
	private var ActorProfile pActiveActorProfile
	private var CharacterProfiles pCharacterProfiles
	
	private var MotionTransferFactories pMotionTransferFactories
	private var LoadActorProfile pLoadActorProfile
	private var LoadCharacterProfile pLoadCharacterProfile
	private var MutableID pNextElementClassID
	
	private var WorldSpawnCharacter pWorldSpawnCharacter
	
	private var WindowMain pWindowMain
	private var DMCWindowConsole pDMCWindowConsole
	
	
	
	/**
	 * Create Game Application.
	 */
	public func new()
		pathDesktopGuiTheme = "/content/gui/main.guitheme.xml"
// 		defaultConsoleBufferSize = 100
	end
	
	
	
	/**
	 * Main window.
	 */
	public func WindowMain getWindowMain()
		return pWindowMain
	end
	
	/**
	 * Console window.
	 */
	public func DMCWindowConsole getDMCWindowConsole()
		return pDMCWindowConsole
	end
	
	
	/**
	 * Initialize game.
	 */
	public func void initGame()
		super.initGame()
		
		pMotionTransferFactories = MotionTransferFactories.new()
		addMotionTransferFactories()
		
		// modify render window to be a regular window showing the VR view
		var Desktop desktop = getDesktop()
		var WindowGameWorld windowGameWorld = getWindowGameWorld()
		windowGameWorld.setTitle("VR View")
		windowGameWorld.setPosition(Point.new(10, 30))
		windowGameWorld.setSize(desktop.getContainerSize() - Point.new(350, 50))
		windowGameWorld.setCanClose(false)
		windowGameWorld.setCanMaximize(false)
		windowGameWorld.setCanMinimize(false)
		windowGameWorld.setCanReorder(false)
		
		// create main window
		pWindowMain = WindowMain.new(desktop)
		
		// create console window
		pDMCWindowConsole = DMCWindowConsole.new(desktop)
		
		// do not capture input. this allows the window to be used like regular windows
		InputSystem.setCaptureInputDevices(false)
		
		// show Drag[en]gine logo then launch the application
		WindowLogo.showDragengineLogo(this.getDesktop(), BlockActionListener.new(\
			block ActionEvent event
				launchApplication()
			end))
	end
	
	/**
	 * Prevent console window from being created. We will create our own.
	 */
	protected func WindowConsole createWindowConsole()
		return null
	end
	
	/**
	 * Launch example application.
	 */
	public func void launchApplication()
		//getWindowGameWorld().addWidget(ShoutConsole.new(getConsole()))
		
		// set input processor to forward mouse and keyboard input to the desktop widget
		// while processing all other inputs using bindings. adding, removing and changing
		// device events are forwarded to both input processors
		setInputProcessor(SplitInputProcessor.new(getBindingManager(), getDesktop()))
		
		// start VR runtime. this can fail hence capture exceptions
		getConsole().addMessage("Start VR Runtime...")
		try
			VRSystem.startRuntime()
			
		catch Exception e
			getConsole().addMessage("Failed starting VR Runtime")
		end
		
		// create other stuff
		pNextElementClassID = MutableID.new(1)
		pLoadActorProfile = LoadActorProfile.new(getConsole())
		pLoadCharacterProfile = LoadCharacterProfile.new(getConsole())
		
		pActorProfiles = ActorProfiles.new("/actors")
		pActorProfiles.loadAll()
		
		if pActorProfiles.getCount() > 0
			var Array names = Array.new()
			pActorProfiles.forEach(block ActorProfile each
				names.add(each.getName())
			end)
			setActiveActorProfile(pActorProfiles.getAt(names.sorted().getAt(0) cast String))
		end
		
		pCharacterProfiles = CharacterProfiles.new("/characters")
		pCharacterProfiles.loadAll()
		
		pWorldSpawnCharacter = WorldSpawnCharacter.new()
		
		// load game world
		getConsole().addMessage("Loading world...")
		startLoadGameWorld(StartGame.new(getWindowGameWorld(), "/content/test/scene1/scene1.deworld"))
// 		startLoadGameWorld(StartGame.new(getWindowGameWorld(), "/content/worlds/default.deworld"))
	end
	
	/**
	 * Create element classes and add them to element class list.
	 */
	protected func void createAndAddElementClasses()
		super.createAndAddElementClasses()
		
		var ElementClassList eclist = getElementClassList()
		//eclist.add(MyElementClass.new())
		
		getLoaders().getElementClass().addXmlElementClasses(eclist, "/content/models", true)
	end
	
	/** Add commands. */
	protected func void createAndAddCommands()
		super.createAndAddCommands()
		
		var CommandManager commands = getCommandManager()
		
		CommandsAAControllerInput.addCommands(commands, getPlayerControlledActor())
	end
	
	/**
	 * Create helper containing default bindings.
	 */
	public func DefaultBindingHelper defaultBindings()
		var DefaultBindingHelper helper = super.defaultBindings()
		
		CommandsAAControllerInput.addDefaultBindings(helper)
		
		// remove mouse and keyboard bindings since we redirect them
		if helper.getMouse() != null
			helper.removeAllDeviceBindings(helper.getMouse())
		end
		if helper.getKeyboard() != null
			helper.removeAllDeviceBindings(helper.getKeyboard())
		end
		
		return helper
	end
	
	/**
	 * Create motion transfer factories.
	 */
	protected func void addMotionTransferFactories()
		pMotionTransferFactories.add(MotionTransferChild.Factory.new())
		pMotionTransferFactories.add(MotionTransferIK.Factory.new())
		pMotionTransferFactories.add(MotionTransferFingers.Factory.new())
		pMotionTransferFactories.add(MotionTransferSlot.Factory.new())
		pMotionTransferFactories.add(MotionTransferBend.Factory.new())
		pMotionTransferFactories.add(MotionTransferCopy.Factory.new())
		pMotionTransferFactories.add(MotionTransferSecondary.Factory.new())
		pMotionTransferFactories.add(MotionTransferAnimation.Factory.new())
	end
	
	/**
	 * Cleaning up.
	 */
	public func void cleanUp()
		if pWorldSpawnCharacter != null
			pWorldSpawnCharacter.dispose()
			pWorldSpawnCharacter = null
		end
		
		if pCharacterProfiles != null
			pCharacterProfiles.dispose()
			pCharacterProfiles = null
		end
		
		pActiveActorProfile = null
		if pActorProfiles != null
			pActorProfiles.dispose()
			pActorProfiles = null
		end
		
		pLoadCharacterProfile = null
		pLoadActorProfile = null
		pWindowMain = null
		pDMCWindowConsole = null
		
		super.cleanUp()
	end
	
	/** Finished loading game world. */
	public func void finishLoadGameWorld()
		super.finishLoadGameWorld()
		pWorldSpawnCharacter.setGameWorld(getWindowGameWorld().getGameWorld())
	end
	
	
	
	/** Get app instance for convenience. */
	static public func GameApp getGameApp()
		return getApp() cast GameApp
	end
	
	/** Actor profiles. */
	public func ActorProfiles getActorProfiles()
		return pActorProfiles
	end
	
	/** Active actor profile or null. */
	public func ActorProfile getActiveActorProfile()
		return pActiveActorProfile
	end
	
	/** Set active actor profile or null. */
	public func void setActiveActorProfile(ActorProfile profile)
		pActiveActorProfile = profile
	end
	
	/** Character profiles. */
	public func CharacterProfiles getCharacterProfiles()
		return pCharacterProfiles
	end
	
	
	
	/** Next unique element class ID generator. */
	public func MutableID getNextElementClassID()
		return pNextElementClassID
	end
	
	/** Motion transfer factories. */
	public func MotionTransferFactories getMotionTransferFactories()
		return pMotionTransferFactories
	end
	
	/** Load/save actor profile. */
	public func LoadActorProfile getLoadActorProfile()
		return pLoadActorProfile
	end
	
	/** Load/save character profile. */
	public func LoadCharacterProfile getLoadCharacterProfile()
		return pLoadCharacterProfile
	end
	
	/** World spawn character. */
	public func WorldSpawnCharacter getWorldSpawnCharacter()
		return pWorldSpawnCharacter
	end
end
