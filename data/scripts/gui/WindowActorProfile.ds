/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Actors

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Edit actor profile window.
 */
class WindowActorProfile extends WindowDialog
	var ActorProfile pActorProfile
	var DefaultTextModel pModelProfileName
	var DefaultTextModel pModelArmLength
	var DefaultTextModel pModelLegLength
	var DefaultTextModel pModelShoulderLevel
	var DefaultTextModel pModelShoulderWidth
	var DefaultTextModel pModelFootDistance
	var bool pInVR
	
	
	
	/** Create window. */
	func new(Window window, bool inVR, ActorProfile profile, ResultListener listener)
		pInVR = inVR
		pActorProfile = profile
		
		pModelProfileName = DefaultTextModel.new(profile.getName())
		pModelArmLength = DefaultTextModel.new(profile.getArmLength().toString())
		pModelLegLength = DefaultTextModel.new(profile.getLegLength().toString())
		pModelShoulderLevel = DefaultTextModel.new(profile.getShoulderLevel().toString())
		pModelShoulderWidth = DefaultTextModel.new(profile.getShoulderWidth().toString())
		pModelFootDistance = DefaultTextModel.new(profile.getFootDistance().toString())
		
		runWhileBlockingLayout(block
			setGuiTheme(window.getGuiTheme())
			setTitle("@UI.ActorProfile")
			setDesignerSelector("Window.Dialog.ActorProfile")
			
			createContent()
		end)
		
		setResultListener(listener)
		
		if inVR
			setPosition(Point.new())
			setSize(window.getDesktop().getSize())
			show(window, false, WindowModal.Placement.none)
			
		else
			show(window, true, WindowModal.Placement.desktop)
		end
	end
	
	/** Dispose of window. */
	func void dispose()
		pModelProfileName = null
		pModelArmLength = null
		pModelLegLength = null
		pModelShoulderLevel = null
		pModelShoulderWidth = null
		pModelFootDistance = null
		
		super.dispose()
	end
	
	
	
	/** Save dialog and close if no problems are found. */
	func void saveAndClose()
		// verify input
		var String profileName = pModelProfileName.getText()
		if not profileName.toLower().equals(pActorProfile.getName().toLower())
			if profileName.empty()
				WindowDialog.message(this, "@UI.SaveProfile",\
					"@UI.Message.ProfileNameEmpty", null, null, null)
				return
			end
			if GameApp.getGameApp().getActorProfiles().hasNamed(profileName)
				WindowDialog.message(this, "@UI.SaveProfile",\
					"@UI.Message.DuplicateProfile", null, null, null)
				return
			end
			if not FileSystem.canDeleteFile(pActorProfile.getPathProfile())
				WindowDialog.message(this, "@UI.SaveProfile",\
					"@UI.Message.RenameProfileFailed", null, null, null)
				return
			end
		end
		
		// save changes
		var String oldPathProfile = pActorProfile.getPathProfile()
		
		pActorProfile.setName(profileName)
		pActorProfile.setArmLength(pModelArmLength.getText().toFloat())
		pActorProfile.setLegLength(pModelLegLength.getText().toFloat())
		pActorProfile.setShoulderLevel(pModelShoulderLevel.getText().toFloat())
		pActorProfile.setShoulderWidth(pModelShoulderWidth.getText().toFloat())
		pActorProfile.setFootDistance(pModelFootDistance.getText().toFloat())
		
		pActorProfile.save()
		
		// if profile file name changed delete the old profile file
		if not pActorProfile.getPathProfile().equals(oldPathProfile) and FileSystem.existsFile(oldPathProfile)
			try
				FileSystem.deleteFile(oldPathProfile)
				
			catch Exception e
				GameApp.getGameApp().getConsole().addError("Delete profile", e)
				WindowDialog.message(this, "@UI.SaveProfile",\
					TranslationManager.get().translate("UI.Message.FailedDeleteOldProfile")\
						.toUTF8() + "\n" + e, null, null, null)
			end
		end
		
		// finish and close window
		setResult(true)
		close()
	end
	
	
	
	/** Create window content. */
	protected func void createContent()
		setLayout(BorderLayout.new(20))
		
		// actor image
		addWidget(DisplayImage.new(Image.new("/content/gui/images/actor_image.png"),\
			"Image.WindowActorProfile.Actor"), BorderLayout.Area.left)
		
		// properties
		var Button button
		
		addWidget(StaticGroup.new("@UI.ActorConfiguration",\
		"dialog_actorprofile", FormLayout.new(true, 10, 2), block Panel p
			var TextField textField
			
			// profile name
			p.addWidget(Label.new("@UI.ProfileName.Label", "Label.FormCaption"))
			
			textField = TextField.new(pModelProfileName, 15)
			textField.setDesignerSelector("TextField.WindowActorProfile")
			p.addWidget(textField)
			
			// shoulder level
			p.addWidget(Label.new("@UI.ShoulderLevel.Label", "Label.FormCaption"))
			
			textField = TextField.new(pModelShoulderLevel, 6)
			textField.setDesignerSelector("TextField.WindowActorProfile")
			textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.positiveNumber))
			textField.setToolTip("@UI.WindowActorProfile.ShoulderLevel.ToolTip")
			p.addWidget(textField)
			
			// shoulder width
			p.addWidget(Label.new("@UI.ShoulderWidth.Label", "Label.FormCaption"))
			
			textField = TextField.new(pModelShoulderWidth, 6)
			textField.setDesignerSelector("TextField.WindowActorProfile")
			textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.positiveNumber))
			textField.setToolTip("@UI.WindowActorProfile.ShoulderWidth.ToolTip")
			p.addWidget(textField)
			
			// arm length
			p.addWidget(Label.new("@UI.ArmLength.Label", "Label.FormCaption"))
			
			textField = TextField.new(pModelArmLength, 6)
			textField.setDesignerSelector("TextField.WindowActorProfile")
			textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.positiveNumber))
			textField.setToolTip("@UI.WindowActorProfile.ArmLength.ToolTip")
			p.addWidget(textField)
			
			// leg length
			p.addWidget(Label.new("@UI.LegLength.Label", "Label.FormCaption"))
			
			textField = TextField.new(pModelLegLength, 6)
			textField.setDesignerSelector("TextField.WindowActorProfile")
			textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.positiveNumber))
			textField.setToolTip("@UI.WindowActorProfile.LegLength.ToolTip")
			p.addWidget(textField)
			
			// foot width
			p.addWidget(Label.new("@UI.FootDistance.Label", "Label.FormCaption"))
			
			textField = TextField.new(pModelFootDistance, 6)
			textField.setDesignerSelector("TextField.WindowActorProfile")
			textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.positiveNumber))
			textField.setToolTip("@UI.WindowActorProfile.FootDistance.ToolTip")
			p.addWidget(textField)
			
		end), BorderLayout.Area.content)
		
		// button line
		addWidget(Panel.new(CentrizedBoxLayout.new(LayoutAxis.x), block Panel p
			button = Button.new("@UI.Save", BlockActionListener.new(block ActionEvent e
				saveAndClose()
			end))
			button.setDesignerSelector("Button.WindowActorProfile.Save")
			p.addWidget(button)
			
			button = Button.new("@UI.Cancel", CloseDialog.new(this, false))
			button.setDesignerSelector("Button.WindowActorProfile.Cancel")
			p.addWidget(button)
		end ), BorderLayout.Area.bottom )
	end
end
