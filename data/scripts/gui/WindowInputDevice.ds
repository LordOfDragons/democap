/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Dragengine.Commands
pin Dragengine.Gui.Borders
pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.LoadSave


/**
 * Input device window.
 */
class WindowInputDevice extends Window
	/** Input device renderer. */
	class InputDeviceRenderer extends DefaultListElementRenderer
		func new()
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, \
		bool selected, bool focused)
			if element != null
				var InputDevice device = element cast InputDevice
				super.updateRenderer(listBox, renderer, "{} ({})".format(Array.newWith(\
					device.getName(), device.getID())), selected, focused)
				
			else
				super.updateRenderer(listBox, renderer, " ", selected, focused)
			end
		end
	end
	
	/** Input device selection listener. */
	class SelectInputDevice extends DefaultListModelListener
		var WindowInputDevice pWindow
		
		func new(WindowInputDevice window)
			pWindow = window
		end
		
		func void selectionChanged(ListModel listModel)
			if listModel.getSelected() != -1
				pWindow.setInputDevice(listModel.getAt(listModel.getSelected()) cast InputDevice)
				
			else
				pWindow.setInputDevice(null)
			end
		end
	end
	
	/** Input device axis renderer. */
	class InputDeviceAxisRenderer extends DefaultListElementRenderer
		func new()
		end
		
		func Widget createRenderer(ListBox listBox)
			return AxisPanel.new()
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, \
		bool selected, bool focused)
			if element != null
				(renderer cast AxisPanel).setAxis(element cast InputDeviceAxis)
			end
		end
	end
	
	/** Axis panel. */
	class AxisPanel extends Panel
		private static fixed var WeightLayout.LayoutSettings column1 = WeightLayout.LayoutSettings.new(0, 100)
		private static fixed var WeightLayout.LayoutSettings column2 = WeightLayout.LayoutSettings.new(1)
		
		var InputDeviceAxis pAxis
		var TextModel pModelName
		var RangeModel pModelValue
		
		func new()
			pModelName = DefaultTextModel.new("?: ")
			pModelValue = DefaultRangeModel.new(-100, 100, 10, 0, 1)
			
			setLayout(WeightLayout.new(LayoutAxis.x, 5))
			
			addWidget(Label.new(pModelName, Label.middleLeft), column1)
			
			var ProgressBar bar = ProgressBar.new(Orientation.horizontal, pModelValue)
			bar.setDesignerSelector("ProgressBar.WindowInputDevice.Axis")
			addWidget(bar, column2)
			
			startFrameUpdater()
		end
		
		func void setAxis(InputDeviceAxis axis)
			pAxis = axis
			pModelName.setText(axis.getName())
		end
		
		func void onFrameUpdate(float elapsed)
			pModelValue.setValue((pAxis.getValue() * 100.0) cast int)
		end
	end
	
	/** Input device button renderer. */
	class InputDeviceButtonRenderer extends DefaultListElementRenderer
		func new()
		end
		
		func Widget createRenderer(ListBox listBox)
			return ButtonPanel.new()
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, \
		bool selected, bool focused)
			if element != null
				(renderer cast ButtonPanel).setButton(element cast InputDeviceButton)
			end
		end
	end
	
	/** Button panel. */
	class ButtonPanel extends Panel
		private static fixed var WeightLayout.LayoutSettings column1 = WeightLayout.LayoutSettings.new(0, 100)
		private static fixed var WeightLayout.LayoutSettings column2 = WeightLayout.LayoutSettings.new(1)
		private static fixed var WeightLayout.LayoutSettings column3 = WeightLayout.LayoutSettings.new(1)
		
		var InputDeviceButton pButton
		var TextModel pModelName
		var Label pLabelPressed
		var Label pLabelTouched
		
		func new()
			pModelName = DefaultTextModel.new("?: ")
			
			setLayout(WeightLayout.new(LayoutAxis.x, 5))
			
			addWidget(Label.new(pModelName, Label.middleLeft), column1)
			addWidget(pLabelPressed = Label.new("@UI.WindowInputDevice.Pressed", "Label.DeviceButtonReleased"), column2)
			addWidget(pLabelTouched = Label.new("@UI.WindowInputDevice.Touched", "Label.DeviceButtonUntouched"), column3)
			
			startFrameUpdater()
		end
		
		func void setButton(InputDeviceButton button)
			pButton = button
			pModelName.setText(button.getName())
		end
		
		func void onFrameUpdate(float elapsed)
			pLabelPressed.setDesignerSelector(pButton.isPressed() if "Label.DeviceButtonPressed" else "Label.DeviceButtonReleased")
			pLabelTouched.setDesignerSelector(pButton.isTouched() if "Label.DeviceButtonTouched" else "Label.DeviceButtonUntouched")
		end
	end
	
	/** Pose panel. */
	class PosePanel extends Panel
		var InputDevice pDevice
		var TextModel pModelPosition
		var TextModel pModelOrientation
		var TextModel pModelLinearVelocity
		var TextModel pModelAngularVelocity
		
		func new()
			pModelPosition = DefaultTextModel.new("(0,0,0)")
			pModelOrientation = DefaultTextModel.new("(0,0,0)")
			pModelLinearVelocity = DefaultTextModel.new("(0,0,0)")
			pModelAngularVelocity = DefaultTextModel.new("(0,0,0)")
			
			setLayout(GridLayout.new(4, 2))
			
			addWidget(Label.new("@UI.WindowInputDevice.Position"))
			addWidget(Label.new("@UI.WindowInputDevice.Rotation"))
			addWidget(Label.new("@UI.WindowInputDevice.LinearVelocity"))
			addWidget(Label.new("@UI.WindowInputDevice.AngularVelocity"))
			addWidget(Label.new(pModelPosition, "Label.WindowInputDevice.VectorValue"))
			addWidget(Label.new(pModelOrientation, "Label.WindowInputDevice.VectorValue"))
			addWidget(Label.new(pModelLinearVelocity, "Label.WindowInputDevice.VectorValue"))
			addWidget(Label.new(pModelAngularVelocity, "Label.WindowInputDevice.VectorValue"))
			
			startFrameUpdater()
		end
		
		func void dispose()
			pModelPosition = null
			pModelOrientation = null
			pModelLinearVelocity = null
			pModelAngularVelocity = null
			super.dispose()
		end
		
		func void setDevice(InputDevice device)
			pDevice = device
		end
		
		func void onFrameUpdate(float elapsed)
			if pDevice != null
				var Vector v = pDevice.getDevicePosePosition()
				pModelPosition.setText("({:+06.3f}, {:+06.3f}, {:+06.3f})".format(\
					Array.newWith(v.getX(), v.getY(), v.getZ())))
				
				v = pDevice.getDevicePoseOrientation().getEulerAngles()
				pModelOrientation.setText("({:+06.1f}, {:+06.1f}, {:+06.1f})°".format(\
					Array.newWith(v.getX(), v.getY(), v.getZ())))
				
				v = pDevice.getDevicePoseLinearVelocity()
				pModelLinearVelocity.setText("({:+05.2f}, {:+05.2f}, {:+05.2f})".format(\
					Array.newWith(v.getX(), v.getY(), v.getZ())))
				
				v = pDevice.getDevicePoseAngularVelocity()
				var float speed = v.getLength()
				v = speed > 0.01 if v.normalize() else Vector.new()
// 				pModelAngularVelocity.setText("({:+06.1f}: {:+5.2f}, {:+5.2f}, {:+5.2f})".format(\
// 					Array.newWith(speed, v.getX(), v.getY(), v.getZ())))
				pModelAngularVelocity.setText("{:+05.0f}°".format(Array.newWith(speed)))
				
			else
				pModelPosition.setText("")
				pModelOrientation.setText("")
				pModelLinearVelocity.setText("")
				pModelAngularVelocity.setText("")
			end
		end
	end
	
	/** Face panel. */
	class FacePanel extends Panel
		var InputDevice pDevice
		var Dictionary pExpressions //!< int -> RangeModel
		
		func new()
			pExpressions = Dictionary.new()
			
			runWhileBlockingLayout(block
				setLayout(GridLayout.new(3, 0))
				
				addExpression(InputDeviceFaceExpression.eyeLeftBlink, "@UI.WindowInputDevice.Expression.EyeLeftBlink")
				addExpression(InputDeviceFaceExpression.eyeLeftWide, "@UI.WindowInputDevice.Expression.EyeLeftWide")
				addExpression(InputDeviceFaceExpression.eyeLeftSqueeze, "@UI.WindowInputDevice.Expression.EyeLeftSqueeze")
				addExpression(InputDeviceFaceExpression.eyeLeftDown, "@UI.WindowInputDevice.Expression.EyeLeftDown")
				addExpression(InputDeviceFaceExpression.eyeLeftUp, "@UI.WindowInputDevice.Expression.EyeLeftUp")
				addExpression(InputDeviceFaceExpression.eyeLeftIn, "@UI.WindowInputDevice.Expression.EyeLeftIn")
				addExpression(InputDeviceFaceExpression.eyeLeftOut, "@UI.WindowInputDevice.Expression.EyeLeftOut")
				
				addExpression(InputDeviceFaceExpression.eyeRightBlink, "@UI.WindowInputDevice.Expression.EyeRightBlink")
				addExpression(InputDeviceFaceExpression.eyeRightWide, "@UI.WindowInputDevice.Expression.EyeRightWide")
				addExpression(InputDeviceFaceExpression.eyeRightSqueeze, "@UI.WindowInputDevice.Expression.EyeRightSqueeze")
				addExpression(InputDeviceFaceExpression.eyeRightDown, "@UI.WindowInputDevice.Expression.EyeRightDown")
				addExpression(InputDeviceFaceExpression.eyeRightUp, "@UI.WindowInputDevice.Expression.EyeRightUp")
				addExpression(InputDeviceFaceExpression.eyeRightIn, "@UI.WindowInputDevice.Expression.EyeRightIn")
				addExpression(InputDeviceFaceExpression.eyeRightOut, "@UI.WindowInputDevice.Expression.EyeRightOut")
				
				addExpression(InputDeviceFaceExpression.jawRight, "@UI.WindowInputDevice.Expression.JawRight")
				addExpression(InputDeviceFaceExpression.jawLeft, "@UI.WindowInputDevice.Expression.JawLeft")
				addExpression(InputDeviceFaceExpression.jawForward, "@UI.WindowInputDevice.Expression.JawForward")
				addExpression(InputDeviceFaceExpression.jawOpen, "@UI.WindowInputDevice.Expression.JawOpen")
				
				addExpression(InputDeviceFaceExpression.cheekPuffRight, "@UI.WindowInputDevice.Expression.CheekPuffRight")
				addExpression(InputDeviceFaceExpression.cheekPuffLeft, "@UI.WindowInputDevice.Expression.CheekPuffLeft")
				addExpression(InputDeviceFaceExpression.cheekSuck, "@UI.WindowInputDevice.Expression.CheekSuck")
				
				addExpression(InputDeviceFaceExpression.mouthApeShape, "@UI.WindowInputDevice.Expression.MouthApeShape")
				addExpression(InputDeviceFaceExpression.mouthUpperRight, "@UI.WindowInputDevice.Expression.MouthUpperRight")
				addExpression(InputDeviceFaceExpression.mouthUpperLeft, "@UI.WindowInputDevice.Expression.MouthUpperLeft")
				addExpression(InputDeviceFaceExpression.mouthUpperUpRight, "@UI.WindowInputDevice.Expression.MouthUpperUpRight")
				addExpression(InputDeviceFaceExpression.mouthUpperUpLeft, "@UI.WindowInputDevice.Expression.MouthUpperUpLeft")
				addExpression(InputDeviceFaceExpression.mouthUpperOverturn, "@UI.WindowInputDevice.Expression.MouthUpperOverturn")
				addExpression(InputDeviceFaceExpression.mouthUpperInside, "@UI.WindowInputDevice.Expression.MouthUpperInside")
				addExpression(InputDeviceFaceExpression.mouthLowerRight, "@UI.WindowInputDevice.Expression.MouthLowerRight")
				addExpression(InputDeviceFaceExpression.mouthLowerLeft, "@UI.WindowInputDevice.Expression.MouthLowerLeft")
				addExpression(InputDeviceFaceExpression.mouthLowerDownRight, "@UI.WindowInputDevice.Expression.MouthLowerDownRight")
				addExpression(InputDeviceFaceExpression.mouthLowerDownLeft, "@UI.WindowInputDevice.Expression.MouthLowerDownLeft")
				addExpression(InputDeviceFaceExpression.mouthLowerOverturn, "@UI.WindowInputDevice.Expression.MouthLowerOverturn")
				addExpression(InputDeviceFaceExpression.mouthLowerInside, "@UI.WindowInputDevice.Expression.MouthLowerInside")
				addExpression(InputDeviceFaceExpression.mouthLowerOverlay, "@UI.WindowInputDevice.Expression.MouthLowerOverlay")
				addExpression(InputDeviceFaceExpression.mouthPout, "@UI.WindowInputDevice.Expression.MouthPout")
				addExpression(InputDeviceFaceExpression.mouthSmileRight, "@UI.WindowInputDevice.Expression.MouthSmileRight")
				addExpression(InputDeviceFaceExpression.mouthSmileLeft, "@UI.WindowInputDevice.Expression.MouthSmileLeft")
				addExpression(InputDeviceFaceExpression.mouthSadRight, "@UI.WindowInputDevice.Expression.MouthSadRight")
				addExpression(InputDeviceFaceExpression.mouthSadLeft, "@UI.WindowInputDevice.Expression.MouthSadLeft")
				
				addExpression(InputDeviceFaceExpression.tongueLeft, "@UI.WindowInputDevice.Expression.TongueLeft")
				addExpression(InputDeviceFaceExpression.tongueRight, "@UI.WindowInputDevice.Expression.TongueRight")
				addExpression(InputDeviceFaceExpression.tongueUp, "@UI.WindowInputDevice.Expression.TongueUp")
				addExpression(InputDeviceFaceExpression.tongueDown, "@UI.WindowInputDevice.Expression.TongueDown")
				addExpression(InputDeviceFaceExpression.tongueRoll, "@UI.WindowInputDevice.Expression.TongueRoll")
				addExpression(InputDeviceFaceExpression.tongueLongStep1, "@UI.WindowInputDevice.Expression.TongueLongStep1")
				addExpression(InputDeviceFaceExpression.tongueLongStep2, "@UI.WindowInputDevice.Expression.TongueLongStep2")
			end)
			
			startFrameUpdater()
		end
		
		func void dispose()
			pExpressions = null
			super.dispose()
		end
		
		func void setDevice(InputDevice device)
			if device != pDevice
				pDevice = device
				
				if device == null
					pExpressions.forEach(block int index, RangeModel each
						each.setValue(0)
					end)
				end
			end
		end
		
		func void onFrameUpdate(float elapsed)
			if pDevice != null
				var int count = pDevice.getDeviceFaceExpressionCount()
				pExpressions.forEach(block int index, RangeModel model
					if index < count
						model.setValue((pDevice.getDeviceFaceExpressionAt(index) * 100) cast int)
					else
						model.setValue(0)
					end
				end)
			end
		end
		
		private func void addExpression(int index, String label)
			addWidget(Panel.new(StackLayout.new(), block Panel p
				var DefaultRangeModel model = DefaultRangeModel.new(0, 100, 10, 0, 1)
				var ProgressBar bar = ProgressBar.new(Orientation.horizontal, model)
				bar.setDesignerSelector("ProgressBar.WindowInputDevice.FaceExpression")
				p.addWidget(bar)
				pExpressions.setAt(index, model)
				p.addWidget(Label.new(label, "Label.WindowInputDevice.FaceExpression"))
			end))
		end
	end
	
	/** Device changes listener. */
	class DeviceChangeListener extends DefaultBindingManagerListener
		var WindowInputDevice pWindow
		
		func new(WindowInputDevice window)
			pWindow = window
		end
		
		func void devicesReloaded(BindingManager manager)
			pWindow.updateInputDeviceModel()
		end
	end
	
	
	
	var DeviceChangeListener pDeviceChangeListener
	var DefaultListModel pModelInputDevices
	var ListBox pListInputDevices
	var InputDevice pInputDevice //<! Active input device
	var DefaultListModel pModelAxes
	var DefaultListModel pModelButtons
	var Panel pPanelAxes, pPanelButtons, pPanelFaceCont
	var PosePanel pPanelPose
	var FacePanel pPanelFace
	
	
	
	/** Create window. */
	func new() super(0, 0, 800, 500)
		pModelInputDevices = DefaultListModel.new()
		pModelAxes = DefaultListModel.new()
		pModelButtons = DefaultListModel.new()
		
		runWhileBlockingLayout(block
			setDesignerSelector("WindowInputDevice")
			setTitle("@UI.WindowInputDevice.Title")
			createContent(this)
		end)
		
		updateInputDeviceModel()
		
		pDeviceChangeListener = DeviceChangeListener.new(this)
		BaseGameApp.getApp().getBindingManager().addListener(pDeviceChangeListener)
	end
	
	/** Dispose of window. */
	func void dispose()
		if pDeviceChangeListener != null
			BaseGameApp.getApp().getBindingManager().removeListener(pDeviceChangeListener)
			pDeviceChangeListener = null
		end
		
		pInputDevice = null
		pModelButtons = null
		pModelAxes = null
		pModelInputDevices = null
		pListInputDevices = null
		pPanelPose = null
		pPanelFace = null
		pPanelAxes = null
		pPanelFaceCont = null
		pPanelButtons = null
		
		super.dispose()
	end
	
	
	
	/** Create content. */
	private func void createContent(Container content)
		content.setLayout(BorderLayout.new(5))
		
		// input device selection
		content.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 10, FlowLayout.Stretch.last), block Panel panel
			pListInputDevices = ListBox.new(pModelInputDevices, InputDeviceRenderer.new())
			pListInputDevices.setNumberDisplayLines(6)
			pModelInputDevices.addListener(SelectInputDevice.new(this))
			panel.addWidget(Label.new("@UI.WindowInputDevice.InputDevice", Label.left | Label.top))
			panel.addWidget(ScrollPanel.new(pListInputDevices))
		end), BorderLayout.Area.top)
		
		// content with list of axes and button states
		content.addWidget(Panel.new(BoxLayout.new(LayoutAxis.x, 10), block Panel panel
			pPanelAxes = Panel.new(BorderLayout.new(3), block Panel subPanel
				subPanel.addWidget(Label.new("@UI.WindowInputDevice.Axes", Label.center), BorderLayout.Area.top)
				subPanel.addWidget(ScrollPanel.new(ListBox.new(\
					pModelAxes, InputDeviceAxisRenderer.new())), BorderLayout.Area.content)
			end)
			panel.addWidget(pPanelAxes)
			
			pPanelButtons = Panel.new(BorderLayout.new(3), block Panel subPanel
				subPanel.addWidget(Label.new("@UI.WindowInputDevice.Buttons", Label.center), BorderLayout.Area.top)
				subPanel.addWidget(ScrollPanel.new(ListBox.new(\
					pModelButtons, InputDeviceButtonRenderer.new())), BorderLayout.Area.content)
			end)
			panel.addWidget(pPanelButtons)
			
			pPanelFaceCont = Panel.new(BorderLayout.new(3), block Panel subPanel
				subPanel.addWidget(Label.new("@UI.WindowInputDevice.FaceExpressions", Label.center), BorderLayout.Area.top)
				subPanel.addWidget(ScrollPanel.new(pPanelFace = FacePanel.new(),\
					ScrollPanel.Policy.hidden, ScrollPanel.Policy.needed), BorderLayout.Area.content)
				subPanel.setVisible(false)
			end)
			panel.addWidget(pPanelFaceCont)
		end), BorderLayout.Area.content)
		
		// poses
		content.addWidget(Panel.new(FlowLayout.new(LayoutAxis.y, 10), block Panel panel
			panel.addWidget(pPanelPose = PosePanel.new())
		end), BorderLayout.Area.bottom)
	end
	
	/** Update input device list. */
	func void updateInputDeviceModel()
		var InputDevice selection = pInputDevice
		var Array content = Array.new()
		
		BaseGameApp.getApp().getBindingManager().forEachDevice(block InputDevice each
			content.add(each)
		end)
		
		pModelInputDevices.setContent(content)
		pModelInputDevices.setSelected(Math.max(content.indexOf(selection), 0))
	end
	
	/** Set input device to show. */
	func void setInputDevice(InputDevice inputDevice)
		if inputDevice == pInputDevice
			return
		end
		
		pInputDevice = inputDevice
		
		runWhileBlockingLayout(block
			pPanelAxes.setVisible(false)
			pPanelButtons.setVisible(false)
			pPanelFaceCont.setVisible(false)
			pPanelPose.setDevice(null)
			pPanelFace.setDevice(null)
			pModelAxes.removeAll()
			pModelButtons.removeAll()
			
			if inputDevice != null
				select inputDevice.getType()
				case InputDeviceType.vrHMD
					pPanelFace.setDevice(inputDevice)
					pPanelPose.setDevice(inputDevice)
					pPanelFaceCont.setVisible(true)
					
				case InputDeviceType.keyboard
					// ignore keyboard because it has lots of keys (buttons)
					
				else
					addDeviceAxesAbsolute(inputDevice)
					addDeviceButtons(inputDevice)
					pPanelPose.setDevice(inputDevice)
					pPanelAxes.setVisible(true)
					pPanelButtons.setVisible(true)
				end
			end
		end)
		doLayout()
	end
	
	protected func void addDeviceAxesAbsolute(InputDevice inputDevice)
		var int i, count = inputDevice.getAxisCount()
		var Array list = Array.new()
		
		for i = 0 to count
			var InputDeviceAxis axis = inputDevice.getAxisAt(i)
			
			select axis.getType()
			case InputDeviceAxisType.mouse, InputDeviceAxisType.mouseWheel
				// not showing absolute axes
			else
				list.add(axis)
			end
		end
		
		pModelAxes.setContent(list)
	end
	
	protected func void addDeviceButtons(InputDevice inputDevice)
		var Array list = Array.new()
		var int i, count = inputDevice.getButtonCount()
		
		for i = 0 to count
			list.add(inputDevice.getButtonAt(i))
		end
		
		pModelButtons.setContent(list)
	end
end
