/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors

pin Dragengine.CameraDirectors
pin Dragengine.Gui.Events
pin Dragengine.Scenery


/**
 * Interact mouse listener. For use by operator on WindowGameWorld and only if VR camera
 * is not enabled.
 */
class InteractMouseListener extends DefaultMouseListener
	/** Hit result. */
	class HitResult
		var BehaviorElement pElement
		var float pDistance
		var DVector pHitPoint
		
		func new(BehaviorElement element, float distance, DVector hitPoint)
			pElement = element
			pDistance = distance
			pHitPoint = hitPoint
		end
		
		func BehaviorElement getElement()
			return pElement
		end
		
		func float getDistance()
			return pDistance
		end
		
		func DVector getHitPoint()
			return pHitPoint
		end
		
		func int compare(Object obj)
			if obj castable HitResult
				return pDistance.compare((obj cast HitResult).pDistance)
				
			else
				return 0
			end
		end
	end
	
	/**
	 * Collect hits. Includes only BehaviorElement.
	 */
	class CollectHits implements ColliderListener
		protected var Array pHits
		protected var DVector pRayOrigin
		protected var DVector pRayDirection
		
		
		func new(Array hits, DVector rayOrigin, Vector rayDirection)
			pHits = hits
			pRayOrigin = rayOrigin
			pRayDirection = DVector.new(rayDirection)
		end
		
		
		func bool canHitCollider(Collider owner, Collider collider)
			return true
		end
		
		func void collisionResponse(Collider owner, CollisionInfo info)
			var Collider collider = info.getCollider()
			if collider == null
				return
			end
			
			var Object hitOwner = collider.getOwner()
			if hitOwner == null or not hitOwner castable BehaviorElement
				return
			end
			
			pHits.add(HitResult.new(hitOwner cast BehaviorElement, info.getDistance(),\
				pRayOrigin + pRayDirection * info.getDistance()))
		end
		
		func void colliderChanged(Collider owner)
		end
	end
	
	
	
	protected var CollisionFilter pCollisionFilter
	protected var float pRayDistance
	protected var ECBInteractHandle.Instance pHandle
	protected var ECBShowInteractHandles.Instance pShowHandles
	protected var ECBehaviorControlDesktop.Instance pControlDesktop
	protected var bool pDragging
	
	
	
	/** Create mouse listener. */
	func new()
		var LayerMask cfcategory = LayerMask.newWith(GameApp.CollisionFilterBit.interaction)
		
		var LayerMask cffilter = LayerMask.new()
		cffilter.setBit(GameApp.CollisionFilterBit.interaction)
		cffilter.setBit(GameApp.CollisionFilterBit.geometry)
		cffilter.setBit(GameApp.CollisionFilterBit.dynamic)
		cffilter.setBit(GameApp.CollisionFilterBit.actor)
		
		pCollisionFilter = CollisionFilter.new(cfcategory, cffilter)
		
		pRayDistance = 100
	end
	
	/** Create mouse listener. */
	func new(CollisionFilter collisionFilter, float rayDistance)
		pCollisionFilter = collisionFilter
		pRayDistance = rayDistance
	end
	
	
	
	/** Mouse button pressed. */
	func void onButtonPress(MouseEvent event)
		// already interacting
		if pHandle != null or pDragging
			return
		end
		
		// requires left mouse button, no VR camera shown and camera director to be present
		if event.getButton() != InputEventMouseButton.left
			return
		end
		
		var GameApp app = GameApp.getGameApp()
		if SessionSettings.get().getModelShowVRCamera().getToggled() or SessionSettings.get().getViewCamera() != null
			return
		end
		
		var WindowGameWorld window = app.getWindowGameWorld()
		var CameraDirector director = window.getCameraDirector()
		if director == null
			return
		end
		
		// if hovering over a control desktop update mouse to be on the save side
		var Camera camera = director.getCamera()
		var DVector rayOrigin = camera.getPosition()
		var Vector rayNormDir = window.backProject(event)
		var Vector rayDirection = rayNormDir * pRayDistance
		
		if pControlDesktop != null
			pControlDesktop.moveMouseRay(camera.getPosition(), DVector.new(rayNormDir))
			pControlDesktop = null
		end
		
		// ray cast to find all hit elements sorted by distance
		var Array hits = Array.new()
		var CollectHits listener = CollectHits.new(hits, rayOrigin, rayDirection)
		
		window.getGameWorld().getWorld().rayHits(rayOrigin, rayDirection, listener, pCollisionFilter)
		hits.sort()
		
		// find the closest hit object supporting handle or selectable interaction and use it
		if hits.find(block HitResult each
			var BehaviorElement element = each.getElement()
			
			if ECBInteractionBubble.getInstanceIn(element) != null
				pControlDesktop = ECBehaviorControlDesktop.getInstanceIn(element)
				if pControlDesktop != null
					pControlDesktop.moveMouseEverywhere(each.getHitPoint())
					pControlDesktop.leftMousePress()
					pDragging = true
					return true
				end
			end
			
			pHandle = ECBInteractHandle.getInstanceIn(element)
			if pHandle != null
				pShowHandles = ECBShowInteractHandles.getInstanceIn(element.getParent() cast BehaviorElement)
				pHandle.startInteract(Matrix.newFromQuaternion(camera.getOrientation()),\
					rayOrigin, rayNormDir, each.getHitPoint())
				if pShowHandles != null
					pShowHandles.notifyStartInteract(pHandle)
				end
				return true
			end
			
			var ECBSelectable.Instance selectable = ECBSelectable.getInstanceIn(element)
			if selectable != null
				app.getSelectionTracker().setElement(selectable)
				return true
			end
			
			return false
		end) == null
			// nothing hit. clear selection
			app.getSelectionTracker().setElement(null)
		end
	end
	
	/** Mouse moved. */
	func void onMouseMove(MouseEvent event)
		var bool showVRCamera = SessionSettings.get().getModelShowVRCamera().getToggled()
		var bool showRemoteCamera = SessionSettings.get().getViewCamera() != null
		var GameApp app = GameApp.getGameApp()
		var WindowGameWorld window = app.getWindowGameWorld()
		var CameraDirector director = window.getCameraDirector()
		
		if pHandle != null
			if showVRCamera or showRemoteCamera or director == null
				pHandle.stopInteract()
				if pShowHandles != null
					pShowHandles.notifyStopInteract(pHandle)
				end
				pHandle = null
				pShowHandles = null
				return
			end
			
			var Camera camera = director.getCamera()
			pHandle.updateInteract(Matrix.newFromQuaternion(camera.getOrientation()),\
				camera.getPosition(), window.backProject(event).normalize())
			
		elif pDragging
			if showVRCamera or showRemoteCamera or director == null
				onButtonRelease(event)
				return
			end
			
			if pControlDesktop != null
				var Camera camera = director.getCamera()
				pControlDesktop.moveMouseRay(camera.getPosition(), DVector.new(window.backProject(event)))
			end
			
		elif not showVRCamera and not showRemoteCamera
			if director == null
				pControlDesktop = null
				return
			end
			
			var Camera camera = director.getCamera()
			var DVector rayOrigin = camera.getPosition()
			var Vector rayNormDir = window.backProject(event)
			var Vector rayDirection = rayNormDir * pRayDistance
			var Array hits = Array.new()
			var CollectHits listener = CollectHits.new(hits, rayOrigin, rayDirection)
			var ECBehaviorControlDesktop.Instance oldControlDesktop = pControlDesktop
			
			pControlDesktop = null
			
			window.getGameWorld().getWorld().rayHits(rayOrigin, rayDirection, listener, pCollisionFilter)
			hits.sort()
			
			hits.find(block HitResult each
				var BehaviorElement element = each.getElement()
				if ECBInteractionBubble.getInstanceIn(element) != null
					pControlDesktop = ECBehaviorControlDesktop.getInstanceIn(element)
					pControlDesktop.moveMouseEverywhere(each.getHitPoint())
					return true
				end
				return false
			end)
			
			if pControlDesktop != oldControlDesktop and oldControlDesktop != null and oldControlDesktop.getElement() != null
				oldControlDesktop.moveMouseRay(camera.getPosition(), DVector.new(rayNormDir))
			end
		end
	end
	
	/** Mouse button released. */
	func void onButtonRelease(MouseEvent event)
		if event.getButton() != InputEventMouseButton.left
			return
		end
		
		if pHandle != null
			pHandle.stopInteract()
			if pShowHandles != null
				pShowHandles.notifyStopInteract(pHandle)
			end
			pHandle = null
			pShowHandles = null
			
		elif pDragging
			if pControlDesktop != null and pControlDesktop.getElement() != null
				pControlDesktop.leftMouseRelease()
			end
			pDragging = false
		end
	end
end
