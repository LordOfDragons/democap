/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2023, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors
pin Democap.Characters
pin Democap.MotionTransferSystem

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery


/**
 * Tweak panel motion transfer.
 */
class WMPPTMTIK extends WMPPMotionTransfer
	class ReachScalingChanged implements ActionListener
		protected var WMPPTMTIK pPanel
		
		func new(WMPPTMTIK panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			pPanel.onReachScalingChanged()
		end
	end
	
	class ReachScalingBackChanged implements ActionListener
		protected var WMPPTMTIK pPanel
		
		func new(WMPPTMTIK panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			pPanel.onReachScalingBackChanged()
		end
	end
	
	
	
	
	var WMPPTweakValue pEditReachScalingX
	var WMPPTweakValue pEditReachScalingY
	var WMPPTweakValue pEditReachScalingZ
	var WMPPTweakValue pEditReachScalingBackX
	var WMPPTweakValue pEditReachScalingBackY
	var WMPPTweakValue pEditReachScalingBackZ
	
	
	
	/** Create panel. */
	func new(WMPPanelTweaking panelTweaking) super(panelTweaking)
		pModelTypeName.setText("@UI.InverseKinematic")
		
		runWhileBlockingLayout(block
			createContent()
		end)
	end
	
	/** Dispose of panel. */
	func void dispose()
		pEditReachScalingX = null
		pEditReachScalingY = null
		pEditReachScalingZ = null
		pEditReachScalingBackX = null
		pEditReachScalingBackY = null
		pEditReachScalingBackZ = null
		super.dispose()
	end
	
	
	
	func void onTweaked()
		if pMotionTransfer == null
			return
		end
		
		runWhileBlockingLayout(block
			var MotionTransferIK mt = pMotionTransfer cast MotionTransferIK
			
			var Vector v = mt.getReachScaling()
			pEditReachScalingX.setValue(v.getX())
			pEditReachScalingY.setValue(v.getY())
			pEditReachScalingZ.setValue(v.getZ())
			
			v = mt.getReachScalingBack()
			pEditReachScalingBackX.setValue(v.getX())
			pEditReachScalingBackY.setValue(v.getY())
			pEditReachScalingBackZ.setValue(v.getZ())
		end)
		doLayoutIfBlocked()
	end
	
	func void onReachScalingChanged()
		var MotionTransferIK mt = pMotionTransfer cast MotionTransferIK
		if mt != null
			var Vector v = Vector.new(pEditReachScalingX.getValue(),\
				pEditReachScalingY.getValue(), pEditReachScalingZ.getValue())
			if not mt.getReachScaling().equals(v)
				mt.setReachScaling(v)
				mt.tweaked()
				pPanelTweaking.notifyCharacterTweaked()
			end
		end
	end
	
	func void onReachScalingBackChanged()
		var MotionTransferIK mt = pMotionTransfer cast MotionTransferIK
		if mt != null
			var Vector v = Vector.new(pEditReachScalingBackX.getValue(),\
				pEditReachScalingBackY.getValue(), pEditReachScalingBackZ.getValue())
			if not mt.getReachScalingBack().equals(v)
				mt.setReachScalingBack(v)
				mt.tweaked()
				pPanelTweaking.notifyCharacterTweaked()
			end
		end
	end
	
	/** Supports motion transfer type. */
	func bool supports(MotionTransfer motionTranfer)
		return motionTranfer castable MotionTransferIK
	end
	
	
	
	/** Create window content. */
	protected func void createContent()
		var Label label
		var String toolTip
		
		createBaseContent("pct_ik")
		
		// reach scaling
		toolTip = "@UI.ToolTip.ReachScalingTwistAxis"
		
		label = Label.new("@UI.ReachScaling.Label", "Label.FormCaption")
		label.setToolTip(toolTip)
		pPanelForm.addWidget(label)
		
		pPanelForm.addWidget(Panel.new(FlowLayout.new(LayoutAxis.y, 1), block Panel p
			var ReachScalingChanged action = ReachScalingChanged.new(this)
			pEditReachScalingX = WMPPTweakValue.new(4, 0.001, 0.01, 0.1, action)
			p.addWidget(pEditReachScalingX)
			pEditReachScalingY = WMPPTweakValue.new(4, 0.001, 0.01, 0.1, action)
			p.addWidget(pEditReachScalingY)
			pEditReachScalingZ = WMPPTweakValue.new(4, 0.001, 0.01, 0.1, action)
			p.addWidget(pEditReachScalingZ)
		end))
		
		// reach scaling backwards
		toolTip = "@UI.ToolTip.ReachScalingTwistAxisBack"
		
		label = Label.new("@UI.ReachScalingBack.Label", "Label.FormCaption")
		label.setToolTip(toolTip)
		pPanelForm.addWidget(label)
		
		pPanelForm.addWidget(Panel.new(FlowLayout.new(LayoutAxis.y, 1), block Panel p
			var ReachScalingBackChanged action = ReachScalingBackChanged.new(this)
			pEditReachScalingBackX = WMPPTweakValue.new(4, 0.001, 0.01, 0.1, action)
			p.addWidget(pEditReachScalingBackX)
			pEditReachScalingBackY = WMPPTweakValue.new(4, 0.001, 0.01, 0.1, action)
			p.addWidget(pEditReachScalingBackY)
			pEditReachScalingBackZ = WMPPTweakValue.new(4, 0.001, 0.01, 0.1, action)
			p.addWidget(pEditReachScalingBackZ)
		end))
	end
end
