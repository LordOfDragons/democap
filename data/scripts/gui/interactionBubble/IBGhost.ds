/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2022, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors
pin Dragengine.Gui.Events
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Interaction bubble button temporary ghost actor.
 */
class IBGhost extends InteractionBubbleToggleButton
	/** Factory. */
	class Factory implements ECBInteractionBubble.ButtonFactory
		protected var Image pImageOn
		protected var Image pImageOff
		
		public func new()
			pImageOn = Image.new("/content/gui/images/interactionBubble/icons/ghost_on.png")
			pImageOff = Image.new("/content/gui/images/interactionBubble/icons/ghost_off.png")
		end
		
		public func void createAndAddButtons(ECBInteractionBubble.Instance instance)
			instance.getPanelButtonsSelected().addWidget(IBGhost.new(instance, pImageOn, pImageOff, false))
			instance.getPanelButtonsDeselected().addWidget(IBGhost.new(instance, pImageOn, pImageOff, true))
		end
	end
	
	class EnabledChanged extends ECBTemporaryGhost.DefaultListener
		protected var IBGhost pOwner
		
		public func new(IBGhost owner)
			pOwner = owner
		end
		
		public func void enabledChanged(ECBTemporaryGhost.Instance instance)
			pOwner.updateImage(true)
		end
	end
	
	
	protected var ECBTemporaryGhost.Instance pTemporaryGhost
	protected var EnabledChanged pEnabledChanged
	
	
	/** Create button. */
	public func new(ECBInteractionBubble.Instance bubble, Image imageOn, Image imageOff, bool hideIfOff)\
	super(bubble, imageOn, imageOff, hideIfOff)
		pTemporaryGhost = ECBTemporaryGhost.getInstanceIn(getElement())
		pEnabledChanged = EnabledChanged.new(this)
		pTemporaryGhost.addListener(pEnabledChanged)
		
		updateImage(false)
		
		setToolTip("Show using ghost textures")
	end
	
	/** Dispose of widget. */
	public func void dispose()
		if pEnabledChanged != null
			pTemporaryGhost.removeListener(pEnabledChanged)
			pEnabledChanged = null
		end
		
		super.dispose()
	end
	
	/** Determine if button should be toggled or not. Subclass responsibility. */
	protected func bool getToggled()
		return pTemporaryGhost.getEnabled()
	end
	
	/** Fire action. */
	public func void fireAction()
		if pTemporaryGhost != null
			pTemporaryGhost.setEnabled(not pTemporaryGhost.getEnabled())
		end
	end
end
