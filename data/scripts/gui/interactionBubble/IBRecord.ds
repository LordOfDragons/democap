/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2022, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors
pin Dragengine.Gui.Events
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Interaction bubble button record actor.
 */
class IBRecord extends InteractionBubbleToggleButton
	/** Factory. */
	class Factory implements ECBInteractionBubble.ButtonFactory
		protected var Image pImageOn
		protected var Image pImageOff
		
		func new()
			pImageOn = Image.new("/content/gui/images/interactionBubble/icons/record_on.png")
			pImageOff = Image.new("/content/gui/images/interactionBubble/icons/record_off.png")
		end
		
		func void createAndAddButtons(ECBInteractionBubble.Instance instance)
			instance.getPanelButtonsSelected().addWidget(IBRecord.new(instance, pImageOn, pImageOff, false))
			instance.getPanelButtonsDeselected().addWidget(IBRecord.new(instance, pImageOn, pImageOff, true))
		end
	end
	
	class RecordChanged extends ECBCapturedAnimations.DefaultListener
		protected var IBRecord pOwner
		
		func new(IBRecord owner)
			pOwner = owner
		end
		
		func void recordChanged(ECBCapturedAnimations.Instance instance)
			pOwner.updateImage(true)
		end
	end
	
	
	protected var ECBCapturedAnimations.Instance pCapturedAnimations
	protected var RecordChanged pRecordChanged
	
	
	/** Create button. */
	func new(ECBInteractionBubble.Instance bubble, Image imageOn, Image imageOff, bool hideIfOff)\
	super(bubble, imageOn, imageOff, hideIfOff)
		pCapturedAnimations = ECBCapturedAnimations.getInstanceIn(getElement())
		pRecordChanged = RecordChanged.new(this)
		pCapturedAnimations.addListener(pRecordChanged)
		
		updateImage(false)
		
		setToolTip("@UI.ToolTip.RecordObjectCapturing")
	end
	
	/** Dispose of widget. */
	func void dispose()
		if pRecordChanged != null
			pCapturedAnimations.removeListener(pRecordChanged)
			pRecordChanged = null
		end
		
		super.dispose()
	end
	
	/** Determine if button should be toggled or not. Subclass responsibility. */
	protected func bool getToggled()
		return pCapturedAnimations.getRecord()
	end
	
	/** Fire action. */
	func void fireAction()
		TimerBlock.new(0, false, block
			if pCapturedAnimations != null
				pCapturedAnimations.setRecord(not pCapturedAnimations.getRecord())
			end
		end)
	end
end
