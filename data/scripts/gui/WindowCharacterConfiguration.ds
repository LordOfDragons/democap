/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Actors
pin Democap.Behaviors
pin Democap.Characters
pin Democap.Elements
pin Democap.MotionTransferSystem

pin Dragengine.CameraDirectors
pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Preloading
pin Dragengine.Scenery


/**
 * Edit character config window.
 */
class WindowCharacterConfiguration extends WindowDialog
	class UpdateListsListener implements WidgetCharacterPreview.Listener
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func void characterReloaded(WidgetCharacterPreview widget)
			pWindow.updateBoneList()
			pWindow.updateMoveList()
			pWindow.updateVPSList()
			pWindow.updateMoveCalibrate()
		end
	end
	
	class MoveCalibrateChanged implements ActionListener
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func void onAction(ActionEvent event)
			pWindow.updateMoveCalibrate()
		end
	end
	
	class MotionTransferRenderer extends DefaultListElementRenderer
		public func new()
		end
		
		public func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text
			if element != null
				text = (element cast MotionTransfer).getName()
			else
				text = " "
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	class MotionTransferFactoryRenderer extends DefaultListElementRenderer
		public func new()
			setDefaultMinimumSize(Point.new(200, 0))
		end
		
		public func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text
			if element != null
				text = (element cast MotionTransferFactory).displayName()
			else
				text = " "
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	class TrackerSlotSelected extends DefaultListModelListener
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func void selectionChanged(ListModel listModel)
			pWindow.selectTrackerSlot()
		end
	end
	
	class TrackerSlotPopupAction extends DefaultActionListener
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			item = MenuItemCommand.new("Add Slot...", BlockActionListener.new(block ActionEvent event
				pWindow.addTrackerSlot()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Add 3-Tracker Slots", BlockActionListener.new(block ActionEvent event
				var Array types = Array.new()
				types.add(CharacterTrackerSlot.Type.headTracker)
				types.add(CharacterTrackerSlot.Type.headHmd)
				types.add(CharacterTrackerSlot.Type.rightHandController)
				types.add(CharacterTrackerSlot.Type.leftHandController)
				pWindow.addTrackerSlotsTemplate(types)
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Add 6-Tracker Slots", BlockActionListener.new(block ActionEvent event
				var Array types = Array.new()
				types.add(CharacterTrackerSlot.Type.headTracker)
				types.add(CharacterTrackerSlot.Type.headHmd)
				types.add(CharacterTrackerSlot.Type.rightHandController)
				types.add(CharacterTrackerSlot.Type.leftHandController)
				types.add(CharacterTrackerSlot.Type.waist)
				types.add(CharacterTrackerSlot.Type.rightFoot)
				types.add(CharacterTrackerSlot.Type.leftFoot)
				pWindow.addTrackerSlotsTemplate(types)
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Add 10-Tracker Slots", BlockActionListener.new(block ActionEvent event
				var Array types = Array.new()
				types.add(CharacterTrackerSlot.Type.headTracker)
				types.add(CharacterTrackerSlot.Type.headHmd)
				types.add(CharacterTrackerSlot.Type.rightHandController)
				types.add(CharacterTrackerSlot.Type.rightElbow)
				types.add(CharacterTrackerSlot.Type.leftHandController)
				types.add(CharacterTrackerSlot.Type.leftElbow)
				types.add(CharacterTrackerSlot.Type.waist)
				types.add(CharacterTrackerSlot.Type.rightKnee)
				types.add(CharacterTrackerSlot.Type.rightFoot)
				types.add(CharacterTrackerSlot.Type.leftKnee)
				types.add(CharacterTrackerSlot.Type.leftFoot)
				pWindow.addTrackerSlotsTemplate(types)
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Add 11-Tracker Slots", BlockActionListener.new(block ActionEvent event
				var Array types = Array.new()
				types.add(CharacterTrackerSlot.Type.headTracker)
				types.add(CharacterTrackerSlot.Type.headHmd)
				types.add(CharacterTrackerSlot.Type.chest)
				types.add(CharacterTrackerSlot.Type.rightHandController)
				types.add(CharacterTrackerSlot.Type.rightElbow)
				types.add(CharacterTrackerSlot.Type.leftHandController)
				types.add(CharacterTrackerSlot.Type.leftElbow)
				types.add(CharacterTrackerSlot.Type.waist)
				types.add(CharacterTrackerSlot.Type.rightKnee)
				types.add(CharacterTrackerSlot.Type.rightFoot)
				types.add(CharacterTrackerSlot.Type.leftKnee)
				types.add(CharacterTrackerSlot.Type.leftFoot)
				pWindow.addTrackerSlotsTemplate(types)
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Duplicate Slot...", BlockActionListener.new(block ActionEvent event
				pWindow.duplicateTrackerSlot()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Cut...", BlockActionListener.new(block ActionEvent event
				pWindow.cutTrackerSlot()
			end))
			item.setEnabled(pWindow.getTrackerSlot() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Copy", BlockActionListener.new(block ActionEvent event
				pWindow.copyTrackerSlot()
			end))
			item.setEnabled(pWindow.getTrackerSlot() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Paste...", BlockActionListener.new(block ActionEvent event
				pWindow.pasteTrackerSlot()
			end))
			item.setEnabled(pWindow.getClipboard().hasClip())
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Remove Slot", BlockActionListener.new(block ActionEvent event
				pWindow.removeTrackerSlot()
			end))
			item.setEnabled(pWindow.getTrackerSlot() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Remove All Slots", BlockActionListener.new(block ActionEvent event
				pWindow.removeAllTrackerSlots()
			end))
			item.setEnabled(pWindow.getTrackerSlot() != null)
			menu.addWidget(item)
			
			pWindow.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	class MotionTransferSelected extends DefaultListModelListener
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func void selectionChanged(ListModel listModel)
			pWindow.selectMotionTransfer()
		end
	end
	
	class MotionTransferPopupAction extends DefaultActionListener
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			var MenuPopup subMenu = MenuPopup.new()
			GameApp.getGameApp().getMotionTransferFactories().toArraySorted().forEach(block MotionTransferFactory factory
				if factory.displayName().startsWith("Auto")
					item = MenuItemCommand.new(factory.displayName(), BlockActionListener.new(block ActionEvent event
						pWindow.addMotionTransfer(factory)
					end))
					subMenu.addWidget(item)
				end
			end)
			subMenu.addWidget(MenuItemSeparator.new())
			GameApp.getGameApp().getMotionTransferFactories().toArraySorted().forEach(block MotionTransferFactory factory
				if not factory.displayName().startsWith("Auto")
					item = MenuItemCommand.new(factory.displayName(), BlockActionListener.new(block ActionEvent event
						pWindow.addMotionTransfer(factory)
					end))
					subMenu.addWidget(item)
				end
			end)
			menu.addWidget(MenuItemSubMenu.new("Add", subMenu))
			
			item = MenuItemCommand.new("Duplicate...", BlockActionListener.new(block ActionEvent event
				pWindow.duplicateMotionTransfer()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Remove", BlockActionListener.new(block ActionEvent event
				pWindow.removeMotionTransfer()
			end))
			item.setEnabled(pWindow.getMotionTransfer() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Remove All", BlockActionListener.new(block ActionEvent event
				pWindow.removeAllMotionTransfers()
			end))
			item.setEnabled(pWindow.getTrackerSlot() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Cut", BlockActionListener.new(block ActionEvent event
				pWindow.cutMotionTransfer()
			end))
			item.setEnabled(pWindow.getMotionTransfer() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Copy", BlockActionListener.new(block ActionEvent event
				pWindow.copyMotionTransfer()
			end))
			item.setEnabled(pWindow.getMotionTransfer() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Paste", BlockActionListener.new(block ActionEvent event
				pWindow.pasteMotionTransfer()
			end))
			item.setEnabled(pWindow.getClipboard().hasClip())
			menu.addWidget(item)
			
			item = MenuItemCommand.new("Reorder...", BlockActionListener.new(block ActionEvent event
				pWindow.reorderMotionTransferUp()
			end))
			item.setEnabled(pWindow.getMotionTransfer() != null)
			menu.addWidget(item)
			
			// TODO replace move up/down with reorder using a dialog with ListBox and ReorderListMouseListener
			item = MenuItemCommand.new("Decompose Transfer", BlockActionListener.new(block ActionEvent event
				pWindow.decomposeMotionTransfer()
			end))
			item.setEnabled(pWindow.getMotionTransfer() != null)
			menu.addWidget(item)
			
			pWindow.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	class PreviewDataQuery implements ECBPreviewDataQuery.DataQuery
		private var WindowCharacterConfiguration pWindow
		
		public func new(WindowCharacterConfiguration window)
			pWindow = window
		end
		
		public func Matrix getSlotMatrix(String name)
			var CharacterTrackerSlot slot = pWindow.getTrackerSlotNamed(name)
			if slot == null
				return Matrix.new()
			end
			return slot.getTargetMatrix()
		end
	end
	
	
	
	private var CharacterConfiguration pCharacterConfiguration
	private var DefaultTextModel pModelConfigName
	private var DefaultListModel pModelScaleMode
	private var SharedListModel pModelListMoveCalibrate
	private var DefaultTextModel pModelTextMoveCalibrate
	private var SharedListModel pModelListCameraBone
	private var DefaultTextModel pModelTextCameraBone
	private var SharedListModel pModelListGrabBoneRight
	private var DefaultTextModel pModelTextGrabBoneRight
	private var SharedListModel pModelListGrabTipBoneRight
	private var DefaultTextModel pModelTextGrabTipBoneRight
	private var SharedListModel pModelListGrabBaseBoneRight
	private var DefaultTextModel pModelTextGrabBaseBoneRight
	private var SharedListModel pModelListGrabBoneLeft
	private var DefaultTextModel pModelTextGrabBoneLeft
	private var SharedListModel pModelListGrabTipBoneLeft
	private var DefaultTextModel pModelTextGrabTipBoneLeft
	private var SharedListModel pModelListGrabBaseBoneLeft
	private var DefaultTextModel pModelTextGrabBaseBoneLeft
	private var EditVector pEditCameraPosition
	private var EditVector pEditCameraRotation
	private var DefaultListModel pModelTrackerSlots
	private var DefaultListModel pModelMotionTransfers
	private var DefaultRangeModel pModelMotionTransferPanel
	private var WidgetCharacterPreview pCharacterPreview
	private var WCCPTrackerSlot pPanelTrackerSlot
	private var Array pPanelsMotionTransfer
	private var bool pInVR
	private var DefaultListModel pModelBones
	private var DefaultListModel pModelMoves
	private var DefaultListModel pModelVPS
	private var DefaultListModel pModelTrackerSlotNames
	
	
	
	/** Create window. */
	public func new(Window window, bool inVR, CharacterConfiguration config, ResultListener listener)
		pInVR = inVR
		pCharacterConfiguration = config
		
		var ActorProfile actorProfile = GameApp.getGameApp().getActiveActorProfile()
		config.getTrackerSlots().forEach(block CharacterTrackerSlot each
			each.initFromType(actorProfile)
		end)
		
		pModelBones = DefaultListModel.new()
		pModelBones.setAutoSelect(false)
		
		pModelMoves = DefaultListModel.new()
		pModelMoves.setAutoSelect(false)
		
		pModelVPS = DefaultListModel.new()
		pModelVPS.setAutoSelect(false)
		
		pModelTrackerSlotNames = DefaultListModel.new()
		pModelTrackerSlotNames.setAutoSelect(false)
		
		pModelConfigName = DefaultTextModel.new(config.getName())
		
		pModelScaleMode = DefaultListModel.new(CharacterConfiguration.ScaleMode.all().toArray())
		pModelScaleMode.setSelected(pModelScaleMode.indexOf(config.getScaleMode()))
		
		pModelListMoveCalibrate = SharedListModel.new(pModelMoves)
		pModelListMoveCalibrate.setAutoSelect(false)
		pModelTextMoveCalibrate = DefaultTextModel.new(config.getMoveCalibrate())
		
		pModelListCameraBone = SharedListModel.new(pModelBones)
		pModelListCameraBone.setAutoSelect(false)
		pModelTextCameraBone = DefaultTextModel.new(config.getCameraBone())
		
		pModelListGrabBoneRight = SharedListModel.new(pModelBones)
		pModelListGrabBoneRight.setAutoSelect(false)
		pModelTextGrabBoneRight = DefaultTextModel.new(config.getGrabBoneRight())
		
		pModelListGrabTipBoneRight = SharedListModel.new(pModelBones)
		pModelListGrabTipBoneRight.setAutoSelect(false)
		pModelTextGrabTipBoneRight = DefaultTextModel.new(config.getGrabTipBoneRight())
		
		pModelListGrabBaseBoneRight = SharedListModel.new(pModelBones)
		pModelListGrabBaseBoneRight.setAutoSelect(false)
		pModelTextGrabBaseBoneRight = DefaultTextModel.new(config.getGrabBaseBoneRight())
		
		pModelListGrabBoneLeft = SharedListModel.new(pModelBones)
		pModelListGrabBoneLeft.setAutoSelect(false)
		pModelTextGrabBoneLeft = DefaultTextModel.new(config.getGrabBoneLeft())
		
		pModelListGrabTipBoneLeft = SharedListModel.new(pModelBones)
		pModelListGrabTipBoneLeft.setAutoSelect(false)
		pModelTextGrabTipBoneLeft = DefaultTextModel.new(config.getGrabTipBoneLeft())
		
		pModelListGrabBaseBoneLeft = SharedListModel.new(pModelBones)
		pModelListGrabBaseBoneLeft.setAutoSelect(false)
		pModelTextGrabBaseBoneLeft = DefaultTextModel.new(config.getGrabBaseBoneLeft())
		
		pModelTrackerSlots = DefaultListModel.new(config.getTrackerSlots().toArray().map(\
			block CharacterTrackerSlot each
				return CharacterTrackerSlot.new(config, each)
			end).sorted())
		
		pModelMotionTransfers = DefaultListModel.new(config.getMotionTransfers().toArray().map(\
			block MotionTransfer each
				return each.createCopy()
			end))
		
		pModelMotionTransferPanel = DefaultRangeModel.new()
		
		pPanelsMotionTransfer = Array.new()
		
		runWhileBlockingLayout(block
			setGuiTheme(window.getGuiTheme())
			setTitle("Character Configuration")
			setDesignerSelector("Window.Dialog.CharacterConfiguration")
			setSize(window.getDesktop().getContainerSize() - Point.new(100, 100))
			
			createContent()
			
			pEditCameraPosition.setVector(config.getCameraPosition())
			pEditCameraRotation.setVector(config.getCameraRotation())
		end)
		doLayout()
		
		pModelTrackerSlots.addListener(TrackerSlotSelected.new(this))
		selectTrackerSlot()
		
		pModelMotionTransfers.addListener(MotionTransferSelected.new(this))
		selectMotionTransfer()
		
		var CharacterAppearance appearance = config.getProfile().getAppearance()
		pCharacterPreview.setPathModel(appearance.getPathModel())
		pCharacterPreview.setPathSkin(appearance.getPathSkin())
		pCharacterPreview.setPathRig(appearance.getPathRig())
		pCharacterPreview.setPathAnimation(appearance.getPathAnimation())
		pCharacterPreview.setBoneAxisRotation(CharacterProfile.boneAxisRotation(config.getProfile().getBoneAxis()))
		pCharacterPreview.setScale(config.getProfile().getScale())
		pCharacterPreview.reloadCharacterPreview()
		
		setResultListener(listener)
		
		if inVR
			setPosition(Point.new())
			setSize(window.getDesktop().getSize())
			show(window, false, WindowModal.Placement.none)
			
		else
			show(window, false, WindowModal.Placement.desktop)
		end
		
		pCharacterPreview.setEditor(null)
		
		updateTrackerSlotLists()
	end
	
	/** Dispose of window. */
	public func void dispose()
		super.dispose()
		
		pPanelsMotionTransfer = null
		pPanelTrackerSlot = null
		pModelConfigName = null
		pModelScaleMode = null
		pModelListMoveCalibrate = null
		pModelTextMoveCalibrate = null
		pModelListCameraBone = null
		pModelTextCameraBone = null
		pModelListGrabBoneRight = null
		pModelTextGrabBoneRight = null
		pModelListGrabTipBoneRight = null
		pModelTextGrabTipBoneRight = null
		pModelListGrabBaseBoneRight = null
		pModelTextGrabBaseBoneRight = null
		pModelListGrabBoneLeft = null
		pModelTextGrabBoneLeft = null
		pModelListGrabTipBoneLeft = null
		pModelTextGrabTipBoneLeft = null
		pModelListGrabBaseBoneLeft = null
		pModelTextGrabBaseBoneLeft = null
		pEditCameraPosition = null
		pEditCameraRotation = null
		pModelTrackerSlots = null
		pModelMotionTransfers = null
		pModelMotionTransferPanel = null
		
		pModelBones = null
		pModelMoves = null
		pModelVPS = null
		pModelTrackerSlotNames = null
	end
	
	
	
	/** Character configuration. */
	public func CharacterConfiguration getCharacterConfiguration()
		return pCharacterConfiguration
	end
	
	/** Character preview widget. */
	public func WidgetCharacterPreview getCharacterPreview()
		return pCharacterPreview
	end
	
	/** Tracker slot panel. */
	public func WCCPTrackerSlot getPanelTrackerSlot()
		return pPanelTrackerSlot
	end
	
	/** Shared models. */
	public func DefaultListModel getModelBones()
		return pModelBones
	end
	
	public func DefaultListModel getModelMoves()
		return pModelMoves
	end
	
	public func DefaultListModel getModelVPS()
		return pModelVPS
	end
	
	public func DefaultListModel getModelTrackerSlotNames()
		return pModelTrackerSlotNames
	end
	
	
	
	/** Save dialog and close if no problems are found. */
	public func void saveAndClose()
		// verify input
		var String configName = pModelConfigName.getText()
		
		if not configName.toLower().equals(pCharacterConfiguration.getName().toLower())
			if configName.empty()
				WindowDialog.message(this, "Save Configuration", "Configuration name can not be empty", null, null, null)
				return
			end
			if pCharacterConfiguration.getProfile().getConfigurations().hasNamed(configName)
				WindowDialog.message(this, "Save Configuration", "Configuration with same name exists", null, null, null)
				return
			end
		end
		
		pPanelTrackerSlot.saveValues()
		
		var int selection = pModelMotionTransferPanel.getValue() - 1
		if selection >= 0
			(pPanelsMotionTransfer.getAt(selection) cast WCCPanelMotionTransfer).saveValues()
		end
		
		// save changes
		pCharacterConfiguration.setName(configName)
		pCharacterConfiguration.setScaleMode(pModelScaleMode.getAt(\
			pModelScaleMode.getSelected()) cast CharacterConfiguration.ScaleMode)
		pCharacterConfiguration.setMoveCalibrate(pModelTextMoveCalibrate.getText())
		pCharacterConfiguration.setCameraBone(pModelTextCameraBone.getText())
		pCharacterConfiguration.setCameraPosition(pEditCameraPosition.getVector())
		pCharacterConfiguration.setCameraRotation(pEditCameraRotation.getVector())
		pCharacterConfiguration.setGrabBoneRight(pModelTextGrabBoneRight.getText())
		pCharacterConfiguration.setGrabTipBoneRight(pModelTextGrabTipBoneRight.getText())
		pCharacterConfiguration.setGrabBaseBoneRight(pModelTextGrabBaseBoneRight.getText())
		pCharacterConfiguration.setGrabBoneLeft(pModelTextGrabBoneLeft.getText())
		pCharacterConfiguration.setGrabTipBoneLeft(pModelTextGrabTipBoneLeft.getText())
		pCharacterConfiguration.setGrabBaseBoneLeft(pModelTextGrabBaseBoneLeft.getText())
		
		pCharacterConfiguration.getTrackerSlots().removeAll()
		pModelTrackerSlots.forEach(block CharacterTrackerSlot each
			pCharacterConfiguration.getTrackerSlots().add(each)
		end)
		
		pCharacterConfiguration.getMotionTransfers().removeAll()
		pModelMotionTransfers.forEach(block MotionTransfer each
			pCharacterConfiguration.getMotionTransfers().add(each.createCopy())
		end)
		
		pCharacterConfiguration.getProfile().save()
		
		// finish and close window
		setResult(true)
		close()
	end
	
	/** UI options prefix. */
	static public func String prefixUIOptions(bool inVR)
		return inVR if "UI/VR/WindowCharacterConfiguration" else "UI/WindowCharacterConfiguration"
	end
	
	
	
	/** Create window content. */
	protected func void createContent()
		var String uioptPrefix = prefixUIOptions(pInVR)
		var LabelledComboBox lcb, lcbtip, lcbbase
		var LabelledTextField ltf
		var TextField textField
		var ComboBox comboBox
		var Button button
		var Array result
		
		var UIBuilder uiBuilder = UIBuilder.new()
		uiBuilder.updateDesignerSelectors(".WindowCharacterConfiguration")
		
		setLayout(BorderLayout.new(20))
		
		pCharacterPreview = WidgetCharacterPreview.new()
		pCharacterPreview.setDataQuery(PreviewDataQuery.new(this))
		pCharacterPreview.addListener(UpdateListsListener.new(this))
		addWidget(pCharacterPreview, BorderLayout.Area.content)
		
		// properties
		addWidget(ScrollPanel.new(Viewport.new(Panel.new(FlowLayout.new(LayoutAxis.y, 5), block Panel p
			// global parameters
			p.addWidget(CollapsibleGroup.new("Character Configuration", "",\
			CollapsibleGroup.persistModel(uioptPrefix + "/GroupCharacter"),\
			FormLayout.new(true, 10, 2), block Panel p2
				ltf = LabelledTextField.new(p2, "Configuration Name:", pModelConfigName)
				ltf.setToolTip("Unique configuration name")
				
				lcb = LabelledComboBox.new(p2, "Scale Mode:", pModelScaleMode)
				lcb.setToolTip("Scale actor or character to match the other.")
				
				lcb = LabelledComboBox.new(p2, "Move Calibrate:", pModelListMoveCalibrate,\
					pModelTextMoveCalibrate, MoveCalibrateChanged.new(this))
				lcb.setToolTip("Animation move to use for calibration. "\
					+ "This move is also applied before the first motion transfer is applied.")
			end))
			
			// camera
			p.addWidget(CollapsibleGroup.new("Camera Parameters", "",\
			CollapsibleGroup.persistModel(uioptPrefix + "/GroupCamera"),\
			FormLayout.new(true, 10, 2), block Panel p2
				result = uiBuilder.formBonePositionRotation(p2, pCharacterPreview,\
					"Bone:", "Position:", "Rotation:",\
					"Bone to attach VR Camera to. If empty string use HMD.",\
					"Attach position of camera relative to bone. Not used if bone is empty string.",\
					"Attach rotation in degrees of camera relative to bone. Not used if bone is empty string.",\
					pModelListCameraBone, pModelTextCameraBone,\
					pCharacterConfiguration.getCameraPosition(), pCharacterConfiguration.getCameraRotation())
				
				pEditCameraPosition = result.getAt(0) cast EditVector
				pEditCameraRotation = result.getAt(1) cast EditVector
			end))
			
			// grab
			p.addWidget(CollapsibleGroup.new("Grab Parameters", "",\
			CollapsibleGroup.persistModel(uioptPrefix + "/GroupGrab"),\
			FormLayout.new(true, 10, 2), block Panel p2
				// right hand
				lcb = LabelledComboBox.new(p2, "Bone Right:", pModelListGrabBoneRight, pModelTextGrabBoneRight)
				lcb.setToolTip("Right hand bone to attach grabbed objects to.")
				CPEBone.new(pCharacterPreview, lcb)
				
				lcbtip = LabelledComboBox.new(p2, "Tip Bone Right:", pModelListGrabTipBoneRight, pModelTextGrabTipBoneRight)
				lcbtip.setToolTip("Right hand tip bone in temporary inverse kinematic chain for grabbing objects with the right hand.")
				
				lcbbase = LabelledComboBox.new(p2, "Base Bone Right:", pModelListGrabBaseBoneRight, pModelTextGrabBaseBoneRight)
				lcbbase.setToolTip("Right hand base bone in temporary inverse kinematic chain for grabbing objects with the right hand.")
				
				CPEBoneChain.new(pCharacterPreview, lcbtip, lcbbase, false)
				CPEBoneChain.new(pCharacterPreview, lcbtip, lcbbase, true)
				
				// left hand
				lcb = LabelledComboBox.new(p2, "Bone Left:", pModelListGrabBoneLeft, pModelTextGrabBoneLeft)
				lcb.setToolTip("Left hand bone to attach grabbed objects to.")
				CPEBone.new(pCharacterPreview, lcb)
				
				lcbtip = LabelledComboBox.new(p2, "Tip Bone Left:", pModelListGrabTipBoneLeft, pModelTextGrabTipBoneLeft)
				lcbtip.setToolTip("Left hand tip bone in temporary inverse kinematic chain for grabbing objects with the left hand.")
				
				lcbbase = LabelledComboBox.new(p2, "Base Bone Left:", pModelListGrabBaseBoneLeft, pModelTextGrabBaseBoneLeft)
				lcbbase.setToolTip("Left hand base bone in temporary inverse kinematic chain for grabbing objects with the left hand.")
				
				CPEBoneChain.new(pCharacterPreview, lcbtip, lcbbase, false)
				CPEBoneChain.new(pCharacterPreview, lcbtip, lcbbase, true)
			end))
			
			// tracker slots
			p.addWidget(CollapsibleGroup.new("Tracker Slots", "",\
			CollapsibleGroup.persistModel(uioptPrefix + "/GroupSlots"),\
			FlowLayout.new(LayoutAxis.y, 5), block Panel p2
				p2.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p3
					comboBox = ComboBox.new(pModelTrackerSlots)
					p3.addWidget(comboBox)
					
					button = Button.new("...", TrackerSlotPopupAction.new(this))
					button.setDesignerSelector("Button.Popup")
					p3.addWidget(button)
				end))
				
				pPanelTrackerSlot = WCCPTrackerSlot.new(this)
				p2.addWidget(pPanelTrackerSlot)
			end))
			
			// motion transfers
			p.addWidget(CollapsibleGroup.new("Motion Transfers", "",\
			CollapsibleGroup.persistModel(uioptPrefix + "/GroupTransfers"),\
			FlowLayout.new(LayoutAxis.y), block Panel p2
				p2.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p3
					comboBox = ComboBox.new(pModelMotionTransfers, MotionTransferRenderer.new())
					p3.addWidget(comboBox)
					
					button = Button.new("...", MotionTransferPopupAction.new(this))
					button.setDesignerSelector("Button.Popup")
					p3.addWidget(button)
				end))
				
				var SwitchPanel switcher = SwitchPanel.new(pModelMotionTransferPanel)
				p2.addWidget(switcher)
				
				pPanelsMotionTransfer.add(WCCPMTAnimation.new(this))
				pPanelsMotionTransfer.add(WCCPMTBend.new(this, uioptPrefix + "/MTBend"))
				pPanelsMotionTransfer.add(WCCPMTChild.new(this, uioptPrefix + "/MTChild"))
				pPanelsMotionTransfer.add(WCCPMTCopy.new(this))
				pPanelsMotionTransfer.add(WCCPMTFingers.new(this))
				pPanelsMotionTransfer.add(WCCPMTEyes.new(this))
				pPanelsMotionTransfer.add(WCCPMTFace.new(this, uioptPrefix + "/MTFace"))
				pPanelsMotionTransfer.add(WCCPMTIK.new(this, uioptPrefix + "/MTIK"))
				pPanelsMotionTransfer.add(WCCPMTSlot.new(this, uioptPrefix + "/MTSlot"))
				pPanelsMotionTransfer.add(WCCPMTSecondary.new(this))
				pPanelsMotionTransfer.add(WCCPMTAutoHuman.new(this, uioptPrefix + "/MTAutoHuman"))
				pPanelsMotionTransfer.add(WCCPMTAutoFPV.new(this, uioptPrefix + "/MTAutoFPV"))
				
				switcher.addWidget(Panel.new()) // empty
				pPanelsMotionTransfer.forEach(block WCCPanelMotionTransfer each
					switcher.addWidget(each)
				end)
			end))
		end), 0, 20, true), ScrollPanel.Policy.hidden, ScrollPanel.Policy.visible), BorderLayout.Area.right)
		
		// button line
		addWidget(Panel.new(CentrizedBoxLayout.new(LayoutAxis.x), block Panel p
			button = Button.new("Save", BlockActionListener.new(block ActionEvent e
				saveAndClose()
			end))
			button.setDesignerSelector("Button.WindowCharacterConfiguration.Save")
			p.addWidget(button)
			
			button = Button.new("Cancel", CloseDialog.new(this, false))
			button.setDesignerSelector("Button.WindowCharacterConfiguration.Cancel")
			p.addWidget(button)
		end ), BorderLayout.Area.bottom )
	end
	
	/** Update shared bone list. */
	public func void updateBoneList()
		var Array bones = Array.new()
		var Rig rig
		
		var BehaviorElement element = pCharacterPreview.getCharacterPreview()
		if element != null
			rig = ECBehaviorComponent.getInstanceIn(element).getComponent().getRig()
		end
		
		if rig != null
			var int i, count = rig.getBoneCount()
			for i = 0 to count
				bones.add(rig.boneGetName(i))
			end
			bones.sort(block String a, String b
				return a.compareNoCase(b)
			end)
		end
		
		pModelBones.setContent(bones)
	end
	
	/** Update shared move list. */
	public func void updateMoveList()
		var Array moves = Array.new()
		var Animation animation
		
		var BehaviorElement element = pCharacterPreview.getCharacterPreview()
		if element != null
			var ECBPreviewAnimation.Instance previewAnimation = ECBPreviewAnimation.getInstanceIn(element)
			if previewAnimation != null
				animation = previewAnimation.getAnimation()
			end
		end
		
		if animation != null
			var int i, count = animation.getMoveCount()
			for i = 0 to count
				moves.add(animation.getMoveName(i))
			end
			moves.sort(block String a, String b
				return a.compareNoCase(b)
			end)
		end
		
		pModelMoves.setContent(moves)
	end
	
	/** Update shared vertex position set list. */
	public func void updateVPSList()
		var Array vps = Array.new()
		var Component component
		
		var BehaviorElement element = pCharacterPreview.getCharacterPreview()
		if element != null
			component = ECBehaviorComponent.getInstanceIn(element).getComponent()
		end
		
		if component != null
			var int i, count = component.getVertexPositionSetCount()
			for i = 0 to count
				vps.add(component.vertexPositionSetGetNameAt(i))
			end
			vps.sort(block String a, String b
				return a.compareNoCase(b)
			end)
		end
		
		pModelVPS.setContent(vps)
	end
	
	/** Update calibration move. */
	public func void updateMoveCalibrate()
		var BehaviorElement element = pCharacterPreview.getCharacterPreview()
		if element == null
			return
		end
		
		var ECBPreviewAnimation.Instance previewAnimation = ECBPreviewAnimation.getInstanceIn(element)
		if previewAnimation != null
			previewAnimation.setMove(pModelTextMoveCalibrate.getText())
		end
		
		var ECBPreviewBones.Instance previewBones = ECBPreviewBones.getInstanceIn(element)
		if previewBones != null
			previewBones.bonePositionChanged()
		end
	end
	
	
	
	/** Active tracker slot. */
	public func CharacterTrackerSlot getTrackerSlot()
		if pModelTrackerSlots.getSelected() != -1
			return pModelTrackerSlots.getAt(pModelTrackerSlots.getSelected()) cast CharacterTrackerSlot
		end
		return null
	end
	
	/** Named tracker slot or null. */
	public func CharacterTrackerSlot getTrackerSlotNamed(String name)
		return pModelTrackerSlots.find(block CharacterTrackerSlot each
			return each.getName().compareNoCase(name) == 0
		end) cast CharacterTrackerSlot
	end
	
	/** Tracker slot with type or null. */
	public func CharacterTrackerSlot getTrackerSlotWithType(CharacterTrackerSlot.Type type)
		return pModelTrackerSlots.find(block CharacterTrackerSlot each
			return each.getType() == type
		end) cast CharacterTrackerSlot
	end
	
	/** Select tracker slot. */
	public func void selectTrackerSlot()
		if pModelTrackerSlots.getSelected() != -1
			pPanelTrackerSlot.saveValues()
			pPanelTrackerSlot.setTrackerSlot(pModelTrackerSlots.getAt(\
				pModelTrackerSlots.getSelected()) cast CharacterTrackerSlot)
			pPanelTrackerSlot.setEnabled(true)
			
		else
			pPanelTrackerSlot.saveValues()
			pPanelTrackerSlot.setTrackerSlot(null)
			pPanelTrackerSlot.setEnabled(false)
		end
	end
	
	/** Add tracker slot. */
	public func void addTrackerSlot()
		var DefaultListModel model = DefaultListModel.new(CharacterTrackerSlot.Type.all().toArray()\
			.sorted(block CharacterTrackerSlot.Type a, CharacterTrackerSlot.Type b
				return a.name().compare(b.name())
			end))
		var DefaultListElementRenderer renderer = DefaultListElementRenderer.new(CharacterTrackerSlot.Type.rightHandController)
		
		WindowDialog.chooseList(this, "Add Tracker Slot", "Slot Type:", null, model, renderer,\
			null, null, WindowDialog.BlockResultListener.new(block int result
				if result != -1
					var CharacterTrackerSlot.Type type = model.getAt(result) cast CharacterTrackerSlot.Type
					var String name = uniqueTrackerSlotName(CharacterTrackerSlot.defaultSlotNameForType(type))
					
					var ActorProfile actorProfile = GameApp.getGameApp().getActiveActorProfile()
					var CharacterTrackerSlot slot = CharacterTrackerSlot.new(pCharacterConfiguration, name)
					slot.setType(type)
					slot.initFromType(actorProfile)
					slot.setTargetFromActor(actorProfile)
					pModelTrackerSlots.add(slot)
					pModelTrackerSlots.sort()
					pModelTrackerSlots.setSelected(pModelTrackerSlots.indexOf(slot))
					updateTrackerSlotLists()
				end
			end))
	end
	
	/** Add template tracker slots. */
	public func void addTrackerSlotsTemplate(Array types)
		var ActorProfile actorProfile = GameApp.getGameApp().getActiveActorProfile()
		
		types.forEach(block CharacterTrackerSlot.Type type
			var String name = uniqueTrackerSlotName(CharacterTrackerSlot.defaultSlotNameForType(type))
			var CharacterTrackerSlot slot = CharacterTrackerSlot.new(pCharacterConfiguration, name)
			slot.setType(type)
			slot.initFromType(actorProfile)
			slot.setTargetFromActor(actorProfile)
			pModelTrackerSlots.add(slot)
		end)
		
		pModelTrackerSlots.sort()
		pModelTrackerSlots.setSelected(pModelTrackerSlots.getCount() - types.getCount())
		updateTrackerSlotLists()
	end
	
	/** Duplicate tracker slot. */
	public func void duplicateTrackerSlot()
		var CharacterTrackerSlot slot = getTrackerSlot()
		if slot == null
			return
		end
		
		WindowDialog.input(this, "Duplicate Tracker Slot", "Slot Name:", null, slot.getName(),\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null
					if pModelTrackerSlots.find(block CharacterTrackerSlot each
						return each.getName().compareNoCase(result) == 0
					end) != null
						WindowDialog.message(this, "Duplicate Tracker Slot", "Slot '"\
							+ result + "' exists already", null, null, null)
						
					else
						slot = CharacterTrackerSlot.new(pCharacterConfiguration, slot)
						slot.setName(result)
						pModelTrackerSlots.add(slot)
						pModelTrackerSlots.sort()
						pModelTrackerSlots.setSelected(pModelTrackerSlots.indexOf(slot))
						updateTrackerSlotLists()
					end
				end
			end))
	end
	
	/** Copy tracker slot. */
	public func void copyTrackerSlot()
		var CharacterTrackerSlot slot = getTrackerSlot()
		if slot == null
			return
		end
		
		getClipboard().setClipWith(CharacterTrackerSlot.new(pCharacterConfiguration, slot))
	end
	
	/** Cut tracker slot. */
	public func void cutTrackerSlot()
		var CharacterTrackerSlot slot = getTrackerSlot()
		if slot == null
			return
		end
		
		WindowDialog.question(this, "Cut Tracker Slot",\
			"Do you really want to cut slot '" + slot.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Cut", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					getClipboard().setClipWith(CharacterTrackerSlot.new(pCharacterConfiguration, slot))
					pModelTrackerSlots.remove(slot)
					updateTrackerSlotLists()
				end
			end))
	end
	
	/** Paste tracker slot. */
	public func void pasteTrackerSlot()
		if getClipboard().isEmpty()
			return
		end
		
		getClipboard().getClip().findCastable(block CharacterTrackerSlot slot
			WindowDialog.input(this, "Paste Tracker Slot", "Slot Name:", null, slot.getName(),\
				null, null, WindowDialog.BlockResultListener.new(block String result
					if result != null
						if pModelTrackerSlots.find(block CharacterTrackerSlot each
							return each.getName().compareNoCase(result) == 0
						end) != null
							WindowDialog.message(this, "Duplicate Tracker Slot", "Slot '"\
								+ result + "' exists already", null, null, null)
							
						else
							slot = CharacterTrackerSlot.new(pCharacterConfiguration, slot)
							slot.setName(result)
							pModelTrackerSlots.add(slot)
							pModelTrackerSlots.sort()
							pModelTrackerSlots.setSelected(pModelTrackerSlots.indexOf(slot))
							updateTrackerSlotLists()
						end
					end
				end))
			return true
		end)
	end
	
	/** Remove tracker slot. */
	public func void removeTrackerSlot()
		var CharacterTrackerSlot slot = getTrackerSlot()
		if slot == null
			return
		end
		
		WindowDialog.question(this, "Remove Tracker Slot",\
			"Do you really want to remove slot '" + slot.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Remove", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pModelTrackerSlots.remove(slot)
					updateTrackerSlotLists()
				end
			end))
	end
	
	/** Remove all tracker slots. */
	public func void removeAllTrackerSlots()
		if pModelTrackerSlots.getCount() == 0
			return
		end
		
		WindowDialog.question(this, "Remove All Tracker Slots",\
			"Do you really want to remove all slots?", null,\
			Array.newWith(WindowDialog.ButtonConfiguration.new("Remove All", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pModelTrackerSlots.removeAll()
					updateTrackerSlotLists()
				end
			end))
	end
	
	/** Ensure unique tracker slot name. */
	public func String uniqueTrackerSlotName(String name)
		var String baseName = name
		var int number = 2
		
		while pModelTrackerSlots.find(block CharacterTrackerSlot each
			return each.getName().compareNoCase(name) == 0
		end) != null
			name = baseName + " #" + number++
		end
		
		return name
	end
	
	/** Tracker slot name changed. */
	public func void onTrackerSlotNameChanged()
		var int index = pModelTrackerSlots.indexOf(getTrackerSlot())
		if index != -1
			pModelTrackerSlots.notifyContentChanged(index, index)
			updateTrackerSlotLists()
		end
	end
	
	/** Update tracker slot lists. */
	public func void updateTrackerSlotLists()
		pModelTrackerSlotNames.setContent(\
			pModelTrackerSlots.getContent().map(block CharacterTrackerSlot each
				return each.getName()
			end).sorted(block String a, String b
				return a.compareNoCase(b)
			end))
	end
	
	
	
	/** Active motion transfer. */
	public func MotionTransfer getMotionTransfer()
		if pModelMotionTransfers.getSelected() != -1
			return pModelMotionTransfers.getAt(pModelMotionTransfers.getSelected()) cast MotionTransfer
		end
		return null
	end
	
	/** Select motion transfer. */
	public func void selectMotionTransfer()
		var int selection = pModelMotionTransferPanel.getValue()
		var WCCPanelMotionTransfer panel
		
		if selection > 0
			panel = pPanelsMotionTransfer.getAt(selection - 1) cast WCCPanelMotionTransfer
			if panel != null
				panel.saveValues()
			end
		end
		
		if pModelMotionTransfers.getSelected() != -1
			var MotionTransfer motionTransfer = pModelMotionTransfers.getAt(\
				pModelMotionTransfers.getSelected()) cast MotionTransfer
			
			panel = pPanelsMotionTransfer.find(block WCCPanelMotionTransfer each
				return each.supports(motionTransfer)
			end) cast WCCPanelMotionTransfer
			
			pModelMotionTransferPanel.setValue(pPanelsMotionTransfer.indexOf(panel) + 1)
			
			if panel != null
				panel.setMotionTransfer(motionTransfer)
			end
			
		else
			pModelMotionTransferPanel.setValue(0)
		end
	end
	
	/** Add motion transfer. */
	public func void addMotionTransfer(MotionTransferFactory factory)
		var MotionTransfer motionTransfer = factory.createMotionTransfer()
		motionTransfer.setName(factory.displayName())
		pModelMotionTransfers.add(motionTransfer)
		pModelMotionTransfers.setSelected(pModelMotionTransfers.indexOf(motionTransfer))
	end
	
	/** Duplicate motion transfer. */
	public func void duplicateMotionTransfer()
		var MotionTransfer motionTransfer = getMotionTransfer()
		if motionTransfer != null
			var int index = pModelMotionTransfers.indexOf(motionTransfer)
			motionTransfer = motionTransfer.createCopy()
			pModelMotionTransfers.insert(index + 1, motionTransfer)
			pModelMotionTransfers.setSelected(index + 1)
		end
	end
	
	/** Copy motion transfer. */
	public func void copyMotionTransfer()
		var MotionTransfer motionTransfer = getMotionTransfer()
		if motionTransfer != null
			getClipboard().setClipWith(motionTransfer.createCopy())
		end
	end
	
	/** Cut motion transfer. */
	public func void cutMotionTransfer()
		var MotionTransfer motionTransfer = getMotionTransfer()
		if motionTransfer == null
			return
		end
		
		WindowDialog.question(this, "Cut Motion Transfer",\
			"Do you really want to cut motion transfer '" + motionTransfer.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Cut", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					getClipboard().setClipWith(motionTransfer.createCopy())
					pModelMotionTransfers.remove(motionTransfer)
				end
			end))
	end
	
	/** Paste motion transfer. */
	public func void pasteMotionTransfer()
		if getClipboard().isEmpty()
			return
		end
		
		getClipboard().getClip().findCastable(block MotionTransfer motionTransfer
			pModelMotionTransfers.add(motionTransfer.createCopy())
			pModelMotionTransfers.setSelected(pModelMotionTransfers.getCount() - 1)
			return true
		end)
	end
	
	/** Remove motion transfer. */
	public func void removeMotionTransfer()
		var MotionTransfer motionTransfer = getMotionTransfer()
		if motionTransfer == null
			return
		end
		
		WindowDialog.question(this, "Remove Motion Transfer",\
			"Do you really want to remove motion transfer '" + motionTransfer.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Remove", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pModelMotionTransfers.remove(motionTransfer)
				end
			end))
	end
	
	/** Remove all motion transfers. */
	public func void removeAllMotionTransfers()
		if pModelMotionTransfers.getCount() == 0
			return
		end
		
		WindowDialog.question(this, "Remove All Motion Transfers",\
			"Do you really want to remove all motion transfers?", null,\
			Array.newWith(WindowDialog.ButtonConfiguration.new("Remove All", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pModelMotionTransfers.removeAll()
				end
			end))
	end
	
	/** Reorder motion transfer up. */
	public func void reorderMotionTransferUp()
		var MotionTransfer motionTransfer = getMotionTransfer()
		if motionTransfer == null
			return
		end
		
		var DefaultListModel list = DefaultListModel.new(pModelMotionTransfers.getContent())
		WindowDialog.reorderList(this, "Reorder Motion Transfers", "Drag motion transfer to reorder",\
			null, list, MotionTransferRenderer.new(), null, null,\
			WindowDialog.BlockResultListener.new(block bool result
				pModelMotionTransfers.setContent(list.getContent())
				pModelMotionTransfers.setSelectedElement(motionTransfer)
			end))
	end
	
	/** Decompose motion transfer. */
	public func void decomposeMotionTransfer()
		var MotionTransfer motionTransfer = getMotionTransfer()
		if motionTransfer == null
			return
		end
		
		var String message = "Do you really want to decompose motion transfer '"\
			+ motionTransfer.getName() + "'?"
		
		if not pCharacterConfiguration.isCalibrated()
			message = "Character is not calibrated.\n\n" + message
		end
		
		WindowDialog.question(this, "Decompose Motion Transfer", message,\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Decompose", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					var Array list = motionTransfer.decompose()
					if list.getCount() > 0
						var int index = pModelMotionTransfers.indexOf(motionTransfer)
						pModelMotionTransfers.remove(motionTransfer)
						pModelMotionTransfers.insertAll(index, list)
						pModelMotionTransfers.setSelected(index)
					end
				end
			end))
	end
	
	/** Motion transfer name changed. */
	public func void onMotionTransferNameChanged()
		var int index = pModelMotionTransfers.indexOf(getMotionTransfer())
		if index != -1
			pModelMotionTransfers.notifyContentChanged(index, index)
		end
	end
end
