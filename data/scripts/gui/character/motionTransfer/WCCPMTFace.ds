/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2023, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors
pin Democap.Characters
pin Democap.MotionTransferSystem

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery


/**
 * Edit character config window motion transfer face panel.
 */
class WCCPMTFace extends WCCPanelMotionTransfer
	class Expression
		public var MotionTransferFace.ExpressionInfo info
		public var SharedListModel modelList
		public var DefaultTextModel modelText
		
		func new(MotionTransferFace.ExpressionInfo info, DefaultListModel modelVPS)
			this.info = info
			
			modelList = SharedListModel.new(modelVPS)
			modelList.setAutoSelect(false)
			
			modelText = DefaultTextModel.new()
		end
	end
	
	
	var Array pExpressions
	
	
	
	/** Create window. */
	func new(WindowCharacterConfiguration window, String optionPrefix) super(window)
		var DefaultListModel modelVPS = window.getModelVPS()
		
		pModelTypeName.setText("@UI.Face")
		
		pExpressions = MotionTransferFace.expressionInfos.map(block MotionTransferFace.ExpressionInfo each
			return Expression.new(each, modelVPS)
		end)
		
		runWhileBlockingLayout(block
			createContent(optionPrefix)
		end)
	end
	
	/** Dispose of window. */
	func void dispose()
		pExpressions = null
		super.dispose()
	end
	
	
	
	/** Load values from tracker slot object if present into edit widgets. */
	func void loadValues()
		runWhileBlockingLayout(block
			super.loadValues()
			
			if pMotionTransfer != null
				var MotionTransferFace mt = pMotionTransfer cast MotionTransferFace
				pExpressions.forEach(block Expression each
					each.modelText.setText(mt.getExpressionVPSAt(each.info.getExpression()))
				end)
				
			else
				pExpressions.forEach(block Expression each
					each.modelText.setText("")
				end)
			end
		end)
		doLayoutIfBlocked()
	end
	
	/** Save value from edit widgets to tracker slot object if present. */
	func void saveValues()
		if pMotionTransfer == null
			return
		end
		super.saveValues()
		
		var MotionTransferFace mt = pMotionTransfer cast MotionTransferFace
		pExpressions.forEach(block Expression each
			mt.setExpressionVPSAt(each.info.getExpression(), each.modelText.getText())
		end)
	end
	
	/** Supports motion transfer type. */
	func bool supports(MotionTransfer motionTranfer)
		return motionTranfer castable MotionTransferFace
	end
	
	
	
	/** Create window content. */
	protected func void createContent(String optionPrefix)
		var WidgetCharacterPreview preview = pWindowCharacterConfiguration.getCharacterPreview()
		var ComboBox comboBox
		var Label label
		
		createBaseContent("motiontransferface")
		
		// expressions
		addExpressionWidgets("@UI.Eye", "eye", optionPrefix + "/ExprEye")
		addExpressionWidgets("@UI.Jaw", "jaw", optionPrefix + "/ExprJaw")
		addExpressionWidgets("@UI.Cheek", "cheek", optionPrefix + "/ExprCheek")
		addExpressionWidgets("@UI.Mouth", "mouth", optionPrefix + "/ExprMouth")
		addExpressionWidgets("@UI.Tongue", "tongue", optionPrefix + "/ExprTongue")
	end
	
	protected func void addExpressionWidgets(String label, String expressionIdPrefix, String optPrefix)
		addWidget(CollapsibleGroup.new(label, "", CollapsibleGroup.persistModel(optPrefix, false),\
		FormLayout.new(true, 10, 2), block Panel p
			var ComboBox comboBox
			
			pExpressions.forEach(block Expression each
				if each.info.getId().startsWith(expressionIdPrefix)
					p.addWidget(Label.new("@" + each.info.getName(), "Label.FormCaption"))
					
					comboBox = ComboBox.new(each.modelList, DefaultComboBoxEditor.new(each.modelText))
					comboBox.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
					comboBox.setToolTip("@" + each.info.getDescription())
					p.addWidget(comboBox)
				end
			end)
		end))
	end
end
