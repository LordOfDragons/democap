/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors
pin Democap.Characters
pin Democap.MotionTransferSystem

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery


/**
 * Edit character config window motion transfer ik panel.
 */
class WCCPMTIK extends WCCPanelMotionTransfer
	private var SharedListModel pModelListTipBone
	private var DefaultTextModel pModelTextTipBone
	private var SharedListModel pModelListBaseBone
	private var DefaultTextModel pModelTextBaseBone
	private var SharedListModel pModelListGuideBone
	private var DefaultTextModel pModelTextGuideBone
	private var WCCPMTTracker pEditTrackerTip
	private var WCCPMTTracker pEditTrackerGuide
	private var WCCPMTTracker pEditTrackerBase
	private var WCCPMTTracker pEditTrackerRelocate
	private var DefaultListModel pModelActorReach
	private var DefaultTextModel pModelReachLimit
	private var EditVector pEditReachScaling
	private var EditVector pEditReachScalingBack
	private var DefaultToggleModel pModelAdjustPosition
	private var SharedListModel pModelListMoveBaseIK
	private var DefaultTextModel pModelTextMoveBaseIK
	private var SharedListModel pModelListMoveRetracted
	private var DefaultTextModel pModelTextMoveRetracted
	private var DefaultTextModel pModelTwistPropagation
	private var DefaultTextModel pModelTwistBoneCount
	private var EditVector pEditTwistAxisRotation
	private var DefaultTextModel pModelGuideTwistStrength
	private var DefaultToggleModel pModelRelocateTip
	private var EditVector2 pEditBaseRotateRangeCenter
	private var EditVector2 pEditBaseRotateRangeMinimum
	private var EditVector2 pEditBaseRotateRangeMaximum
	private var EditVector2 pEditBaseRotateAngleMinimum
	private var EditVector2 pEditBaseRotateAngleMaximum
	
	
	
	/** Create window. */
	public func new(WindowCharacterConfiguration window, String optionPrefix) super(window)
		pModelTypeName.setText("Inverse Kinematic")
		
		pModelListTipBone = SharedListModel.new(window.getModelBones())
		pModelListTipBone.setAutoSelect(false)
		pModelTextTipBone = DefaultTextModel.new()
		
		pModelListBaseBone = SharedListModel.new(window.getModelBones())
		pModelListBaseBone.setAutoSelect(false)
		pModelTextBaseBone = DefaultTextModel.new()
		
		pModelListGuideBone = SharedListModel.new(window.getModelBones())
		pModelListGuideBone.setAutoSelect(false)
		pModelTextGuideBone = DefaultTextModel.new()
		
		pModelActorReach = DefaultListModel.new(MotionTransferIK.ActorReach.all().toArray())
		pModelReachLimit = DefaultTextModel.new()
		pModelAdjustPosition = DefaultToggleModel.new()
		pModelListMoveBaseIK = SharedListModel.new(window.getModelMoves())
		pModelListMoveBaseIK.setAutoSelect(false)
		pModelTextMoveBaseIK = DefaultTextModel.new()
		pModelListMoveRetracted = SharedListModel.new(window.getModelMoves())
		pModelListMoveRetracted.setAutoSelect(false)
		pModelTextMoveRetracted = DefaultTextModel.new()
		pModelTwistPropagation = DefaultTextModel.new()
		pModelTwistBoneCount = DefaultTextModel.new()
		pModelGuideTwistStrength = DefaultTextModel.new()
		pModelRelocateTip = DefaultToggleModel.new()
		
		runWhileBlockingLayout(block
			createContent(window.getModelTrackerSlotNames(), optionPrefix)
		end)
	end
	
	/** Dispose of window. */
	public func void dispose()
		pModelListTipBone = null
		pModelTextTipBone = null
		pModelListBaseBone = null
		pModelTextBaseBone = null
		pModelListGuideBone = null
		pModelTextGuideBone = null
		pEditTrackerTip.setTracker(null)
		pEditTrackerTip = null
		pEditTrackerGuide.setTracker(null)
		pEditTrackerGuide = null
		pEditTrackerBase.setTracker(null)
		pEditTrackerBase = null
		pEditTrackerRelocate = null
		pModelActorReach = null
		pModelReachLimit = null
		pEditReachScaling = null
		pModelAdjustPosition = null
		pModelListMoveBaseIK = null
		pModelTextMoveBaseIK = null
		pModelListMoveRetracted = null
		pModelTextMoveRetracted = null
		pModelTwistPropagation = null
		pModelTwistBoneCount = null
		pModelGuideTwistStrength = null
		pModelRelocateTip = null
		pEditTwistAxisRotation = null
		pEditBaseRotateRangeCenter = null
		pEditBaseRotateRangeMinimum = null
		pEditBaseRotateRangeMaximum = null
		pEditBaseRotateAngleMinimum = null
		pEditBaseRotateAngleMaximum = null
		super.dispose()
	end
	
	
	
	/** Load values from tracker slot object if present into edit widgets. */
	public func void loadValues()
		runWhileBlockingLayout(block
			super.loadValues()
			
			if pMotionTransfer != null
				var MotionTransferIK mt = pMotionTransfer cast MotionTransferIK
				pModelTextTipBone.setText(mt.getTipBone())
				pModelTextBaseBone.setText(mt.getBaseBone())
				pModelTextGuideBone.setText(mt.getGuideBone())
				pEditTrackerTip.setTracker(mt.getTipTracker())
				pEditTrackerTip.loadValues()
				pEditTrackerGuide.setTracker(mt.getGuideTracker())
				pEditTrackerGuide.loadValues()
				pEditTrackerBase.setTracker(mt.getBaseTracker())
				pEditTrackerBase.loadValues()
				pEditTrackerRelocate.setTracker(mt.getRelocateTracker())
				pEditTrackerRelocate.loadValues()
				pModelActorReach.setSelected(pModelActorReach.indexOf(mt.getActorReach()))
				pModelReachLimit.setText(mt.getReachLimit().toString())
				pEditReachScaling.setVector(mt.getReachScaling())
				pEditReachScalingBack.setVector(mt.getReachScalingBack())
				pModelAdjustPosition.setToggled(mt.getAdjustPosition())
				pModelTextMoveBaseIK.setText(mt.getMoveBaseIK())
				pModelTextMoveRetracted.setText(mt.getMoveRetracted())
				pModelTwistPropagation.setText(mt.getTwistPropagation().toString())
				pModelTwistBoneCount.setText(mt.getTwistBoneCount().toString())
				pEditTwistAxisRotation.setVector(mt.getTwistAxisRotation())
				pModelGuideTwistStrength.setText(mt.getGuideTwistStrength().toString())
				pModelRelocateTip.setToggled(mt.getRelocateTip())
				pEditBaseRotateRangeCenter.setVector2(mt.getBaseRotateRangeCenter())
				pEditBaseRotateRangeMinimum.setVector2(mt.getBaseRotateRangeMinimum())
				pEditBaseRotateRangeMaximum.setVector2(mt.getBaseRotateRangeMaximum())
				pEditBaseRotateAngleMinimum.setVector2(mt.getBaseRotateAngleMinimum())
				pEditBaseRotateAngleMaximum.setVector2(mt.getBaseRotateAngleMaximum())
				
			else
				pModelTextTipBone.setText("")
				pModelTextBaseBone.setText("")
				pModelTextGuideBone.setText("")
				pEditTrackerTip.setTracker(null)
				pEditTrackerGuide.setTracker(null)
				pEditTrackerBase.setTracker(null)
				pEditTrackerRelocate.setTracker(null)
				pModelActorReach.setSelected(pModelActorReach.indexOf(MotionTransferIK.ActorReach.none))
				pModelReachLimit.setText("")
				pEditReachScaling.setVector(Vector.new(1, 1, 1))
				pEditReachScalingBack.setVector(Vector.new(1, 1, 1))
				pModelAdjustPosition.setToggled(false)
				pModelTextMoveBaseIK.setText("")
				pModelTextMoveRetracted.setText("")
				pModelTwistPropagation.setText("")
				pModelTwistBoneCount.setText("")
				pEditTwistAxisRotation.setVector(Vector.new())
				pModelGuideTwistStrength.setText("")
				pModelRelocateTip.setToggled(false)
				pEditBaseRotateRangeCenter.setVector2(Vector2.new())
				pEditBaseRotateRangeMinimum.setVector2(Vector2.new())
				pEditBaseRotateRangeMaximum.setVector2(Vector2.new())
				pEditBaseRotateAngleMinimum.setVector2(Vector2.new())
				pEditBaseRotateAngleMaximum.setVector2(Vector2.new())
			end
		end)
		doLayoutIfBlocked()
	end
	
	/** Save value from edit widgets to tracker slot object if present. */
	public func void saveValues()
		if pMotionTransfer == null
			return
		end
		super.saveValues()
		
		var MotionTransferIK mt = pMotionTransfer cast MotionTransferIK
		mt.setTipBone(pModelTextTipBone.getText())
		mt.setBaseBone(pModelTextBaseBone.getText())
		mt.setGuideBone(pModelTextGuideBone.getText())
		pEditTrackerTip.saveValues()
		pEditTrackerGuide.saveValues()
		pEditTrackerBase.saveValues()
		pEditTrackerRelocate.saveValues()
		mt.setActorReach(pModelActorReach.getAt(pModelActorReach.getSelected()) cast MotionTransferIK.ActorReach)
		mt.setReachLimit(pModelReachLimit.getText().toFloat())
		mt.setReachScaling(pEditReachScaling.getVector())
		mt.setReachScalingBack(pEditReachScalingBack.getVector())
		mt.setAdjustPosition(pModelAdjustPosition.getToggled())
		mt.setMoveBaseIK(pModelTextMoveBaseIK.getText())
		mt.setMoveRetracted(pModelTextMoveRetracted.getText())
		mt.setTwistPropagation(pModelTwistPropagation.getText().toFloat())
		mt.setTwistBoneCount(pModelTwistBoneCount.getText().toInt())
		mt.setTwistAxisRotation(pEditTwistAxisRotation.getVector())
		mt.setGuideTwistStrength(pModelGuideTwistStrength.getText().toFloat())
		mt.setRelocateTip(pModelRelocateTip.getToggled())
		mt.setBaseRotateRangeCenter(pEditBaseRotateRangeCenter.getVector2())
		mt.setBaseRotateRangeMinimum(pEditBaseRotateRangeMinimum.getVector2())
		mt.setBaseRotateRangeMaximum(pEditBaseRotateRangeMaximum.getVector2())
		mt.setBaseRotateAngleMinimum(pEditBaseRotateAngleMinimum.getVector2())
		mt.setBaseRotateAngleMaximum(pEditBaseRotateAngleMaximum.getVector2())
	end
	
	/** Supports motion transfer type. */
	public func bool supports(MotionTransfer motionTranfer)
		return motionTranfer castable MotionTransferIK
	end
	
	
	
	/** Create window content. */
	protected func void createContent(DefaultListModel modelTrackerSlotNames, String optionPrefix)
		var WidgetCharacterPreview preview = pWindowCharacterConfiguration.getCharacterPreview()
		var ComboBox comboBox, comboBoxTip, comboBoxBase
		var Label label, labelTip, labelBase
		var TextField textField
		var Button button
		var ListBox listBox
		var CheckBox checkBox
		var String toolTip
		
		createBaseContent("motiontransferik")
		
		// bone chain
		labelTip = Label.new("Tip Bone:", "Label.FormCaption")
		pPanelForm.addWidget(labelTip)
		
		comboBoxTip = ComboBox.new(pModelListTipBone, DefaultComboBoxEditor.new(pModelTextTipBone))
		comboBoxTip.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
		comboBoxTip.setToolTip("Tip of bone chain.")
		pPanelForm.addWidget(comboBoxTip)
		
		labelBase = Label.new("Base Bone:", "Label.FormCaption")
		pPanelForm.addWidget(labelBase)
		
		comboBoxBase = ComboBox.new(pModelListBaseBone, DefaultComboBoxEditor.new(pModelTextBaseBone))
		comboBoxBase.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
		comboBoxBase.setToolTip("Base bone of the chain.")
		pPanelForm.addWidget(comboBoxBase)
		
		CPEBoneChain.new(preview, labelTip, comboBoxTip, null, comboBoxBase, false)
		CPEBoneChain.new(preview, null, comboBoxTip, labelBase, comboBoxBase, true)
		
		// guide bone
		label = Label.new("Guide Bone:", "Label.FormCaption")
		pPanelForm.addWidget(label)
		
		comboBox = ComboBox.new(pModelListGuideBone, DefaultComboBoxEditor.new(pModelTextGuideBone))
		comboBox.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
		comboBox.setToolTip("Guide of bone chain. This is the bone near the base of the chain to point at the guide tracker.")
		pPanelForm.addWidget(comboBox)
		
		CPEBone.new(preview, label, comboBox)
		
		// trackers
		pEditTrackerTip = WCCPMTTracker.new(modelTrackerSlotNames, optionPrefix + "/TipTracker", this, "Tip Tracker", false)
		addWidget(pEditTrackerTip)
		
		pEditTrackerGuide = WCCPMTTracker.new(modelTrackerSlotNames, optionPrefix + "/GuideTracker", this, "Guide Tracker", false)
		addWidget(pEditTrackerGuide)
		
		pEditTrackerBase = WCCPMTTracker.new(modelTrackerSlotNames, optionPrefix + "/BaseTracker", this, "Base Tracker", false)
		addWidget(pEditTrackerBase)
		
		pEditTrackerRelocate = WCCPMTTracker.new(modelTrackerSlotNames, optionPrefix + "/RelocateTracker", this, "Relocate Tracker", false)
		addWidget(pEditTrackerRelocate)
		
		// actor reach
		pPanelForm.addWidget(Label.new("Actor Reach:", "Label.FormCaption"))
		
		comboBox = ComboBox.new(pModelActorReach)
		comboBox.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
		comboBox.setToolTip("What actor profile parameter to use to scale reach length. "\
			+ " Scales the reach in actor space (VR device location) to match bone chain reach.")
		pPanelForm.addWidget(comboBox)
		
		// reach limit
		pPanelForm.addWidget(Label.new("Reach Limit:", "Label.FormCaption"))
		
		textField = TextField.new(pModelReachLimit, 6)
		textField.setDesignerSelector("TextField.WindowCharacterConfiguration")
		textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.positiveNumber))
		textField.setToolTip("Limit reach to percentage of bone chain reach. Avoids overstretching.")
		pPanelForm.addWidget(textField)
		
		// reach scaling
		toolTip = "Reach scaling relative to Twist Axis: positive X, positive Y, positive Z"
		
		label = Label.new("Reach Scaling:", "Label.FormCaption")
		label.setToolTip(toolTip)
		pPanelForm.addWidget(label)
		
		pEditReachScaling = EditVector.new()
		pEditReachScaling.setToolTip(toolTip)
		pPanelForm.addWidget(pEditReachScaling)
		
		// reach scaling backwards
		toolTip = "Reach scaling relative to Twist Axis: negative X, negative Y, negative Z"
		
		label = Label.new("Reach Scaling Back:", "Label.FormCaption")
		label.setToolTip(toolTip)
		pPanelForm.addWidget(label)
		
		pEditReachScalingBack = EditVector.new()
		pEditReachScalingBack.setToolTip(toolTip)
		pPanelForm.addWidget(pEditReachScalingBack)
		
		// adjust position
		pPanelForm.addWidget(Label.new(""))
		
		checkBox = CheckBox.new(pModelAdjustPosition, "Adjust Position")
		checkBox.setToolTip("Adjust position of Base Bone if Bone Count is 1")
		pPanelForm.addWidget(checkBox)
		
		// move base ik
		pPanelForm.addWidget(Label.new("Move Base IK:", "Label.FormCaption"))
		
		comboBox = ComboBox.new(pModelListMoveBaseIK, DefaultComboBoxEditor.new(pModelTextMoveBaseIK))
		comboBox.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
		comboBox.setToolTip("Animation move to use to init base IK state for better results. "\
			+ "Applied before applying inverse kinematic.")
		pPanelForm.addWidget(comboBox)
		
		// move retracted
		pPanelForm.addWidget(Label.new("Move Retracted:", "Label.FormCaption"))
		
		comboBox = ComboBox.new(pModelListMoveRetracted, DefaultComboBoxEditor.new(pModelTextMoveRetracted))
		comboBox.setDesignerSelector("ComboBox.WindowCharacterConfiguration")
		comboBox.setToolTip("Animation move to use to init retracted pose for better results. "\
			+ "Applied after Move Base IK and before inverse kinematic. "\
			+ "Percentage of capture bone chain length to original bone chain length is used "\
			+ "to blend move (start=relaxed, end=retracted).")
		pPanelForm.addWidget(comboBox)
		
		// twist propagation
		pPanelForm.addWidget(Label.new("Twist Propagation:", "Label.FormCaption"))
		
		textField = TextField.new(pModelTwistPropagation, 6)
		textField.setDesignerSelector("TextField.WindowCharacterConfiguration")
		textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.number))
		textField.setToolTip("Percentage of Tip Bone twist relative to Base Bone to propagate along bone chain.")
		pPanelForm.addWidget(textField)
		
		// twist bone count
		pPanelForm.addWidget(Label.new("Twist Bone Count:", "Label.FormCaption"))
		
		textField = TextField.new(pModelTwistBoneCount, 4)
		textField.setDesignerSelector("TextField.WindowCharacterConfiguration")
		textField.setValidator(SimpleTextValidator.new(4, SimpleTextValidator.CharacterClass.digits))
		textField.setToolTip("Count of bones along chain to apply twist to starting at first bone after Tip Bone.")
		pPanelForm.addWidget(textField)
		
		// twist axis rotation
		label = Label.new("Twist Axis Rotation:", "Label.FormCaption")
		pPanelForm.addWidget(label)
		
		pEditTwistAxisRotation = EditVector.new()
		pEditTwistAxisRotation.setToolTip("Rotation of twist axis relative to Tip Bone")
		pPanelForm.addWidget(pEditTwistAxisRotation)
		
		CPECoordinateSystemAxis.new(preview, null, null, label, pEditTwistAxisRotation, comboBoxTip, null, true)
		
		// guide twist
		pPanelForm.addWidget(Label.new("Guide Twist Strength:", "Label.FormCaption"))
		
		textField = TextField.new(pModelGuideTwistStrength, 6)
		textField.setDesignerSelector("TextField.WindowCharacterConfiguration")
		textField.setValidator(SimpleTextValidator.new(10, SimpleTextValidator.CharacterClass.number))
		textField.setToolTip("Percentage of Tip Bone twist relative to Base Bone to apply to guide bone.")
		pPanelForm.addWidget(textField)
		
		// relocate tip
		pPanelForm.addWidget(Label.new(""))
		
		checkBox = CheckBox.new(pModelAdjustPosition, "Relocate Tip")
		checkBox.setToolTip("Relocate tip from reference position to target position. Use if characters arms are located far away.")
		pPanelForm.addWidget(checkBox)
		
		// base bone rotation
		addWidget(CollapsibleGroup.new("Base Bone Rotation", "", CollapsibleGroup.persistModel(optionPrefix + "/BaseRotate", false),\
		FormLayout.new(true, 10, 2), block Panel p
			// range center
			toolTip = "Base bone rotation range center along X and Y axis. At range center applied rotation is 0."
			
			label = Label.new("Range Center:", "Label.FormCaption")
			label.setToolTip(toolTip)
			p.addWidget(label)
			
			pEditBaseRotateRangeCenter = EditVector2.new()
			pEditBaseRotateRangeCenter.setToolTip(toolTip)
			p.addWidget(pEditBaseRotateRangeCenter)
			
			// range minimum
			toolTip = "Base bone rotation range minimum along X and Y axis. At range minimum applied rotation is minimum angle."
			
			label = Label.new("Range Minimum:", "Label.FormCaption")
			label.setToolTip(toolTip)
			p.addWidget(label)
			
			pEditBaseRotateRangeMinimum = EditVector2.new()
			pEditBaseRotateRangeMinimum.setToolTip(toolTip)
			p.addWidget(pEditBaseRotateRangeMinimum)
			
			// range maximum
			toolTip = "Base bone rotation range maximum along X and Y axis. At range maximum applied rotation is maximum angle."
			
			label = Label.new("Range Maximum:", "Label.FormCaption")
			label.setToolTip(toolTip)
			p.addWidget(label)
			
			pEditBaseRotateRangeMaximum = EditVector2.new()
			pEditBaseRotateRangeMaximum.setToolTip(toolTip)
			p.addWidget(pEditBaseRotateRangeMaximum)
			
			// angle minimum
			toolTip = "Base bone rotation minimum angle along X and Y axis. At range minimum applied rotation is minimum angle."
			
			label = Label.new("Angle Minimum:", "Label.FormCaption")
			label.setToolTip(toolTip)
			p.addWidget(label)
			
			pEditBaseRotateAngleMinimum = EditVector2.new()
			pEditBaseRotateAngleMinimum.setToolTip(toolTip)
			p.addWidget(pEditBaseRotateAngleMinimum)
			
			// angle maximum
			toolTip = "Base bone rotation maximum angle along X and Y axis. At range maximum applied rotation is maximum angle."
			
			label = Label.new("Angle Maximum:", "Label.FormCaption")
			label.setToolTip(toolTip)
			p.addWidget(label)
			
			pEditBaseRotateAngleMaximum = EditVector2.new()
			pEditBaseRotateAngleMaximum.setToolTip(toolTip)
			p.addWidget(pEditBaseRotateAngleMaximum)
		end))
	end
	
	/** Motion transfer changed. */
	protected func void onMotionTransferChanged()
		if pEditTrackerTip != null
			pEditTrackerTip.setTracker(null)
		end
		if pEditTrackerGuide != null
			pEditTrackerGuide.setTracker(null)
		end
		if pEditTrackerBase != null
			pEditTrackerBase.setTracker(null)
		end
		if pEditTrackerRelocate != null
			pEditTrackerRelocate.setTracker(null)
		end
	end
end
