/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Actors
pin Democap.Actions
pin Democap.Behaviors
pin Democap.Characters
pin Democap.Worlds

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * ECBShowInteractHandles panel.
 */
class WMPSOShowInteractHandles extends WMPPanelSceneObject
	class HandleSizeChanged implements ActionListener
		protected var WMPSOShowInteractHandles pPanel
		protected var DefaultTextModel pModel
		
		func new(WMPSOShowInteractHandles panel, DefaultTextModel model)
			pPanel = panel
			pModel = model
		end
		
		func void onAction(ActionEvent event)
			var ECBShowInteractHandles.Instance behavior = pPanel.getBehaviorShowInteractHandles()
			
			behavior.setHandleSize(pModel.getText().toFloat())
			
			if behavior.getVisible()
				behavior.hide()
				behavior.show()
			end
		end
	end
	
	
	
	protected var ECBShowInteractHandles.Instance pBehaviorShowInteractHandles
	protected var DefaultTextModel pModelHandleSize
	
	
	
	/** Create panel. */
	func new(WMPanelProject panelProject, String bgpTypeId) super(panelProject, bgpTypeId,\
	"", "@UI.InteractHandles", "WMPSOShowInteractHandles", "ppob_interacthandles")
		pModelHandleSize = DefaultTextModel.new("0.2")
		
		getPanelContent().runWhileBlockingLayout(block
			createContent(getPanelContent())
		end)
	end
	
	/** Dispose of widget. */
	func void dispose()
		setBehavior(null)
		
		pModelHandleSize = null
		super.dispose()
	end
	
	
	
	/** ShowInteractHandles behavior. */
	func ECBShowInteractHandles.Instance getBehaviorShowInteractHandles()
		return pBehaviorShowInteractHandles
	end
	
	/** Update behavior. */
	func void updateBehavior()
		if pBehaviorShowInteractHandles == null
			return
		end
		
		pModelHandleSize.setText(pBehaviorShowInteractHandles.getHandleSize().toString())
	end
	
	
	
	/** Create panel content. */
	protected func void createContent(Panel content)
		var TextField textField
		
		content.setLayout(FlowLayout.new(LayoutAxis.y, 2))
		
		content.addWidget(Panel.new(FormLayout.new(true, 5, 2), block Panel p
			// handle size
			p.addWidget(Label.new("@UI.Size.Label", "Label.FormCaption.SingleLine"))
			
			textField = TextField.new(pModelHandleSize)
			textField.setValidator(SimpleTextValidator.new(8, SimpleTextValidator.CharacterClass.positiveNumber))
			textField.setToolTip("@UI.ToolTip.HandleSize")
			p.addWidget(textField)
			textField.addActionListener(HandleSizeChanged.new(this, pModelHandleSize))
		end))
	end
	
	protected func void onBehaviorChanged()
		pBehaviorShowInteractHandles = pBehavior cast ECBShowInteractHandles.Instance
	end
end
