/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2022, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Actors
pin Democap.Actions
pin Democap.Behaviors
pin Democap.Characters
pin Democap.Worlds

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * ECBTemporaryGhost panel.
 */
class WMPSOTemporaryGhost extends WMPPanelSceneObject
	class EnabledChanged extends DefaultToggleModelListener
		protected var WMPSOTemporaryGhost pPanel
		
		func new(WMPSOTemporaryGhost panel)
			pPanel = panel
		end
		
		func void toggledChanged(ToggleModel model)
			pPanel.getBehaviorTemporaryGhost().setEnabled(model.getToggled())
		end
	end
	
	class BehaviorEnabledChanged extends ECBTemporaryGhost.DefaultListener
		protected var DefaultToggleModel pModelEnabled
		
		func new(DefaultToggleModel modelEnabled)
			pModelEnabled = modelEnabled
		end
		
		func void enabledChanged(ECBTemporaryGhost.Instance instance)
			pModelEnabled.setToggled(instance.getEnabled())
		end
	end
	
	
	
	protected var ECBTemporaryGhost.Instance pBehaviorTemporaryGhost
	protected var DefaultToggleModel pModelEnabled
	protected var BehaviorEnabledChanged pEnabledChanged
	
	
	
	/** Create panel. */
	func new(WMPanelProject panelProject, String bgpTypeId) super(panelProject, bgpTypeId,\
	"", "@UI.TemporaryGhost", "WMPSOTemporaryGhost", "ppob_temporaryghost")
		pModelEnabled = DefaultToggleModel.new(false)
		
		getPanelContent().runWhileBlockingLayout(block
			createContent(getPanelContent())
		end)
		
		pModelEnabled.addListener(EnabledChanged.new(this))
		
		pEnabledChanged = BehaviorEnabledChanged.new(pModelEnabled)
	end
	
	/** Dispose of widget. */
	func void dispose()
		setBehavior(null)
		
		pEnabledChanged = null
		pModelEnabled = null
		
		super.dispose()
	end
	
	
	
	/** TemporaryGhost behavior. */
	func ECBTemporaryGhost.Instance getBehaviorTemporaryGhost()
		return pBehaviorTemporaryGhost
	end
	
	/** Update behavior. */
	func void updateBehavior()
		if pBehaviorTemporaryGhost == null
			return
		end
		
		pModelEnabled.setToggled(pBehaviorTemporaryGhost.getEnabled())
	end
	
	
	
	/** Create panel content. */
	protected func void createContent(Panel content)
		var CheckBox checkBox
		
		content.setLayout(FlowLayout.new(LayoutAxis.y, 2))
		
		content.addWidget(Panel.new(FormLayout.new(true, 5, 2), block Panel p
			// enabled
			p.addWidget(Label.new("", "Label.FormCaption.SingleLine"))
			
			checkBox = CheckBox.new(pModelEnabled, "@UI.Enabled")
			checkBox.setToolTip("@UI.ToolTip.ReplaceTexturesGhost")
			p.addWidget(checkBox)
		end))
	end
	
	protected func void onBehaviorChanged()
		if pBehaviorTemporaryGhost != null
			if pBehaviorTemporaryGhost.getElement() != null // dispose protection
				pBehaviorTemporaryGhost.removeListener(pEnabledChanged)
			end
		end
		
		pBehaviorTemporaryGhost = pBehavior cast ECBTemporaryGhost.Instance
		
		if pBehaviorTemporaryGhost != null
			pBehaviorTemporaryGhost.addListener(pEnabledChanged)
		end
	end
end
