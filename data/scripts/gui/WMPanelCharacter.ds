/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Actors
pin Democap.Actions
pin Democap.Characters

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery



/**
 * Main window character panel.
 */
class WMPanelCharacter extends CollapsibleGroup
	/** Actor profile list element renderer. */
	class ActorProfileRenderer extends DefaultListElementRenderer
		public func new()
		end
		
		public func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text = String.new(' ', 20)
			if element != null
				text = (element cast ActorProfile).getName()
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Actor profile selection listener. */
	class ActorProfileSelection extends DefaultListModelListener
		protected var WMPanelCharacter pWindow
		
		public func new(WMPanelCharacter window)
			pWindow = window
		end
		
		public func void contentChanged(ListModel model, int fromIndex, int toIndex)
			selectionChanged(model)
		end
		
		public func void selectionChanged(ListModel model)
			var int index = model.getSelected()
			if index != -1
				pWindow.setActorProfile(model.getAt(index) cast ActorProfile)
				
			else
				pWindow.setActorProfile(null)
			end
		end
	end
	
	/** Character profile list element renderer. */
	class CharacterProfileRenderer extends DefaultListElementRenderer
		public func new()
		end
		
		public func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text = String.new(' ', 20)
			if element != null
				text = (element cast CharacterProfile).getName()
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Character profile selection listener. */
	class CharacterProfileSelection extends DefaultListModelListener
		protected var WMPanelCharacter pWindow
		
		public func new(WMPanelCharacter window)
			pWindow = window
		end
		
		public func void contentChanged(ListModel model, int fromIndex, int toIndex)
			pWindow.updateCharacterConfigurations()
			selectionChanged(model)
		end
		
		public func void selectionChanged(ListModel model)
			var int index = model.getSelected()
			if index != -1
				pWindow.setCharacterProfile(model.getAt(index) cast CharacterProfile)
				
			else
				pWindow.setCharacterProfile(null)
			end
		end
	end
	
	/** Character configuration list element renderer. */
	class CharacterConfigurationRenderer extends DefaultListElementRenderer
		public func new()
		end
		
		public func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text = String.new(' ', 20)
			if element != null
				text = (element cast CharacterConfiguration).getName()
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Character configuration selection listener. */
	class CharacterConfigurationSelection extends DefaultListModelListener
		protected var WMPanelCharacter pWindow
		
		public func new(WMPanelCharacter window)
			pWindow = window
		end
		
		public func void contentChanged(ListModel model, int fromIndex, int toIndex)
			selectionChanged(model)
		end
		
		public func void selectionChanged(ListModel model)
			var int index = model.getSelected()
			if index != -1
				pWindow.setCharacterConfiguration(model.getAt(index) cast CharacterConfiguration)
				
			else
				pWindow.setCharacterConfiguration(null)
			end
		end
	end
	
	/** Actor profiles listener. */
	class ListenActorProfiles extends DefaultActorProfilesListener
		protected var WMPanelCharacter pWindow
		
		public func new(WMPanelCharacter window)
			pWindow = window
		end
		
		public func void profileAdded(ActorProfiles profiles, ActorProfile profile)
			pWindow.updateActorProfiles()
		end
		
		public func void profileRemoved(ActorProfiles profiles, ActorProfile profile)
			pWindow.updateActorProfiles()
		end
		
		public func void allProfilesRemoved(ActorProfiles profiles)
			pWindow.updateActorProfiles()
		end
	end
	
	/** Character profiles listener. */
	class ListenCharacterProfiles extends DefaultCharacterProfilesListener
		protected var WMPanelCharacter pWindow
		
		public func new(WMPanelCharacter window)
			pWindow = window
		end
		
		public func void profileAdded(CharacterProfiles profiles, CharacterProfile profile)
			pWindow.updateCharacterProfiles()
		end
		
		public func void profileRemoved(CharacterProfiles profiles, CharacterProfile profile)
			pWindow.updateCharacterProfiles()
		end
		
		public func void allProfilesRemoved(CharacterProfiles profiles)
			pWindow.updateCharacterProfiles()
		end
	end
	
	/** Character configurations listener. */
	class ListenCharacterConfigurations extends DefaultCharacterConfigurationsListener
		protected var WMPanelCharacter pWindow
		
		public func new(WMPanelCharacter window)
			pWindow = window
		end
		
		public func void configurationAdded(CharacterConfigurations configurations, CharacterConfiguration configuration)
			pWindow.updateCharacterConfigurations()
		end
		
		public func void configurationRemoved(CharacterConfigurations configurations, CharacterConfiguration configuration)
			pWindow.updateCharacterConfigurations()
		end
		
		public func void allConfigurationsRemoved(CharacterConfigurations configurations)
			pWindow.updateCharacterConfigurations()
		end
	end
	
	/** Actor profile popup menu. */
	class ActorProfilePopupAction extends DefaultActionListener
		private var WMPanelCharacter pPanel
		
		public func new(WMPanelCharacter panel)
			pPanel = panel
		end
		
		public func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var WindowPopup popup = WindowPopup.new()
			var Button button
			
			button = Button.new("Add Actor...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.addActorProfile()
			end))
			popup.addWidget(button)
			
			button = Button.new("Duplicate Actor...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.duplicateActorProfile()
			end))
			popup.addWidget(button)
			
			button = Button.new("Remove Actor", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.removeActorProfile()
			end))
			button.setEnabled(pPanel.getActorProfile() != null)
			popup.addWidget(button)
			
			button = Button.new("Edit Actor...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.editActorProfile()
			end))
			popup.addWidget(button)
			
			button = Button.new("Browse Actors...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.browseActorProfiles()
			end))
			popup.addWidget(button)
			
			pPanel.getDesktop().addWindow(popup)
			popup.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	/** Character profile popup menu. */
	class CharacterProfilePopupAction extends DefaultActionListener
		private var WMPanelCharacter pPanel
		
		public func new(WMPanelCharacter panel)
			pPanel = panel
		end
		
		public func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var WindowPopup popup = WindowPopup.new()
			var Button button
			
			button = Button.new("Add Character...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.addCharacterProfile()
			end))
			popup.addWidget(button)
			
			button = Button.new("Duplicate Character...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.duplicateCharacterProfile()
			end))
			popup.addWidget(button)
			
			button = Button.new("Remove Character", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.removeCharacterProfile()
			end))
			button.setEnabled(pPanel.getCharacterProfile() != null)
			popup.addWidget(button)
			
			button = Button.new("Edit Character...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.editCharacterProfile()
			end))
			popup.addWidget(button)
			
			button = Button.new("Browse Characters...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.browseCharacterProfiles()
			end))
			popup.addWidget(button)
			
			button = Button.new("Browse Models...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.browseCharacterModels()
			end))
			popup.addWidget(button)
			
			pPanel.getDesktop().addWindow(popup)
			popup.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	/** Character configuration popup menu. */
	class CharacterConfigurationPopupAction extends DefaultActionListener
		private var WMPanelCharacter pPanel
		
		public func new(WMPanelCharacter panel)
			pPanel = panel
		end
		
		public func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var WindowPopup popup = WindowPopup.new()
			var Button button
			
			button = Button.new("Add Configuration...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.addCharacterConfiguration()
			end))
			popup.addWidget(button)
			
			button = Button.new("Duplicate Configuration...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.duplicateCharacterConfiguration()
			end))
			popup.addWidget(button)
			
			button = Button.new("Remove Configuration", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.removeCharacterConfiguration()
			end))
			button.setEnabled(pPanel.getCharacterProfile() != null)
			popup.addWidget(button)
			
			button = Button.new("Edit Configuration...", BlockActionListener.new(block ActionEvent event
				popup.popdown()
				pPanel.editCharacterConfiguration()
			end))
			popup.addWidget(button)
			
			pPanel.getDesktop().addWindow(popup)
			popup.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	
	
	protected var DefaultListModel pModelActorProfiles
	protected var DefaultListModel pModelCharacterProfiles
	protected var DefaultListModel pModelCharacterConfigurations
	protected var ListenCharacterConfigurations pListenCharacterConfigurations
	protected var ActorProfile pActorProfile
	protected var CharacterProfile pCharacterProfile
	protected var CharacterConfiguration pCharacterConfiguration
	protected var bool pInVR
	
	
	/** Create character panel. */
	public func new(bool inVR) super("Character Selection", CollapsibleGroup.persistModel(prefixUIOptions(inVR)))
		pInVR = inVR
		
		pModelActorProfiles = DefaultListModel.new()
		pModelCharacterProfiles = DefaultListModel.new()
		pModelCharacterConfigurations = DefaultListModel.new()
		pListenCharacterConfigurations = ListenCharacterConfigurations.new(this)
		
		getPanelContent().runWhileBlockingLayout(block
			createContent(getPanelContent())
		end)
		
		pModelActorProfiles.addListener(ActorProfileSelection.new(this))
		pModelCharacterProfiles.addListener(CharacterProfileSelection.new(this))
		pModelCharacterConfigurations.addListener(CharacterConfigurationSelection.new(this))
		
		GameApp.getGameApp().getActorProfiles().addListener(ListenActorProfiles.new(this))
		updateActorProfiles()
		
		GameApp.getGameApp().getCharacterProfiles().addListener(ListenCharacterProfiles.new(this))
		updateCharacterProfiles()
	end
	
	/** Dispose of widget. */
	public func void dispose()
		setCharacterProfile(null)
		setActorProfile(null)
		
		pModelActorProfiles = null
		pModelCharacterProfiles = null
		pModelCharacterConfigurations = null
		pListenCharacterConfigurations = null
		
		super.dispose()
	end
	
	/** UI options prefix. */
	static public func String prefixUIOptions(bool inVR)
		return inVR if "UI/VR/WMPanelCharacter" else "UI/WMPanelCharacter"
	end
	
	
	
	/** Shown in VR. */
	public func bool getInVR()
		return pInVR
	end
	
	
	
	/** Selected actor profile or null. */
	public func ActorProfile getActorProfile()
		return pActorProfile
	end
	
	/** Set selected actor profile or null. */
	public func void setActorProfile(ActorProfile profile)
		if profile == pActorProfile
			return
		end
		
		pActorProfile = profile
	end
	
	/** Update actor profiles model. */
	public func void updateActorProfiles()
		var ActorProfile profile = pActorProfile
		
		pModelActorProfiles.setContent(GameApp.getGameApp().getActorProfiles().toArray().sorted())
		
		var int index = pModelActorProfiles.indexOf(profile)
		if index != -1
			pModelActorProfiles.setSelected(index)
			pModelActorProfiles.notifyContentChanged(index, index)
		end
	end
	
	/** Add actor profile. */
	public func void addActorProfile()
		WindowDialog.input(getWindow(), "Add Actor Profile", "Profile Name:", null, "Actor",\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null
					var ActorProfiles profiles = GameApp.getGameApp().getActorProfiles()
					
					if profiles.hasNamed(result)
						WindowDialog.message(getWindow(), "Add Actor Profile", "Actor profile '"\
							+ result + "' exists already", null, null, null)
						
					else
						var ActorProfile profile = ActorProfile.new(result)
						profile.save()
						profiles.add(profile)
						pModelActorProfiles.setSelected(pModelActorProfiles.indexOf(profile))
						editActorProfile()
					end
				end
			end))
	end
	
	/** Duplicate actor profile. */
	public func void duplicateActorProfile()
		if pActorProfile == null
			return
		end
		
		WindowDialog.input(getWindow(), "Duplicate Actor Profile", "Profile Name:", null,\
			pActorProfile.getName(), null, null, WindowDialog.BlockResultListener.new(\
			block String result
				if result != null
					var ActorProfiles profiles = GameApp.getGameApp().getActorProfiles()
					
					if profiles.hasNamed(result)
						WindowDialog.message(getWindow(), "Duplicate Actor Profile", "Actor profile '"\
							+ result + "' exists already", null, null, null)
						
					else
						var ActorProfile profile = ActorProfile.new(result, pActorProfile)
						profile.setName(result)
						profile.save()
						profiles.add(profile)
						pModelActorProfiles.setSelected(pModelActorProfiles.indexOf(profile))
					end
				end
			end))
	end
	
	/** Remove actor profile. */
	public func void removeActorProfile()
		if pActorProfile == null
			return
		end
		
		WindowDialog.question(getWindow(), "Remove Actor Profile",\
			"Do you really want to remove actor profile '" + pActorProfile.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Remove", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					var ActorProfiles profiles = GameApp.getGameApp().getActorProfiles()
					pActorProfile.delete()
					profiles.remove(pActorProfile)
				end
			end))
	end
	
	/** Edit actor profile. */
	public func void editActorProfile()
		if pActorProfile == null
			return
		end
		
		WindowActorProfile.new(getWindow(), pInVR, pActorProfile,\
			WindowDialog.BlockResultListener.new(block bool accepted
				updateActorProfiles()
			end))
	end
	
	/** Browse actor profiles in overlay directory. */
	public func void browseActorProfiles()
		FileSystem.browseConfig(GameApp.getGameApp().getActorProfiles().getDirectory())
	end
	
	
	
	/** Selected character profile or null. */
	public func CharacterProfile getCharacterProfile()
		return pCharacterProfile
	end
	
	/** Set selected character profile or null. */
	public func void setCharacterProfile(CharacterProfile profile)
		if profile == pCharacterProfile
			return
		end
		
		if pCharacterProfile != null
			pCharacterProfile.getConfigurations().removeListener(pListenCharacterConfigurations)
		end
		
		pCharacterProfile = profile
		
		if profile != null
			profile.getConfigurations().addListener(pListenCharacterConfigurations)
		end
		
		updateCharacterConfigurations()
	end
	
	/** Update character profiles model. */
	public func void updateCharacterProfiles()
		var CharacterProfile profile = pCharacterProfile
		
		pModelCharacterProfiles.setContent(GameApp.getGameApp().getCharacterProfiles().toArray().sorted())
		
		var int index = pModelCharacterProfiles.indexOf(profile)
		if index != -1
			pModelCharacterProfiles.setSelected(index)
		end
	end
	
	/** Add character profile. */
	public func void addCharacterProfile()
		WindowDialog.input(getWindow(), "Add Character Profile", "Profile Name:", null, "Character",\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null
					var CharacterProfiles profiles = GameApp.getGameApp().getCharacterProfiles()
					
					if profiles.hasNamed(result)
						WindowDialog.message(getWindow(), "Add Character Profile", "Character profile '"\
							+ result + "' exists already", null, null, null)
						
					else
						var CharacterProfile profile = CharacterProfile.new(result)
						profile.save()
						profiles.add(profile)
						pModelCharacterProfiles.setSelected(pModelCharacterProfiles.indexOf(profile))
						editCharacterProfile()
					end
				end
			end))
	end
	
	/** Duplicate character profile. */
	public func void duplicateCharacterProfile()
		if pCharacterProfile == null
			return
		end
		
		WindowDialog.input(getWindow(), "Duplicate Character Profile", "Profile Name:",\
			null, pCharacterProfile.getName(), null, null, WindowDialog.BlockResultListener.new(\
			block String result
				if result != null
					var CharacterProfiles profiles = GameApp.getGameApp().getCharacterProfiles()
					
					if profiles.hasNamed(result)
						WindowDialog.message(getWindow(), "Duplicate Character Profile", "Character profile '"\
							+ result + "' exists already", null, null, null)
						
					else
						var CharacterProfile profile = CharacterProfile.new(result, pCharacterProfile)
						profile.setName(result)
						profile.save()
						profiles.add(profile)
						pModelCharacterProfiles.setSelected(pModelCharacterProfiles.indexOf(profile))
					end
				end
			end))
	end
	
	/** Remove character profile. */
	public func void removeCharacterProfile()
		if pCharacterProfile == null
			return
		end
		
		WindowDialog.question(getWindow(), "Remove Character Profile",\
			"Do you really want to remove character profile '" + pCharacterProfile.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Remove", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					var CharacterProfiles profiles = GameApp.getGameApp().getCharacterProfiles()
					pCharacterProfile.delete()
					profiles.remove(pCharacterProfile)
				end
			end))
	end
	
	/** Edit character profile. */
	public func void editCharacterProfile()
		if pCharacterProfile != null
			WindowCharacterProfile.new(getWindow(), pInVR, pCharacterProfile,\
				WindowDialog.BlockResultListener.new(block bool accepted
					updateCharacterProfiles()
				end))
		end
	end
	
	/** Browse character profiles in overlay directory. */
	public func void browseCharacterProfiles()
		FileSystem.browseConfig(GameApp.getGameApp().getCharacterProfiles().getDirectory())
	end
	
	/** Browse character models in overlay directory. */
	public func void browseCharacterModels()
		FileSystem.browseOverlay("/content/models/characters")
	end
	
	
	
	/** Selected character configuration or null. */
	public func CharacterConfiguration getCharacterConfiguration()
		return pCharacterConfiguration
	end
	
	/** Set selected character configuration or null. */
	public func void setCharacterConfiguration(CharacterConfiguration configuration)
		if configuration == pCharacterConfiguration
			return
		end
		
		pCharacterConfiguration = configuration
	end
	
	/** Selected character configuration or null. */
	public func CharacterConfiguration getSelectedCharacterConfiguration()
		var int index = pModelCharacterConfigurations.getSelected()
		if index != -1
			return pModelCharacterConfigurations.getAt(index) cast CharacterConfiguration
		end
		return null
	end
	
	/** Update character configurations model. */
	public func void updateCharacterConfigurations()
		var CharacterConfiguration selected = getSelectedCharacterConfiguration()
		
		if pCharacterProfile != null
			pModelCharacterConfigurations.setContent(pCharacterProfile.getConfigurations().toArray().sorted())
			
		else
			pModelCharacterConfigurations.removeAll()
		end
		
		var int index = pModelCharacterConfigurations.indexOf(selected)
		if index != -1
			pModelCharacterConfigurations.setSelected(index)
		end
	end
	
	/** Add character configuration. */
	public func void addCharacterConfiguration()
		if pCharacterProfile == null
			return
		end
		
		WindowDialog.input(getWindow(), "Add Character Configuration", "Configuration Name:", null, "Configuration",\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null
					var CharacterConfigurations configurations = pCharacterProfile.getConfigurations()
					
					if configurations.hasNamed(result)
						WindowDialog.message(getWindow(), "Add Character Configuration", "Character configuration '"\
							+ result + "' exists already", null, null, null)
						
					else
						var CharacterConfiguration configuration = CharacterConfiguration.new(pCharacterProfile, result)
						configurations.add(configuration)
						pCharacterProfile.save()
						pModelCharacterConfigurations.setSelected(pModelCharacterConfigurations.indexOf(configuration))
						editCharacterConfiguration()
					end
				end
			end))
	end
	
	/** Duplicate character configuration. */
	public func void duplicateCharacterConfiguration()
		if pCharacterConfiguration == null or pCharacterProfile == null
			return
		end
		
		WindowDialog.input(getWindow(), "Duplicate Character Configuration", "Configuration Name:",\
			null, pCharacterConfiguration.getName(), null, null, WindowDialog.BlockResultListener.new(\
			block String result
				if result != null
					var CharacterConfigurations configurations = pCharacterProfile.getConfigurations()
					
					if configurations.hasNamed(result)
						WindowDialog.message(getWindow(), "Duplicate Character Configuration", "Character configuration '"\
							+ result + "' exists already", null, null, null)
						
					else
						var CharacterConfiguration configuration = CharacterConfiguration.new(\
							pCharacterProfile, result, pCharacterConfiguration)
						configuration.setName(result)
						configurations.add(configuration)
						pCharacterProfile.save()
						pModelCharacterConfigurations.setSelected(pModelCharacterConfigurations.indexOf(configuration))
					end
				end
			end))
	end
	
	/** Remove character configuration. */
	public func void removeCharacterConfiguration()
		if pCharacterConfiguration == null or pCharacterProfile == null
			return
		end
		
		WindowDialog.question(getWindow(), "Remove Character Configuration",\
			"Do you really want to remove character configuration '" + pCharacterConfiguration.getName() + "'?",\
			null, Array.newWith(WindowDialog.ButtonConfiguration.new("Remove", true), \
				WindowDialog.ButtonConfiguration.new("Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pCharacterProfile.getConfigurations().remove(pCharacterConfiguration)
					pCharacterProfile.save()
				end
			end))
	end
	
	/** Edit character configuration. */
	public func void editCharacterConfiguration()
		if pCharacterConfiguration != null and pCharacterProfile !=  null
			WindowCharacterConfiguration.new(getWindow(), pInVR, pCharacterConfiguration,\
				WindowDialog.BlockResultListener.new(block bool accepted
					updateCharacterConfigurations()
				end))
		end
	end
	
	/** Activate selected character configuration if present. */
	public func void activateCharacterConfiguration()
		if pActorProfile == null
			WindowDialog.message(getWindow(), "Activate Configuration", "No actor selected", null, null, null)
			return
		end
		
		if pCharacterConfiguration == null
			WindowDialog.message(getWindow(), "Activate Configuration", "No character configuration selected", null, null, null)
			return
		end
		
		GameApp.getGameApp().getWorldSpawnCharacter().setCharacter(pCharacterConfiguration)
	end
	
	/** Calibrate character. */
	public func void calibrateCharacter()
		var BaseVRActor actor = GameApp.getGameApp().getWorldSpawnCharacter().getActor()
		if actor == null
			return
		end
		
		var AAControlCommands aacc = actor.getAIAction().getAction() cast AAControlCommands
		if aacc != null
			aacc.startCalibrate()
		end
	end
	
	
	
	/** Create panel content. */
	protected func void createContent(Panel content)
		var Button button
		
		content.setLayout(BorderLayout.new(2))
		
		content.addWidget(Panel.new(FormLayout.new(true, 5, 2), block Panel p
			p.addWidget(Label.new("Actors:", "Label.FormCaption"))
			
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelActorProfiles, ActorProfileRenderer.new()))
				
				button = Button.new("...", ActorProfilePopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
			
			p.addWidget(Label.new("Character:", "Label.FormCaption"))
			
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelCharacterProfiles, CharacterProfileRenderer.new()))
				
				button = Button.new("...", CharacterProfilePopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
			
			p.addWidget(Label.new("Configuration:", "Label.FormCaption"))
			
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelCharacterConfigurations, CharacterConfigurationRenderer.new()))
				
				button = Button.new("...", CharacterConfigurationPopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
		end), BorderLayout.Area.content)
		
		content.addWidget(Panel.new(GridLayout.new(2, 0), block Panel p
			button = Button.new("Activate", BlockActionListener.new(block ActionEvent event
				activateCharacterConfiguration()
			end))
			button.setToolTip("Activate character. Adds character to world under actor control")
			p.addWidget(button)
			
			button = Button.new("Calibrate", BlockActionListener.new(block ActionEvent event
				calibrateCharacter()
			end))
			button.setToolTip("Start calibrating character. Follow instructions displayed in HMD")
			p.addWidget(button)
		end), BorderLayout.Area.bottom)
	end
end
