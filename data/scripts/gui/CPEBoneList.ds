/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors

pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts
pin Dragengine.Scenery
pin Dragengine.Utils.StyledText.Definition
pin Dragengine.Utils.StyledText.Definition.Nodes


/**
 * Character preview editor to select multiple bones.
 */
class CPEBoneList extends CharacterPreviewEditor
	/**
	 * List element renderer to ask for bone to select. Modified version of
	 * DefaultListElementRenderer supporting ECBPreviewBones.Instance.Bone
	 */
	class SelectBoneRenderer extends DefaultListElementRenderer
		func new()
			setDefaultMinimumSize(Point.new(250, 0))
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var ECBPreviewBones.Instance.Bone bone = element cast ECBPreviewBones.Instance.Bone
			if bone == null
				// null element typically is the one asking for the default size
				super.updateRenderer(listBox, renderer, String.new('X', 15), selected, focused)
				return
			end
			
			var String text = bone.name
			
			if bone.color.equals(CharacterPreviewEditor.previewBoneColorSelected)
				text = "{} [{}]".format(Array.newWith(text, TranslationManager.get().translate("UI.CPEBone.Selected").toUTF8()))
				
			elif bone.color.equals(CharacterPreviewEditor.previewBoneColorChain)
				text = "{} [{}]".format(Array.newWith(text, TranslationManager.get().translate("UI.CPEBone.Chain").toUTF8()))
				
			elif bone.color.equals(CharacterPreviewEditor.previewBoneColorEnd)
				text = "{} [{}]".format(Array.newWith(text, TranslationManager.get().translate("UI.CPEBone.ChainEnd").toUTF8()))
			end
			
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Element clicked listener. */
	class ElementClicked extends ClickedActionListener
		var CPEBoneList pEditor
		
		func new(CPEBoneList editor)
			pEditor = editor
		end
		
		func void onAction()
			pEditor.onElementClicked()
		end
	end
	
	
	
	protected var Label pLabelBone
	protected var ListBox pListBones
	protected var ElementClicked pElementClicked
	protected var SelectBoneRenderer pSelectBoneRenderer
	
	
	
	/** Create character preview editor. */
	func new(WidgetCharacterPreview preview, Label labelBones, ListBox listBones)\
	this(preview, labelBones, listBones, true)
	end
	
	protected func new(WidgetCharacterPreview preview, Label labelBones, ListBox listBones,\
	bool addListeners) super(preview)
		if listBones == null
			throw ENullPointer.new("listBones")
		end
		pListBones = listBones
		pLabelBone = labelBones
		pSelectBoneRenderer = SelectBoneRenderer.new()
		
		if addListeners
			if labelBones != null
				labelBones.addMouseListener(ClickedActivateEditor.new(this, listBones))
				labelBones.addMouseListener(ParentWheelActionListener.new(labelBones.getParent()))
			end
			
			listBones.addFocusListener(ReceiveFocusActivateEditor.new(this))
			listBones.getModel().addListener(UpdateOnListChanged.new(this))
		end
	end
	
	
	
	/** List box bone. */
	func ListBox getListBoxBone()
		return pListBones
	end
	
	
	
	/** Editor has been activated. */
	func void onActivate()
		updateBoneColors()
		
		pElementClicked = ElementClicked.new(this)
		getPreview().getInteractionPanel().addMouseListener(pElementClicked)
		
		if pLabelBone != null
			pLabelBone.setDesignerSelector("Label.FormCaption.Hilight")
		end
	end
	
	/** Editor has been deactivated. */
	func void onDeactivate()
		if pElementClicked != null
			getPreview().getInteractionPanel().removeMouseListener(pElementClicked)
			pElementClicked = null
		end
		
		var ECBPreviewBones.Instance bones = getPreviewBones()
		if bones != null
			bones.setAllBoneColor(previewBoneColorDefault)
		end
		
		if pLabelBone != null
			pLabelBone.setDesignerSelector("Label.FormCaption")
		end
	end
	
	/** Model value changed. */
	func void onModelValueChanged()
		if isActive()
			updateBoneColors()
		end
	end
	
	/** Frame update. */
	func void onFrameUpdate(float elapsed)
		updateToolTip()
	end
	
	/** Element clicked. */
	func void onElementClicked()
		var Array hits = Array.new()
		getPreview().viewRayCollisions(hits)
		
		if hits.getCount() > 0
			selectElement(hits)
		end
	end
	
	
	
	/** Update preview bone colors. */
	protected func void updateBoneColors()
		var ECBPreviewBones.Instance bones = getPreviewBones()
		if bones == null
			return
		end
		
		bones.setAllBoneColor(previewBoneColorDefault)
		
		var ListModel model = pListBones.getModel()
		var int i, count = model.getCount()
		for i = 0 to count
			bones.setBoneColor(model.getAt(i) cast String, previewBoneColorSelected)
		end
	end
	
	/** Select element. */
	protected func void selectElement(Array hits)
		// filter preview bones
		hits = hits.collectCastable(block WidgetCharacterPreview.HitResult each
			return each.getObject() castable ECBPreviewBones.Instance.Bone
		end)
		
		// if more than one bone is hit ask which one to select
		if hits.getCount() > 1
			// model has to contain bones so map the array
			var Array bones = hits.map(block WidgetCharacterPreview.HitResult each
				return each.getObject() cast ECBPreviewBones.Instance.Bone
			end)
			
			var DefaultListModel model = DefaultListModel.new(bones)
			
			// (de)select bone if possible
			var String selectedName
			
			if pListBones.getSelectedElement() != -1
				selectedName = pListBones.getModel().getAt(pListBones.getSelectedElement()) cast String
			end
			
			if selectedName != null
				var ECBPreviewBones.Instance.Bone bone = bones.find(block ECBPreviewBones.Instance.Bone each
					return each.name.equals(selectedName)
				end) cast ECBPreviewBones.Instance.Bone
				
				if bone != null
					model.setSelected(bones.indexOf(bone))
				end
			end
			
			// show dialog to make user choose bone
			WindowDialog.chooseList(getPreview().getWindow(), "@UI.CPEBoneList.SelectBones.Title",\
				"@UI.CPEBoneList.SelectBones.Message", null, model, pSelectBoneRenderer, null, null, \
				WindowDialog.BlockResultListener.new(block int result
					if result != -1
						selectElement(Array.newWith(hits.getAt(result)))
					end
				end))
			
			// result listener will call this method again with modified hits array
			return
		end
		
		// if only one bone is hit (de)select it
		if hits.getCount() == 1
			var WidgetCharacterPreview.HitResult hit = hits.getAt(0) cast WidgetCharacterPreview.HitResult
			var ECBPreviewBones.Instance.Bone bone = hit.getObject() cast ECBPreviewBones.Instance.Bone
			
			var DefaultListModel model = pListBones.getModel() cast DefaultListModel
			if model.has(bone.name)
				model.remove(bone.name)
				
			else
				model.add(bone.name)
				model.setSelected(model.indexOf(bone.name))
			end
		end
	end
	
	/** Edit combo box. */
	protected func ListBox getEditListBox()
		return pListBones
	end
end
