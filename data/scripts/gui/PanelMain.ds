/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Scenery


/**
 * Main panel.
 */
class PanelMain extends Panel
	var DMCWindowConsole pWindowConsole
	
	
	/** Create main window */
	func new(Desktop desktop, bool inVR)
		runWhileBlockingLayout(block
			setDesignerSelector("Panel.Main")
			createContent(inVR)
		end)
		
		// create console window and add it to desktop
		pWindowConsole = DMCWindowConsole.new(desktop)
	end
	
	/** Dispose of window. */
	func void dispose()
		pWindowConsole = null
		super.dispose()
	end
	
	
	/** Console window. */
	func DMCWindowConsole getWindowConsole()
		return pWindowConsole
	end
	
	
	/** Create window content. */
	protected func void createContent(bool inVR)
		setLayout(BorderLayout.new(2))
		
		// switch panel buttons
		var ToggleButtonGroup tbgroup = ToggleButtonGroup.new()
		var ToggleButton tbutton
		
		addWidget(Panel.new(BoxLayout.new(LayoutAxis.x), block Panel p2
			tbutton = ToggleButton.new(" ")
			tbutton.setDesignerSelector("Button.Category.Settings")
			tbutton.setToolTip("@UI.ToolTip.Category.Settings")
			p2.addWidget(tbutton)
			tbutton.setButtonGroup(tbgroup)
			
			tbutton = ToggleButton.new(" ")
			tbutton.setDesignerSelector("Button.Category.Character")
			tbutton.setToolTip("@UI.ToolTip.Category.Character")
			p2.addWidget(tbutton)
			tbutton.setButtonGroup(tbgroup)
			
			tbutton = ToggleButton.new(" ")
			tbutton.setDesignerSelector("Button.Category.Project")
			tbutton.setToolTip("@UI.ToolTip.Category.Project")
			p2.addWidget(tbutton)
			tbutton.setButtonGroup(tbgroup)
			
			tbutton = ToggleButton.new(" ")
			tbutton.setDesignerSelector("Button.Category.ActorControl")
			tbutton.setToolTip("@UI.ToolTip.Category.Actor")
			p2.addWidget(tbutton)
			tbutton.setButtonGroup(tbgroup)
			
			tbutton = ToggleButton.new(" ")
			tbutton.setDesignerSelector("Button.Category.Capture")
			tbutton.setToolTip("@UI.ToolTip.Category.Session")
			p2.addWidget(tbutton)
			tbutton.setButtonGroup(tbgroup)
			
			p2.addMouseListener(SwitchPanel.ScrollHeaderToggleButtons.new(tbgroup))
		end), BorderLayout.Area.top)
		
		// switch panels
		var String prefixUIOptions = inVR if "UI/VR/PanelMain" else "UI/PanelMain"
		
		addWidget(SwitchPanel.new(block SwitchPanel p
			// settings panel
			p.addWidget(ScrollPanel.new(Viewport.new(Panel.new(FlowLayout.new(LayoutAxis.y, 5), block Panel p2
				p2.addWidget(WMPanelSettings.new(inVR, prefixUIOptions))
			end), 0, 20, true), ScrollPanel.Policy.hidden, ScrollPanel.Policy.needed))
			
			// character panel
			p.addWidget(ScrollPanel.new(Viewport.new(Panel.new(FlowLayout.new(LayoutAxis.y, 5), block Panel p2
				p2.addWidget(WMPanelCharacter.new(inVR, prefixUIOptions))
			end), 0, 20, true), ScrollPanel.Policy.hidden, ScrollPanel.Policy.needed))
			
			// project panel
			var WMPanelProject panelProject = WMPanelProject.new(inVR, prefixUIOptions)
			var ScrollPanel scrollPanel = ScrollPanel.new(Viewport.new(Panel.new(FlowLayout.new(LayoutAxis.y, 5), block Panel p2
				p2.addWidget(panelProject)
			end), 0, 20, true), ScrollPanel.Policy.hidden, ScrollPanel.Policy.needed)
			p.addWidget(scrollPanel)
			
			var WMPanelProject.EnableUpdateObjectsListener enableUpdateObjects = \
				WMPanelProject.EnableUpdateObjectsListener.new(panelProject, Array.newWith(scrollPanel, this))
			scrollPanel.addWidgetListener(enableUpdateObjects)
			addWidgetListener(enableUpdateObjects)
			
			// actor control panel
			p.addWidget(ScrollPanel.new(Viewport.new(Panel.new(FlowLayout.new(LayoutAxis.y, 5), block Panel p2
				p2.addWidget(WMPanelActorControl.new(inVR, prefixUIOptions))
			end), 0, 20, true), ScrollPanel.Policy.hidden, ScrollPanel.Policy.needed))
			
			// session panel
			p.addWidget(ScrollPanel.new(Viewport.new(Panel.new(FlowLayout.new(LayoutAxis.y, 5), block Panel p2
				p2.addWidget(WMPanelSession.new(inVR, prefixUIOptions))
			end), 0, 20, true), ScrollPanel.Policy.hidden, ScrollPanel.Policy.needed))
			
			tbgroup.addListener(SwitchPanel.SwitchToggleGroup.new(p))
		end), BorderLayout.Area.content)
		
		addWidget(Panel.new(BoxLayout.new(LayoutAxis.x), block Panel p
			p.addWidget(Button.new("@UI.Quit", QuitGameActionListener.new()))
			
			p.addWidget(Button.new("@UI.Console", BlockActionListener.new(block ActionEvent event
				pWindowConsole.setVisible(true)
			end)))
		end), BorderLayout.Area.bottom)
	end
end
