/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Actors
pin Democap.Actions
pin Democap.Behaviors
pin Democap.Characters
pin Democap.Worlds

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Preloading
pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * Main window project panel.
 */
class WMPanelProject extends Panel
	class EnableUpdateObjectsListener extends DefaultWidgetListener
		protected var WMPanelProject pPanel
		protected var Array pWidgets
		
		func new(WMPanelProject panel, Array widgets)
			pPanel = panel
			pWidgets = widgets
		end
		
		func void onWidgetShown(WidgetEvent event)
			pPanel.setEnableObjectUpdates(pWidgets.find(block Widget each
				return not each.getVisible()
			end) == null)
		end
		
		func void onWidgetHidden(WidgetEvent event)
			pPanel.setEnableObjectUpdates(false)
		end
	end
	
	/** Game changes listener. */
	class GameChangedListener extends DefaultGameListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void projectChanged(GameApp app)
			pPanel.setProject(app.getProject())
		end
		
		func void sceneChanged(GameApp app)
			pPanel.setScene(app.getScene())
		end
		
		func void gameWorldChanged(GameApp app)
			pPanel.updateModelObjects()
		end
		
		func void savedAnimationsChanged(GameApp app)
			pPanel.updateSavedAnimations()
		end
	end
	
	
	
	/** Project popup menu. */
	class ProjectPopupAction extends DefaultActionListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			item = MenuItemCommand.new("@UI.New.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.newProject()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Open.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.openProject()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Save", BlockActionListener.new(block ActionEvent event
				pPanel.saveProject()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.ReloadProject", BlockActionListener.new(block ActionEvent event
				pPanel.reloadProject()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.BrowseData.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.browseProjectData()
			end))
			menu.addWidget(item)
			
			pPanel.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	
	
	/** Scenes changed listener. */
	class ScenesChangedListener extends DefaultScenesListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void sceneAdded(Scenes scenes, Scene scene)
			pPanel.updateModelScenes()
		end
		
		func void sceneRemoved(Scenes scenes, Scene scene)
			pPanel.updateModelScenes()
		end
		
		func void allScenesRemoved(Scenes scenes)
			pPanel.updateModelScenes()
		end
	end
	
	/** Scene list element renderer. */
	class SceneListElementRenderer extends DefaultListElementRenderer
		func new()
			setDefaultMinimumSize(Point.new(50, 10))
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text = ""
			if element != null
				text = (element cast Scene).getName()
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Scene selection listener. */
	class SelectScene extends DefaultListModelListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void contentChanged(ListModel model, int fromIndex, int toIndex)
			selectionChanged(model)
		end
		
		func void selectionChanged(ListModel model)
			if not pPanel.preventUpdate
				var int index = model.getSelected()
				if index != -1
					GameApp.getGameApp().setScene(model.getAt(index) cast Scene)
				end
			end
		end
	end
	
	/** Scene changed listener. */
	class SceneChangedListener extends DefaultSceneListener
		protected var WMPanelProject pPanel
		protected var TimerBlock pTimer
		protected var bool pUpdateModelScenes
		protected var bool pUpdateModelObjects
		protected var ECBSceneObject.Instance pUpdateModelObjectName
		protected var bool pUpdateObject
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void sceneNameChanged(Scene scene)
			pUpdateModelScenes = true
			pStartTimer()
		end
		
		func void objectAdded(Scene scene, ECBSceneObject.Instance object)
			pUpdateModelObjects = true
			pUpdateModelObjectName = null
			pStartTimer()
		end
		
		func void objectRemoved(Scene scene, ECBSceneObject.Instance object)
			pUpdateModelObjects = true
			pUpdateModelObjectName = null
			pStartTimer()
		end
		
		func void allObjectsRemoved(Scene scene)
			pUpdateModelObjects = true
			pUpdateModelObjectName = null
			pUpdateObject = false
			pStartTimer()
		end
		
		func void objectNameChanged(Scene scene, ECBSceneObject.Instance object)
			if pUpdateModelObjectName == null
				pUpdateModelObjectName = object
			else
				pUpdateModelObjectName = null
				pUpdateModelObjects = true
			end
			pStartTimer()
		end
		
		func void objectGeometryChanged(Scene scene, ECBSceneObject.Instance object)
			if object == pPanel.getObject()
				pUpdateObject = true
				pStartTimer()
			end
		end
		
		func void objectParameterChanged(Scene scene, ECBSceneObject.Instance object)
			if object == pPanel.getObject()
				pUpdateObject = true
				pStartTimer()
			end
		end
		
		protected func void pStartTimer()
			if pTimer != null
				return
			end
			
			pTimer = TimerBlock.new(0, false, block
				pRunTimer()
			end)
		end
		
		protected func void pRunTimer()
			pTimer = null
			if pUpdateModelScenes
				pUpdateModelScenes = false
				pPanel.updateModelObjects()
			end
			if pUpdateModelObjects
				pUpdateModelObjects = false
				pPanel.updateModelObjects()
			end
			if pUpdateModelObjectName != null
				pPanel.updateModelObjectName(pUpdateModelObjectName)
				pUpdateModelObjectName = null
			end
			if pUpdateObject
				pUpdateObject = false
				pPanel.updateObject()
			end
		end
	end
	
	/** Scene popup menu. */
	class ScenePopupAction extends DefaultActionListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			item = MenuItemCommand.new("@UI.Add.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.addScene()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Duplicate.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.duplicateScene()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Rename.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.renameScene()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Remove.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.removeScene()
			end))
			menu.addWidget(item)
			
			pPanel.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	
	
	/** Loadable world list element renderer. */
	class LoadableWorldRenderer extends DefaultListElementRenderer
		func new()
			setDesignerSelector("Label.ListRenderer.WorldSelection")
			setDefaultMinimumSize(Point.new(50, 10))
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var LoadableWorld loadableWorld = element cast LoadableWorld
			var Label label = renderer cast Label
			var String selector = getDesignerSelector()
			
			if selected
				selector = selector + ".Selected"
			end
			if loadableWorld != null
				var Project project = GameApp.getGameApp().getProject()
				if project == null or not project.getDataDirectory().isParentOf(loadableWorld.getPath())
					selector = selector + ".Global"
				end
			end
			
			label.setDesignerSelector(selector)
			
			if loadableWorld == null
				label.setText(String.new(' ', 20))
				
			else
				label.setText(loadableWorld.getName())
			end
		end
	end
	
	/** Loadable worlds listener. */
	class ListenLoadableWorlds extends DefaultLoadableWorldsListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void worldAdded(LoadableWorlds worlds, LoadableWorld world)
			pPanel.updateModelLoadableWorlds()
		end
		
		func void worldRemoved(LoadableWorlds worlds, LoadableWorld world)
			pPanel.updateModelLoadableWorlds()
		end
		
		func void allWorldsRemoved(LoadableWorlds worlds)
			pPanel.updateModelLoadableWorlds()
		end
	end
	
	/** Loadable world popup menu. */
	class LoadableWorldPopupAction extends DefaultActionListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			item = MenuItemCommand.new("@UI.ActivateWorld", BlockActionListener.new(block ActionEvent event
				pPanel.activateWorld()
			end))
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.BrowseWorlds.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.browseLoadableWorlds()
			end))
			menu.addWidget(item)
			
			pPanel.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	
	
	/** Saved animation list element renderer. */
	class SavedAnimationRenderer extends DefaultListElementRenderer
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
			setDefaultMinimumSize(Point.new(50, 10))
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text = ""
			if element != null
				var CharacterAnimation animation = element cast CharacterAnimation
				text = animation.getName()
				
				if pPanel.getProject() != null
					text = text + " [" + animation.getCharacterProfile(pPanel.getProject()) + "]"
				end
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Saved animation popup menu. */
	class SavedAnimationPopupAction extends DefaultActionListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			item = MenuItemCommand.new("@UI.Rename.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.renameSavedAnimation()
			end))
			item.setToolTip("@UI.ToolTip.RenameSavedAnimation")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Load", BlockActionListener.new(block ActionEvent event
				pPanel.loadSavedAnimation()
			end))
			item.setToolTip("@UI.ToolTip.LoadSavedAnimation")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Remove", BlockActionListener.new(block ActionEvent event
				pPanel.removeSavedAnimation()
			end))
			item.setToolTip("@UI.ToolTip.RemoveSavedAnimation")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Export.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.exportSavedAnimation()
			end))
			item.setToolTip("@UI.ToolTip.ExportSavedAnimation")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.BrowseExported.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.browseExportedAnimations()
			end))
			item.setToolTip("@UI.ToolTip.BrowseExportedAnimations")
			menu.addWidget(item)
			
			pPanel.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	
	
	/** Object element renderer. */
	class ObjectElementRenderer extends DefaultListElementRenderer
		func new()
			setDefaultMinimumSize(Point.new(80, 20))
		end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text = ""
			if element != null
				text = (element cast ECBSceneObject.Instance).getDisplayName()
				
			else
				text = "@UI.ListElement.None"
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	/** Select object listener. */
	class SelectObject extends DefaultListModelListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void contentChanged(ListModel model, int fromIndex, int toIndex)
			selectionChanged(model)
		end
		
		func void selectionChanged(ListModel model)
			if pPanel.preventUpdateObjects
				return
			end
			
			var ECBSelectable.Instance selectable
			var int index = model.getSelected()
			if index != -1
				var ECBSceneObject.Instance sceneObject = model.getAt(index) cast ECBSceneObject.Instance
				if sceneObject != null
					selectable = ECBSelectable.getInstanceIn(sceneObject.getElement())
				end
			end
			GameApp.getGameApp().getSelectionTracker().setElement(selectable)
		end
	end
	
	/** Selected selectable object changed. */
	class SelectedSelectableObjectChanged extends ECBSelectable.Tracker.DefaultListener
		protected var WMPanelProject pPanel
		protected var TimerBlock pTimer
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void selectedElementChanged(ECBSelectable.Tracker tracker)
			pStartTimer()
		end
		
		protected func void pStartTimer()
			if pTimer != null
				return
			end
			
			pTimer = TimerBlock.new(0, false, block
				pTimer = null
				pPanel.selectObjectFromTracker()
			end)
		end
	end
	
	/** Object popup menu. */
	class ObjectPopupAction extends DefaultActionListener
		var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			
			item = MenuItemCommand.new("@UI.Add.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.addObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectAdd")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Duplicate", BlockActionListener.new(block ActionEvent event
				pPanel.duplicateObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectDuplicate")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Remove", BlockActionListener.new(block ActionEvent event
				pPanel.removeObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectRemove")
			item.setEnabled(pPanel.getObject() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.RemoveAll", BlockActionListener.new(block ActionEvent event
				pPanel.removeAllObjects()
			end))
			item.setToolTip("@UI.ToolTip.RemoveAllObjects")
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.ChangeClass.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.changeObjectClass()
			end))
			item.setToolTip("@UI.ToolTip.ObjectChangeClass")
			item.setEnabled(pPanel.getObject() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Rename.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.renameObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectRename")
			item.setEnabled(pPanel.getObject() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Cut.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.cutObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectCut")
			item.setEnabled(pPanel.getObject() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Copy", BlockActionListener.new(block ActionEvent event
				pPanel.copyObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectCopy")
			item.setEnabled(pPanel.getObject() != null)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Paste", BlockActionListener.new(block ActionEvent event
				pPanel.pasteObject()
			end))
			item.setToolTip("@UI.ToolTip.ObjectPaste")
			item.setEnabled(pPanel.getClipboard().hasClip())
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.BrowseObjects.Dialog", BlockActionListener.new(block ActionEvent event
				pPanel.browseObjects()
			end))
			item.setToolTip("@UI.ToolTip.ObjectBrowse")
			menu.addWidget(item)
			
			pPanel.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	class SelectParent extends DefaultListModelListener
		protected var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void contentChanged(ListModel model, int fromIndex, int toIndex)
			selectionChanged(model)
		end
		
		func void selectionChanged(ListModel model)
			if pPanel.preventUpdateObjects
				return
			end
			
			var ECBSceneObject.Instance object = pPanel.getObject()
			if object == null or object.getElement().getID().equals(UniqueID.new())
				return
			end
			
			var ECBSceneObject.Instance parent
			var int index = model.getSelected()
			if index != -1
				parent = model.getAt(index) cast ECBSceneObject.Instance
			end
			
			if parent == object
				WindowDialog.message(pPanel.getWindow(), "@UI.SetParent",\
					"@UI.Message.NoSelfAttach", null, null, null)
				parent = null
			end
			
			var UniqueID parentId = parent != null if parent.getElement().getID() else null
			var BehaviorElement element = object.getElement()
			var UniqueID oldParentId = element.getStub().getAttachTo()
			if parentId == oldParentId or (parentId != null and parentId.equals(oldParentId))
				return
			end
			
			element.getStub().setAttachTo(parentId)
			pPanel.elementStubChanged()
		end
	end
	
	class ParentPopupAction extends DefaultActionListener
		var WMPanelProject pPanel
		
		func new(WMPanelProject panel)
			pPanel = panel
		end
		
		func void onAction(ActionEvent event)
			var Widget widget = event.getSource() cast Widget
			var MenuPopup menu = MenuPopup.new()
			var MenuItemCommand item
			var bool canAttach = pPanel.getObject() != null and pPanel.getObject().getECBehavior().canRemove.getValue()
			
			item = MenuItemCommand.new("@UI.AttachClosest", BlockActionListener.new(block ActionEvent event
				pPanel.objectAttachClosest()
			end))
			item.setToolTip("@UI.ToolTip.ObjectAttachClosest")
			item.setEnabled(canAttach)
			menu.addWidget(item)
			
			item = MenuItemCommand.new("@UI.Detach", BlockActionListener.new(block ActionEvent event
				pPanel.objectDetach()
			end))
			item.setToolTip("@UI.ToolTip.ObjectDetach")
			item.setEnabled(canAttach)
			menu.addWidget(item)
			
			pPanel.getDesktop().addWindow(menu)
			menu.popup(widget.getDesktopPosition() + Point.new(0, widget.getHeight()))
		end
	end
	
	/** Object position changed. */
	class ObjectPositionChanged implements ActionListener
		var WMPanelProject pPanel
		var EditVector pEditVector
		
		func new(WMPanelProject panel, EditVector editVector)
			pPanel = panel
			pEditVector = editVector
		end
		
		func void onAction(ActionEvent event)
			var ECBSceneObject.Instance object = pPanel.getObject()
			if object != null and not pEditVector.isEqualTo(object.getElement().getPosition().toVector())
				ECBShowInteractHandles.directMoveObjectTo(object.getElement(), DVector.new(pEditVector.getVector()))
			end
		end
	end
	
	/** Object rotation changed. */
	class ObjectRotationChanged implements ActionListener
		var WMPanelProject pPanel
		var EditVector pEditVector
		
		func new(WMPanelProject panel, EditVector editVector)
			pPanel = panel
			pEditVector = editVector
		end
		
		func void onAction(ActionEvent event)
			var ECBSceneObject.Instance object = pPanel.getObject()
			if object != null and not pEditVector.isEqualTo(object.getElement().getOrientation().getEulerAngles())
				ECBShowInteractHandles.directRotateObjectTo(object.getElement(), Quaternion.newFromEuler(pEditVector.getVector()))
			end
		end
	end
	
	/** Object class help button. */
	class ObjectHelpButton extends Button
		var WMPanelProject pPanel
		
		func new(WMPanelProject panel) super("?")
			pPanel = panel
			setDesignerSelector("Button.Help")
			setToolTip("@UI.ToolTip.HelpButton")
		end
		
		func void fireAction(int modifiers)
			super.fireAction(modifiers)
			
			var ECBSceneObject.Instance object = pPanel.getObject()
			if object != null
				var ECBHelpTopic.Instance helpTopic = ECBHelpTopic.getInstanceIn(object.getElement())
				if helpTopic != null and not helpTopic.getHelpTopic().empty()
					FileSystem.openUrl("https://developer.dragondreams.ch/wiki/doku.php/democap:" + helpTopic.getHelpTopic())
				end
			end
		end
	end
	
	
	
	public var bool preventUpdate
	
	protected var DefaultTextModel pModelProjectFilename
	protected var DefaultListModel pModelScenes
	protected var DefaultListModel pModelLoadableWorlds
	protected var DefaultListModel pModelSavedAnimations
	protected var DefaultListModel pModelObjects
	protected var DefaultTextModel pModelObjectClass
	protected var DefaultListModel pModelParent
	protected var EditVector pEditObjectPosition
	protected var EditVector pEditObjectRotation
	protected var ECBSceneObject.Instance pObject
	protected var bool pInVR
	protected var bool pDisposing
	protected var String pPrefixUIOptions
	protected var Project pProject
	protected var Scene pScene
	protected var ScenesChangedListener pScenesChangedListener
	protected var SceneChangedListener pSceneChangedListener
	protected var ListenLoadableWorlds pListenLoadableWorlds
	protected var GameChangedListener pGameChangedListener
	protected var SelectedSelectableObjectChanged pSelectedSelectableObjectChanged
	protected var CollapsibleGroup pPanelParenting
	protected var Panel pPanelBehaviors
	protected var ECBBehaviorGuiPanels.Manager pBGPManager
	protected var bool pEnableObjectUpdates
	
	public var bool preventUpdateObjects
	
	
	
	/** Create panel. */
	func new(bool inVR, String prefixUIOptions)
		pInVR = inVR
		pPrefixUIOptions = prefixUIOptions + "/WMPanelProject"
		pEnableObjectUpdates = true
		pBGPManager = ECBBehaviorGuiPanels.Manager.new(this)
		
		pModelProjectFilename = DefaultTextModel.new("")
		pModelScenes = DefaultListModel.new()
		pModelScenes.setAutoSelect(false)
		pModelLoadableWorlds = DefaultListModel.new()
		pModelSavedAnimations = DefaultListModel.new()
		
		pModelObjects = DefaultListModel.new()
		pModelObjects.setAutoSelect(false)
		
		pModelObjectClass = DefaultTextModel.new("")
		
		pModelParent = DefaultListModel.new()
		
		runWhileBlockingLayout(block
			createContent()
		end)
		
		var GameApp app = GameApp.getGameApp()
		pListenLoadableWorlds = ListenLoadableWorlds.new(this)
		app.getLoadableWorlds().addListener(pListenLoadableWorlds)
		updateModelLoadableWorlds()
		
		pModelScenes.addListener(SelectScene.new(this))
		pModelObjects.addListener(SelectObject.new(this))
		pModelParent.addListener(SelectParent.new(this))
		
		pScenesChangedListener = ScenesChangedListener.new(this)
		pSceneChangedListener = SceneChangedListener.new(this)
		
		pGameChangedListener = GameChangedListener.new(this)
		app.addListener(pGameChangedListener)
		
		pSelectedSelectableObjectChanged = SelectedSelectableObjectChanged.new(this)
		app.getSelectionTracker().addListener(pSelectedSelectableObjectChanged)
		
		setProject(app.getProject())
		setScene(app.getScene(), true)
	end
	
	/** Dispose of widget. */
	func void dispose()
		pDisposing = true
		
		var GameApp app = GameApp.getGameApp()
		if pSelectedSelectableObjectChanged != null
			app.getSelectionTracker().removeListener(pSelectedSelectableObjectChanged)
		end
		if pListenLoadableWorlds != null
			app.getLoadableWorlds().removeListener(pListenLoadableWorlds)
		end
		if pGameChangedListener != null
			app.removeListener(pGameChangedListener)
		end
		
		setObject(null)
		setScene(null)
		setProject(null)
		
		pSelectedSelectableObjectChanged = null
		pSceneChangedListener = null
		pScenesChangedListener = null
		pGameChangedListener = null
		pListenLoadableWorlds = null
		
		pModelParent = null
		pModelObjectClass = null
		pModelObjects = null
		pModelLoadableWorlds = null
		pModelSavedAnimations = null
		pModelScenes = null
		pModelProjectFilename = null
		pEditObjectPosition = null
		pEditObjectRotation = null
		pPanelBehaviors = null
		
		if pBGPManager != null
			pBGPManager.dispose()
			pBGPManager = null
		end
		
		super.dispose()
	end
	
	
	
	/** Shown in VR. */
	func bool getInVR()
		return pInVR
	end
	
	/** UI Option Prefix. */
	func String getPrefixUIOptions()
		return pPrefixUIOptions
	end
	
	/** Scene objects model. */
	func DefaultListModel getModelObjects()
		return pModelObjects
	end
	
	func bool getEnableObjectUpdates()
		return pEnableObjectUpdates
	end
	
	func void setEnableObjectUpdates(bool enable)
		if enable == pEnableObjectUpdates
			return
		end
		
		pEnableObjectUpdates = enable
		
		if enable
			selectObjectFromTracker()
			
		else
			setObject(null)
		end
	end
	
	
	
	/** Active project or null. */
	func Project getProject()
		return pProject
	end
	
	/** Set active project or null. */
	func void setProject(Project project)
		if project == pProject
			return
		end
		
		setScene(null)
		
		if pProject != null and pProject.getScenes() != null // dispose protection
			pProject.getScenes().removeListener(pScenesChangedListener)
		end
		
		pProject = project
		
		if project != null
			project.getScenes().addListener(pScenesChangedListener)
			pModelProjectFilename.setText(File.new(project.getPath()).getName())
			
		else
			pModelProjectFilename.setText("")
		end
		
		updateModelScenes()
		updateSavedAnimations()
	end
	
	
	
	/** Create new project. */
	func void newProject()
		if pProject == null
			return
		end
		
		var GameApp app = GameApp.getGameApp()
		
		DialogFileSelect.showSaveFile(getWindow(), "@UI.NewProject", app.getPatternsProject(),\
		File.new(app.getPathProjects(), "Project.demcp").getPath(),\
			DialogFileSelect.BlockResultListener.new(block String result
				if result != null
					if FileSystem.existsFile(result)
						WindowDialog.message(getWindow(), "@UI.NewProject",\
							"@UI.Message.DuplicateProject", null, null, null)
						
					else
						try
							var Project project = Project.new(result)
							project.getScenes().add(Scene.new("Scene"))
							project.save()
							app.setProject(project)
							app.setScene(project.getScenes().getAt(0))
							
						catch Exception e
							app.getConsole().addError("create project failed: " + result, e)
							WindowDialog.message(getWindow(), "@UI.NewProject",\
								"{}\n{}\n{}".format(Array.newWith(\
									TranslationManager.get().translate("UI.Message.NewProjectFailed").toUTF8(),\
									result, e)), null, null, null)
						end
					end
				end
			end))
	end
	
	/** Open project. */
	func void openProject()
		if pProject == null
			return
		end
		
		var GameApp app = GameApp.getGameApp()
		
		DialogFileSelect.showOpenFile(getWindow(), "@UI.OpenProject",\
		app.getPatternsProject(), pProject.getPath(),\
			DialogFileSelect.BlockResultListener.new(block String result
				if result != null
					try
						var Project project = Project.load(result)
						app.setProject(project)
						app.setScene(project.getScenes().getNamed(project.getPostLoadActivateScene()))
						
					catch Exception e
						app.getConsole().addError("open project failed: " + result, e)
						WindowDialog.message(getWindow(), "@UI.OpenProject", "{}\n{}\n{}".format(Array.newWith(\
							TranslationManager.get().translate("UI.Message.OpenProjectFailed").toUTF8(),\
							result, e)), null, null, null)
					end
				end
			end))
	end
	
	/** Save project. */
	func void saveProject()
		if pProject != null
			pProject.save()
		end
	end
	
	/** Browse project data in overlay directory. */
	func void browseProjectData()
		if pProject == null
			return
		end
		
		var String path = pProject.getDataDirectory().getPath()
		if path.startsWith("/config")
			FileSystem.browseConfig(path)
			
		elif path.startsWith("/capture")
			FileSystem.browseCapture(path)
			
		else
			FileSystem.browseOverlay(path)
		end
	end
	
	/** Reload project. */
	func void reloadProject()
		if pProject == null
			return
		end
		
		var GameApp app = GameApp.getGameApp()
		
		try
			pProject.save()
			
			var Project project = Project.load(pProject.getPath())
			app.setProject(project)
			app.setScene(project.getScenes().getNamed(project.getPostLoadActivateScene()))
			
		catch Exception e
			app.getConsole().addError("reload project failed", e)
			WindowDialog.message(getWindow(), "@UI.ReloadProject", "@UI.Message.ReloadProjectFailed", null, null, null)
		end
	end
	
	
	
	/** Update scenes model. */
	func void updateModelScenes()
		var Array content = Array.new()
		if GameApp.getGameApp().getProject() != null
			content = GameApp.getGameApp().getProject().getScenes().toArray().sorted(block Scene a, Scene b
				return a.getName().compare(b.getName())
			end)
		end
		
		preventUpdate = true
		try
			pModelScenes.setSelected(-1)
			pModelScenes.setContent(content)
			
		catch Exception e
			preventUpdate = false
			throw
		end
		preventUpdate = false
		
		selectScene()
	end
	
	/** Select scene. */
	func void selectScene()
		pModelScenes.setSelected(pModelScenes.indexOf(GameApp.getGameApp().getScene()))
	end
	
	/** Active scene or null. */
	func Scene getScene()
		return pScene
	end
	
	/** Set active scene or null. */
	func void setScene(Scene scene)
		setScene(scene, false)
	end
	
	func void setScene(Scene scene, bool noLoad)
		if scene == pScene
			return
		end
		
		if pScene != null
			pScene.removeListener(pSceneChangedListener)
		end
		
		pScene = scene
		
		if scene != null
			scene.addListener(pSceneChangedListener)
		end
		
		selectScene()
		
		if scene != null and not noLoad
			var Object world = pModelLoadableWorlds.find(block LoadableWorld each
				return each.getPath().equals(scene.getPathWorld())
			end)
			if world != null
				pModelLoadableWorlds.setSelected(pModelLoadableWorlds.indexOf(world))
			end
		end
		
		updateModelObjects()
	end
	
	/** Add scene. */
	func void addScene()
		var LoadableWorld world = getLoadableWorld()
		if world == null
			return
		end
		
		WindowDialog.input(getWindow(), "@UI.AddScene",\
			"@UI.Name.Label", null, "Scene", null, null,\
			WindowDialog.BlockResultListener.new(block String result
				if result != null
					if result.empty()
						WindowDialog.message(getWindow(), "@UI.AddScene",\
							"@UI.Message.EmptySceneName", null, null, null)
						
					elif pProject.getScenes().hasNamed(result)
						WindowDialog.message(getWindow(), "@UI.AddScene",\
							"@UI.Message.DuplicateScene", null, null, null)
						
					else
						var Scene scene = Scene.new(result)
						scene.setPathWorld(world.getPath())
						pProject.getScenes().add(scene)
						GameApp.getGameApp().setScene(scene)
					end
				end
			end))
	end
	
	/** Duplicate scene. */
	func void duplicateScene()
		if pScene == null
			return
		end
		
		WindowDialog.input(getWindow(), "@UI.DuplicateScene",\
			"@UI.Name.Label", null, pScene.getName(),\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null
					if result.empty()
						WindowDialog.message(getWindow(), "@UI.DuplicateScene",\
							"@UI.Message.EmptySceneName", null, null, null)
						
					elif pProject.getScenes().hasNamed(result)
						WindowDialog.message(getWindow(), "@UI.DuplicateScene",\
							"@UI.Message.DuplicateScene", null, null, null)
						
					else
						var Scene scene = Scene.new(result, pScene)
						pProject.getScenes().add(scene)
						GameApp.getGameApp().setScene(scene)
					end
				end
			end))
	end
	
	/** Remove scene. */
	func void removeScene()
		if pScene == null
			return
		end
		
		if pProject.getScenes().getCount() == 1
			WindowDialog.message(getWindow(), "@UI.RemoveScene",\
				"@UI.Message.RemoveLastScene", null, null, null)
			return
		end
		
		WindowDialog.question(getWindow(), "@UI.RemoveScene",\
			TranslationManager.get().translate("UI.Message.AskRemoveScene")\
				.toUTF8().format(Array.newWith(pScene.getName())), null, Array.newWith(\
				WindowDialog.ButtonConfiguration.new("@UI.Remove", true),\
				WindowDialog.ButtonConfiguration.new("@UI.Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					var Scene scene = pScene
					var int index = pModelScenes.indexOf(scene)
					if index < pModelScenes.getCount() - 1
						index++
						
					else
						index--
					end
					GameApp.getGameApp().setScene(pModelScenes.getAt(index) cast Scene)
					pProject.getScenes().remove(scene)
					pProject.save()
				end
		end))
	end
	
	/** Rename scene. */
	func void renameScene()
		if pScene == null
			return
		end
		
		WindowDialog.input(getWindow(), "@UI.RenameScene",\
			"@UI.Name.Label", null, pScene.getName(),\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null and not result.equals(pScene.getName())
					if result.empty()
						WindowDialog.message(getWindow(), "@UI.RenameScene",\
							"@UI.Message.EmptySceneName", null, null, null)
						
					elif pProject.getScenes().hasNamed(result)
						WindowDialog.message(getWindow(), "@UI.RenameScene",\
							"@UI.Message.DuplicateScene", null, null, null)
						
					else
						pScene.setName(result)
					end
				end
			end))
	end
	
	
	
	/** Selected loadable world or null. */
	func LoadableWorld getLoadableWorld()
		if pModelLoadableWorlds.getSelected() == -1
			return null
		end
		return pModelLoadableWorlds.getAt(pModelLoadableWorlds.getSelected()) cast LoadableWorld
	end
	
	/** Update loadable worlds model. */
	func void updateModelLoadableWorlds()
		var LoadableWorld world = getLoadableWorld()
		
		pModelLoadableWorlds.setContent(GameApp.getGameApp().getLoadableWorlds().toArray().sorted())
		
		var int index = pModelLoadableWorlds.indexOf(world)
		if index != -1
			pModelLoadableWorlds.setSelected(index)
			pModelLoadableWorlds.notifyContentChanged(index, index)
		end
	end
	
	/** Browse loadable worlds in overlay directory. */
	func void browseLoadableWorlds()
		FileSystem.browseConfig(GameApp.getGameApp().getProject().getWorldsDirectory().getPath())
	end
	
	/** Activate world. */
	func void activateWorld()
		var GameApp app = GameApp.getGameApp()
		var LoadableWorld world = getLoadableWorld()
		if pScene == null or world == null or app.getGameWorldLoader() != null
			return
		end
		
		// remove special scene objects that should not be kept
		app.getWorldSpawnCharacter().setGameWorld(null)
		
		// store scene objects to restore later on
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		var Array keepObjects = Array.new()
		
		if gameWorld != null
			gameWorld.forEachElement(block BehaviorElement each
				if each.getID() != UniqueID.new()
					var ECBSceneObject.Instance sceneObject = ECBSceneObject.getInstanceIn(each)
					if sceneObject != null
						keepObjects.add(sceneObject)
					end
				end
			end)
			
			keepObjects.forEach(block ECBSceneObject.Instance each
				gameWorld.removeElement(each.getElement())
			end)
		end
		
		// dispose game world. this removes scene objects and clears held file usages
		app.getWindowGameWorld().setGameWorld(null)
		
		// drop saved state
		pScene.dropSaveState()
		
		// set new path and load world
		pScene.setPathWorld(world.getPath())
		pProject.save()
		
		pScene.loadWorld(pProject, keepObjects)
	end
	
	
	
	/** Selected saved animation or null. */
	func CharacterAnimation getSavedAnimation()
		if pModelSavedAnimations.getSelected() == -1
			return null
		end
		return pModelSavedAnimations.getAt(pModelSavedAnimations.getSelected()) cast CharacterAnimation
	end
	
	/** Rename saved animation. */
	func void renameSavedAnimation()
		var CharacterAnimation savedAnimation = getSavedAnimation()
		if pProject == null or savedAnimation == null
			return
		end
		
		WindowDialog.input(getWindow(), "@UI.RenameSavedAnimation",\
			"@UI.Name.Label", null,\
			savedAnimation.getName(), null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null and not result.equals(savedAnimation.getName())
					if result.empty()
						WindowDialog.message(getWindow(), "@UI.RenameSavedAnimation",\
							"@UI.Message.EmptyName", null, null, null)
						
					elif pProject.getSavedAnimations().hasNamed(result)
						WindowDialog.message(getWindow(), "@UI.RenameSavedAnimation",\
							"@UI.Message.DuplicateSavedAnimation", null, null, null)
						
					else
						savedAnimation.setName(result)
						GameApp.getGameApp().notifySavedAnimationsChanged()
					end
				end
			end))
	end
	
	/** Load saved animation. */
	func void loadSavedAnimation()
		var CharacterAnimation savedAnimation = getSavedAnimation()
		if pObject == null or pProject == null or savedAnimation == null
			return
		end
		
		var ECBCapturedAnimations.Instance capturedAnimations =\
			ECBCapturedAnimations.getInstanceIn(pObject.getElement())
		if capturedAnimations == null
			return
		end
		
		var CharacterAnimation animation = capturedAnimations.getAnimations().getActive()
		if animation == null
			return
		end
		
		select capturedAnimations.getCaptureTarget()
		case ECBCapturedAnimations.CaptureTarget.character
			var String targetProfile = capturedAnimations.getCharacterProfile()
			var String sourceProfile = savedAnimation.getCharacterProfile(pProject)
			if targetProfile.empty() or sourceProfile.empty() or not sourceProfile.equals(targetProfile)
				WindowDialog.message(getWindow(), "@UI.LoadSavedAnimation",\
					"@UI.Message.ObjectIncompatible", null, null, null)
				return
			end
			
		case ECBCapturedAnimations.CaptureTarget.object
			// find some rules here
		end
		
		animation.assign(savedAnimation)
		animation.loadAnimation(pProject)
		capturedAnimations.getAnimations().notifyAnimationChanged(animation)
	end
	
	/** Update saved animations model. */
	func void updateSavedAnimations()
		var CharacterAnimation savedAnimation = getSavedAnimation()
		
		if pProject != null
			pModelSavedAnimations.setContent(pProject.getSavedAnimations().toArray().sorted())
			
		else
			pModelSavedAnimations.removeAll()
		end
		
		pModelSavedAnimations.setSelected(pModelSavedAnimations.indexOf(savedAnimation))
	end
	
	/** Remove saved animation. */
	func void removeSavedAnimation()
		var CharacterAnimation savedAnimation = getSavedAnimation()
		if savedAnimation == null or pProject == null
			return
		end
		
		WindowDialog.question(getWindow(), "@UI.RemoveSavedAnimation",\
			TranslationManager.get().translate("UI.Message.AskRemoveSavedAnimation")\
				.toUTF8().format(Array.newWith(savedAnimation.getName())), null, Array.newWith(\
				WindowDialog.ButtonConfiguration.new("@UI.Remove", true),\
				WindowDialog.ButtonConfiguration.new("@UI.Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pProject.getSavedAnimations().remove(savedAnimation)
					GameApp.getGameApp().notifySavedAnimationsChanged()
				end
		end))
	end
	
	/** Export saved animation. */
	func void exportSavedAnimation()
		var CharacterAnimation savedAnimation = getSavedAnimation()
		if savedAnimation == null or pProject == null
			return
		end
		
		WindowDialog.input(getWindow(), "@UI.ExportSavedAnimation",\
			"@UI.Name.Label", null, "Animation",\
			null, null, WindowDialog.BlockResultListener.new(block String result
				if result != null and not result.empty()
					savedAnimation.export(pProject, result)
				end
			end))
	end
	
	/** Browse exported animations directory. */
	func void browseExportedAnimations()
		if pProject != null
			pProject.browseExportedAnimations()
		end
	end
	
	
	
	/** Selected object or null. */
	func ECBSceneObject.Instance getObject()
		return pObject
	end
	
	/** Set selected object or null. */
	func void setObject(ECBSceneObject.Instance object)
		if object == pObject
			return
		end
		
		pPanelBehaviors.runWhileBlockingLayout(block
			while pPanelBehaviors.getWidgetCount() > 0
				var WMPPanelSceneObject panel = pPanelBehaviors.getWidget(pPanelBehaviors.getWidgetCount() - 1) cast WMPPanelSceneObject
				pBGPManager.returnPanel(panel)
				pPanelBehaviors.removeWidget(panel)
			end
		end)
		
		pObject = object
		
		var bool showParenting = true
		
		if object != null
			var ECBBehaviorGuiPanels.Instance behaviorGuiPanels = \
				ECBBehaviorGuiPanels.getInstanceIn(object.getElement() cast BehaviorElement)
			if behaviorGuiPanels != null
				behaviorGuiPanels.forEachBehaviorGuiPanel(block ECBBehaviorGuiPanels.BehaviorGuiPanel each
					var WMPPanelSceneObject panel = pBGPManager.getPanel(each)
					panel.setBehavior(each.getInstance())
					pPanelBehaviors.addWidget(panel)
				end)
			end
			pPanelBehaviors.doLayoutIfBlocked()
		end
		
		pPanelParenting.setVisible(showParenting)
		
		updateObject()
	end
	
	/** Get selected scene object from tracker. */
	func ECBSceneObject.Instance getTrackerSelectedObject()
		var ECBSelectable.Tracker tracker = GameApp.getGameApp().getSelectionTracker()
		if tracker.getElement() != null
			return ECBSceneObject.getInstanceIn(tracker.getElement().getElement())
		end
		return null
	end
	
	/** Select object from tracker. */
	func void selectObjectFromTracker()
		if not pEnableObjectUpdates
			return
		end
		
// 			var RuntimeMeter rm = RuntimeMeter.new()
// 			rm.counterReset(0)
// 			rm.reset(0)
		var ECBSceneObject.Instance selection = getTrackerSelectedObject()
		preventUpdateObjects = true
		pModelObjects.setSelectedElement(selection)
		preventUpdateObjects = false
		setObject(selection)
// 			rm.counterAdd(0, 0)
// 			GameApp.getGameApp().getConsole().addMessage("WMPanelProject(" + pInVR + ").selectObjectFromTracker: " + pObject + " => " + rm.counterTime(0))
	end
	
	/** Update objects model. */
	func void updateModelObjects()
		if pModelObjects == null
			return // dispose protection
		end
		
		var ECBSceneObject.Instance selection = getTrackerSelectedObject()
		var ECBSceneObject.Instance selectionParent = pModelParent.getSelectedElement() cast ECBSceneObject.Instance
		
		var Array content = Array.new()
		
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if gameWorld != null
			gameWorld.forEachElement(block BehaviorElement each
				var ECBSceneObject.Instance sceneObject = ECBSceneObject.getInstanceIn(each)
				if sceneObject != null
					content.add(sceneObject)
				end
			end)
		end
		
		content.sort(block ECBSceneObject.Instance a, ECBSceneObject.Instance b
			return a.getDisplayName().compare(b.getDisplayName())
		end)
		
		content.insert(0, null)
		preventUpdateObjects = true
		pModelObjects.setContent(content)
		
		pModelParent.setContent(content.collect(block ECBSceneObject.Instance each
			return each == null or ECBehaviorAttachments.getInstanceIn(each.getElement()) != null
		end))
		
		pModelObjects.setSelectedElement(selection)
		
		pModelParent.setSelectedElement(selectionParent)
		preventUpdateObjects = false
		setObject(selection)
	end
	
	func void updateModelObjectName(ECBSceneObject.Instance object)
		if pModelObjects == null or pModelParent == null
			return // dispose protection
		end
		
		preventUpdateObjects = true
		var int index = pModelObjects.indexOf(object)
		if index != -1
			pModelObjects.notifyElementChanged(index)
		end
		
		index = pModelParent.indexOf(object)
		if index != -1
			pModelParent.notifyElementChanged(index)
		end
		preventUpdateObjects = false
	end
	
	/** Update object. */
	func void updateObject()
		runWhileBlockingLayout(block
			if pObject != null
				var BehaviorElement element = pObject.getElement()
				pModelObjectClass.setText(element.getClass().getClassname())
				pEditObjectPosition.setVector(element.getPosition().toVector())
				pEditObjectRotation.setVector(element.getOrientation().getEulerAngles())
				
				var UniqueID parentId = element.getStub().getAttachTo()
				var Element parent
				if parentId != null
					parent = element.getGameWorld().getElementWithID(parentId)
				end
				if parent != null
					pModelParent.setSelectedElement(ECBSceneObject.getInstanceIn(parent cast BehaviorElement))
					
				else
					pModelParent.setSelectedElement(null)
				end
				
				pPanelBehaviors.forEachWidget(block WMPPanelSceneObject each
					each.updateBehavior()
				end)
				
			else
				pModelObjectClass.setText("")
				pModelParent.setSelectedElement(null)
				pEditObjectPosition.setVector(Vector.new())
				pEditObjectRotation.setVector(Vector.new())
			end
		end)
		doLayoutIfBlocked()
	end
	
	/** Add object. */
	func void addObject()
		if pScene == null
			return
		end
		
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if gameWorld == null
			return
		end
		
		var ElementClassList eclist = GameApp.getGameApp().getECListSceneObjects()
		var DefaultListModel model = DefaultListModel.new()
		eclist.forEach(block ElementClass each
			model.add(each.getClassname())
		end)
		model.sort()
		
		WindowDialog.chooseList(getWindow(), "@UI.AddSceneObject",\
			"@UI.ObjectClass.Label", null, model,\
			DefaultListElementRenderer.new(String.new('X', 30)),\
			WindowDialog.ButtonConfiguration.new("@UI.AddObject"),\
			WindowDialog.ButtonConfiguration.new("@UI.Cancel"),\
			WindowDialog.BlockResultListener.new(block int result
				if result != -1
					var String cname = model.getAt(result) cast String
					var BehaviorElementClass eclass = eclist.getNamed(cname) cast BehaviorElementClass
					var ECBSceneObject sceneObject = ECBSceneObject.getBehaviorIn(eclass)
					var StubElement stub = StubElement.new(eclass)
					
					if eclass.getPersistable()
						stub.setID(GameApp.getGameApp().getIDGenerator().nextID())
					end
					
					var DMatrix matrix = DMatrix.newTranslation(DVector.new(sceneObject.addActorOffset.getVector()))
					var BaseVRActor actor = GameApp.getGameApp().getWorldSpawnCharacter().getActor()
					if actor != null
						matrix = matrix * actor.getElementMatrix().normalize()
					end
					
					stub.setPosition(matrix.getPosition())
					stub.setRotation(matrix.normalize().getEulerAngles().toVector())
					
					if not sceneObject.runSceneObjectStubInitializers(stub, getWindow())
						return null
					end
					
					var BehaviorElement element = stub.createElement() cast BehaviorElement
					gameWorld.addElement(element)
					ECBSelectable.getInstanceIn(element).selectElement()
				end
		end))
	end
	
	/** Duplicate object. */
	func void duplicateObject()
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if pObject == null or pScene == null or gameWorld == null
			return
		end
		
		var ECBCopyObject.Instance behavior = ECBCopyObject.getInstanceIn(pObject.getElement())
		if behavior == null
			WindowDialog.message(getWindow(), "@UI.DuplicateSceneObject",\
				TranslationManager.get().translate("UI.Message.NoDuplicateObject")\
					.toUTF8().format(Array.newWith(pObject.getElement().getClass().getClassname())), null, null, null)
			return
		end
		
		var Array objects = behavior.createCopyData().createCopy(gameWorld, getWindow(), DVector.new(0.2, 0, 0.2))
		if objects.getCount() > 0
			ECBSelectable.getInstanceIn(objects.getAt(0) cast BehaviorElement).selectElement()
		end
	end
	
	/** Copy object. */
	func void copyObject()
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if pObject == null or pScene == null or gameWorld == null
			return
		end
		
		var ECBCopyObject.Instance behavior = ECBCopyObject.getInstanceIn(pObject.getElement())
		if behavior == null
			WindowDialog.message(getWindow(), "@UI.CopySceneObject",\
				TranslationManager.get().translate("UI.Message.NoCopyObject")\
					.toUTF8().format(Array.newWith(pObject.getElement().getClass().getClassname())), null, null, null)
			
		else
			getClipboard().setClipWith(behavior.createCopyData())
		end
	end
	
	/** Cut object. */
	func void cutObject()
		if pObject == null
			return
		end
		
		var ECBCopyObject.Instance behavior = ECBCopyObject.getInstanceIn(pObject.getElement())
		if behavior == null or not pObject.getECBehavior().canRemove.getValue()
			WindowDialog.message(getWindow(), "@UI.CutSceneObject",\
				TranslationManager.get().translate("UI.Message.NoCutObject")\
					.toUTF8().format(Array.newWith(pObject.getElement().getClass().getClassname())), null, null, null)
			return
		end
		
		WindowDialog.question(getWindow(), "@UI.CutSceneObject",\
			TranslationManager.get().translate("UI.Message.AskCutObject")\
				.toUTF8().format(Array.newWith(pObject.getDisplayName())), null, Array.newWith(\
				WindowDialog.ButtonConfiguration.new("@UI.Cut", true),\
				WindowDialog.ButtonConfiguration.new("@UI.Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					getClipboard().setClipWith(behavior.createCopyData())
					pObject.getElement().safeDispose()
				end
		end))
	end
	
	/** Paste object. */
	func void pasteObject()
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if pScene == null or gameWorld == null or getClipboard().isEmpty()
			return
		end
		
		getClipboard().getClip().findCastable(block ECBCopyObject.ObjectCopyData data
			var Array objects = data.createCopy(gameWorld, getWindow(), DVector.new(0.2, 0, 0.2))
			if objects.getCount() > 0
				ECBSelectable.getInstanceIn(objects.getAt(0) cast BehaviorElement).selectElement()
			end
			return true
		end)
	end
	
	/** Remove object. */
	func void removeObject()
		if pObject == null
			return
		end
		
		if not pObject.getECBehavior().canRemove.getValue()
			WindowDialog.message(getWindow(), "@UI.RemoveSceneObject",\
				TranslationManager.get().translate("UI.Message.NoRemoveObject")\
				.toUTF8().format(Array.newWith(pObject.getElement().getClass().getClassname())), null, null, null)
			return
		end
		
		WindowDialog.question(getWindow(), "@UI.RemoveSceneObject",\
			TranslationManager.get().translate("UI.Message.AskRemoveObject")\
				.toUTF8().format(Array.newWith(pObject.getDisplayName())), null, Array.newWith(\
				WindowDialog.ButtonConfiguration.new("@UI.Remove", true),\
				WindowDialog.ButtonConfiguration.new("@UI.Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					pObject.getElement().safeDispose()
				end
		end))
	end
	
	/** Remove all objects. */
	func void removeAllObjects()
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if gameWorld == null
			return
		end
		
		var Array objects = Array.new()
		gameWorld.forEachElement(block BehaviorElement each
			var ECBSceneObject.Instance sceneObject = ECBSceneObject.getInstanceIn(each)
			if sceneObject != null and sceneObject.getECBehavior().canRemove.getValue()
				objects.add(sceneObject.getElement())
			end
		end)
		
		if objects.getCount() == 0
			return
		end
		
		WindowDialog.question(getWindow(), "@UI.RemoveAllSceneObjects",\
			"@UI.Message.AskRemoveAllObjects", null, Array.newWith(\
				WindowDialog.ButtonConfiguration.new("@UI.Remove", true),\
				WindowDialog.ButtonConfiguration.new("@UI.Cancel", false)),\
			WindowDialog.BlockResultListener.new(block bool result
				if result
					objects.forEach(block BehaviorElement each
						each.safeDispose()
					end)
				end
		end))
	end
	
	/** Rename object. */
	func void renameObject()
		if pObject == null
			return
		end
		
		WindowDialog.input(getWindow(), "@UI.RenameSceneObject",\
		"@UI.Name.Label", null, pObject.getName(),\
		null, null, WindowDialog.BlockResultListener.new(block String result
			if result != null
				pObject.getElement().getStub().setPropertyValueFor(pObject.getECBehavior().name.getName(), result)
				pObject.setName(result)
			end
		end))
	end
	
	/** Change object class. */
	func void changeObjectClass()
		if pScene == null or pObject == null
			return
		end
		
		var GameWorld gameWorld = GameApp.getGameApp().getWindowGameWorld().getGameWorld()
		if gameWorld == null
			return
		end
		
		var ElementClassList eclist = GameApp.getGameApp().getECListSceneObjects()
		var DefaultListModel model = DefaultListModel.new()
		eclist.forEach(block ElementClass each
			model.add(each.getClassname())
		end)
		model.sort()
		
		var String curCName = pObject.getElement().getClass().getClassname()
		model.setSelectedElement(curCName)
		
		WindowDialog.chooseList(getWindow(), "@UI.ChangeObjectClass",\
		"@UI.ObjectClass.Label", null, model,\
			DefaultListElementRenderer.new(String.new('X', 30)),\
			WindowDialog.ButtonConfiguration.new("@UI.ChangeClass"),\
			WindowDialog.ButtonConfiguration.new("@UI.Cancel"),\
			WindowDialog.BlockResultListener.new(block int result
				if result == -1
					return null
				end
				
				var String newCName = model.getAt(result) cast String
				if newCName.equals(curCName)
					return null
				end
				
				var BehaviorElement element = pObject.getElement()
				var String soname = ECBSceneObject.getInstanceIn(element).getName()
				
				var BehaviorElementClass eclass = eclist.getNamed(newCName) cast BehaviorElementClass
				var ECBSceneObject sceneObject = ECBSceneObject.getBehaviorIn(eclass)
				var StubElement stub = StubElement.new(eclass)
				
				if not sceneObject.runSceneObjectStubInitializers(stub, getWindow())
					return null
				end
				
				stub.setPosition(element.getPosition())
				stub.setRotation(element.getOrientation().getEulerAngles())
				
				if eclass.getPersistable()
					stub.setID(element.getStub().getID())
					
				else
					stub.setID(UniqueID.new())
				end
				
				var Array attachments
				
				var ECBehaviorAttachments.Instance behaviorAttachments = ECBehaviorAttachments.getInstanceIn(element)
				if behaviorAttachments != null
					attachments = Array.new()
					behaviorAttachments.forEachAttachment(block BehaviorElement each
						attachments.add(each)
					end)
					behaviorAttachments = null
					
					attachments.forEach(block BehaviorElement each
						gameWorld.removeElement(each)
					end)
				end
				
				element.safeDispose()
				
				element = stub.createElement() cast BehaviorElement
				sceneObject.instance(element).setName(soname)
				
				gameWorld.addElement(element)
				
				if attachments != null
					behaviorAttachments = ECBehaviorAttachments.getInstanceIn(element)
					if behaviorAttachments != null
						attachments.forEach(block BehaviorElement each
							gameWorld.addElement(each)
						end)
						behaviorAttachments = null
						
					else
						attachments.forEach(block BehaviorElement each
							each.getStub().setAttachTo(null)
						end)
					end
				end
				
				ECBSelectable.getInstanceIn(element).selectElement()
		end))
	end
	
	/** Browse objects in overlay directory. */
	func void browseObjects()
		FileSystem.browseOverlay(GameApp.getGameApp().getPathObjects())
	end
	
	/** Attach to closest suitable object. */
	protected class FindClosestParent extends ElementVisitor
		public var ECBSceneObject.Instance bestParent
		public var BehaviorElement object
		public var DVector position
		public var float bestDistance
		
		func new(BehaviorElement object)
			this.object = object
			position = object.getPosition()
		end
		
		func void visitBehaviorElement(BehaviorElement element)
			if element == object
				return
			end
			
			var ECBSceneObject.Instance sceneObject = ECBSceneObject.getInstanceIn(element)
			var ECBehaviorAttachments.Instance attachments = ECBehaviorAttachments.getInstanceIn(element)
			if sceneObject == null or attachments == null
				return
			end
			
			var float distance = (element.getPosition() - position).getLength()
			if bestParent == null or distance < bestDistance
				bestParent = sceneObject
				bestDistance = distance
			end
		end
	end
	
	func void objectAttachClosest()
		if pObject == null or not pObject.getECBehavior().canRemove.getValue()
			return
		end
		
		var BehaviorElement element = pObject.getElement()
		var GameWorld gameWorld = element.getGameWorld()
		var FindClosestParent visitor = FindClosestParent.new(pObject.getElement())
		gameWorld.visitElements(visitor)
		
		element.getStub().setAttachTo(visitor.bestParent != null if visitor.bestParent.getElement().getID() else null)
		elementStubChanged()
	end
	
	/** Detach object. */
	func void objectDetach()
		if pObject == null
			return
		end
		
		var BehaviorElement element = pObject.getElement()
		if element.getStub().getAttachTo() == null
			return
		end
		
		var GameWorld gameWorld = element.getGameWorld()
		gameWorld.removeElement(element)
		element.getStub().setAttachTo(null)
		gameWorld.addElement(element)
	end
	
	/** Move object. */
	func void moveObject(DVector direction)
		if pObject != null
			ECBShowInteractHandles.directMoveObject(pObject.getElement(), direction)
		end
	end
	
	/** Move object. */
	func void moveObjectTo(DVector position)
		if pObject != null
			ECBShowInteractHandles.directMoveObjectTo(pObject.getElement(), position)
		end
	end
	
	/** Rotate object. */
	func void rotateObject(Vector axis)
		if pObject != null
			ECBShowInteractHandles.directRotateObject(pObject.getElement(), axis)
		end
	end
	
	/** Rotate object. */
	func void rotateObjectTo(Quaternion orientation)
		if pObject != null
			ECBShowInteractHandles.directRotateObjectTo(pObject.getElement(), orientation)
		end
	end
	
	
	
	/** Behavior panel matching prefix UI options value or null. */
	func WMPPanelSceneObject getBehaviorPanelWith(String prefix)
		return pPanelBehaviors.findWidget(block WMPPanelSceneObject each
			return each.getPrefixUIOptions().equals(prefix)
		end) cast WMPPanelSceneObject
	end
	
	
	
	/** Element stub changed. */
	func void elementStubChanged()
		if pObject == null or not pObject.getECBehavior().canRemove.getValue()
			return
		end
		
		var BehaviorElement element = pObject.getElement()
		var GameWorld gameWorld = element.getGameWorld()
		
		var Widget focusWidget = getDesktop().getDesktop().getFocusWidget()
		var Point focusPosition
		var String puio
		
		if focusWidget != null
			var Container widget = focusWidget.getParent()
			while widget != null
				if widget castable WMPPanelSceneObject
					focusPosition = focusWidget.getRelativePosition(widget) + focusWidget.getSize() / 2
					puio = (widget cast WMPPanelSceneObject).getPrefixUIOptions()
					break
				end
				widget = widget.getParent()
			end
		end
		
		// we delay this by one frame update to avoid complicated problems due to disposing
		TimerBlock.new(0, false, block
			var StubElement stub = element.getStub()
			if stub == null
				return null
			end
			
			stub.setPosition(element.getPosition())
			stub.setRotation(element.getOrientation().getEulerAngles())
			
			var Array attachments
			
			var ECBehaviorAttachments.Instance behaviorAttachments = ECBehaviorAttachments.getInstanceIn(element)
			if behaviorAttachments != null
				attachments = Array.new()
				behaviorAttachments.forEachAttachment(block BehaviorElement each
					attachments.add(each)
				end)
				behaviorAttachments = null
				
				attachments.forEach(block BehaviorElement each
					gameWorld.removeElement(each)
				end)
			end
			
			var MemoryFile objectState = MemoryFile.new("objectState")
			var PersistencyEnvironment env = PersistencyEnvironment.new(GameApp.getGameApp().getLoaders().getPersistency())
			element.writeToFile(env, objectState.getWriter(false))
			
			element.safeDispose()
			
			element = stub.createElement() cast BehaviorElement
			
			element.readFromFile(env, objectState.getReader())
			
			gameWorld.addElement(element)
			
			if attachments != null
				behaviorAttachments = ECBehaviorAttachments.getInstanceIn(element)
				if behaviorAttachments != null
					attachments.forEach(block BehaviorElement each
						gameWorld.addElement(each)
					end)
					behaviorAttachments = null
					
				else
					attachments.forEach(block BehaviorElement each
						each.safeDispose()
					end)
				end
			end
			
			ECBSelectable.getInstanceIn(element).selectElement()
			
			if focusPosition != null
				var WMPPanelSceneObject pso = getBehaviorPanelWith(puio)
				if pso != null
					focusWidget = pso.getWidgetAt(focusPosition)
					if focusWidget != null
						focusWidget.grabFocus()
					end
				end
			end
		end)
	end
	
	
	/** Create panel content. */
	protected func void createContent()
		var DefaultListModel comboBoxListModel
		var ComboBox comboBox
		var Button button
		var Label label
		
		setLayout(FlowLayout.new(LayoutAxis.y, 2))
		
		addWidget(CollapsibleGroup.new("@UI.Project", "uipanelproject",\
		CollapsibleGroup.persistModel(pPrefixUIOptions + "/Project"),\
		FormLayout.new(true, 5, 2), block Panel p
			p.addWidget(Label.new("@UI.Project.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				label = Label.new(pModelProjectFilename, "Label.FormText")
				label.setToolTip("@UI.ToolTip.Project")
				p2.addWidget(label)
				
				button = Button.new("...", ProjectPopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
			
			p.addWidget(Label.new("@UI.Scene.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelScenes, SceneListElementRenderer.new()))
				
				button = Button.new("...", ScenePopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
			
			p.addWidget(Label.new("@UI.World.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelLoadableWorlds, LoadableWorldRenderer.new()))
				
				button = Button.new("...", LoadableWorldPopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
			
			p.addWidget(Label.new("@UI.SavedAnims.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelSavedAnimations, SavedAnimationRenderer.new(this)))
				
				button = Button.new("...", SavedAnimationPopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
		end))
		
		addWidget(Panel.new(FormLayout.new(true, 5, 2), block Panel p
			p.addWidget(Label.new("@UI.Object.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 1, FlowLayout.Stretch.first), block Panel p2
				p2.addWidget(ComboBox.new(pModelObjects, ObjectElementRenderer.new()))
				
				button = Button.new("...", ObjectPopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
		end))
		
		pPanelParenting = CollapsibleGroup.new("@UI.Parenting", "uipanelproject#parenting", \
		CollapsibleGroup.persistModel(pPrefixUIOptions + "/ObjectParenting"),\
		FormLayout.new(true, 5, 2), block Panel p
			p.addWidget(Label.new("@UI.Parent.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 0, FlowLayout.Stretch.first), block Panel p2
				comboBox = ComboBox.new(pModelParent, ObjectElementRenderer.new())
				comboBox.setToolTip("@UI.ToolTip.ObjectParent")
				p2.addWidget(comboBox)
				
				button = Button.new("...", ParentPopupAction.new(this))
				button.setDesignerSelector("Button.Popup")
				p2.addWidget(button)
			end))
		end)
		addWidget(pPanelParenting)
		
		addWidget(CollapsibleGroup.new("@UI.Geometry", "uipanelproject#geometry", \
		CollapsibleGroup.persistModel(pPrefixUIOptions + "/ObjectGeometry"),\
		FormLayout.new(true, 5, 2), block Panel p
			p.addWidget(Label.new("@UI.Class.Label", "Label.FormCaption.SingleLine"))
			p.addWidget(Panel.new(FlowLayout.new(LayoutAxis.x, 0, FlowLayout.Stretch.first), block Panel p2
				label = Label.new(pModelObjectClass, "Label.FormText")
				label.setToolTip("@UI.ToolTip.ObjectClass")
				p2.addWidget(label)
				p2.addWidget(ObjectHelpButton.new(this))
			end))
			
			p.addWidget(Label.new("@UI.Position.Label", "Label.FormCaption.SingleLine"))
			pEditObjectPosition = EditVector.new()
			pEditObjectPosition.setToolTip("@UI.ToolTip.ObjectPosition")
			p.addWidget(pEditObjectPosition)
			pEditObjectPosition.addActionListener(ObjectPositionChanged.new(this, pEditObjectPosition))
			
			p.addWidget(Label.new("@UI.Rotation.Label", "Label.FormCaption.SingleLine"))
			pEditObjectRotation = EditVector.new()
			pEditObjectRotation.setToolTip("@UI.ToolTip.ObjectRotation")
			p.addWidget(pEditObjectRotation)
			pEditObjectRotation.addActionListener(ObjectRotationChanged.new(this, pEditObjectRotation))
			
			// manual movement
			label = Label.new("@UI.ManualMove.Label", "Label.FormCaption.SingleLine")
			label.setToolTip("@UI.ToolTip.ManualMove.Object")
			p.addWidget(label)
			
			p.addWidget(Panel.new(CentrizedBoxLayout.new(LayoutAxis.x, 2), block Panel p2
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					moveObject(DVector.new(-1, 0, 0))
				end))
				button.setDesignerSelector("Button.MovementLeft")
				button.setToolTip("@UI.ToolTip.ManualMoveLeft.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					moveObject(DVector.new(0, 1, 0))
				end))
				button.setDesignerSelector("Button.MovementUp")
				button.setToolTip("@UI.ToolTip.ManualMoveUp.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					moveObject(DVector.new(0, 0, 1))
				end))
				button.setDesignerSelector("Button.MovementForward")
				button.setToolTip("@UI.ToolTip.ManualMoveForward.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					moveObject(DVector.new(0, 0, -1))
				end))
				button.setDesignerSelector("Button.MovementBackwards")
				button.setToolTip("@UI.ToolTip.ManualMoveBackward.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					moveObject(DVector.new(0, -1, 0))
				end))
				button.setDesignerSelector("Button.MovementDown")
				button.setToolTip("@UI.ToolTip.ManualMoveDown.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					moveObject(DVector.new(1, 0, 0))
				end))
				button.setDesignerSelector("Button.MovementRight")
				button.setToolTip("@UI.ToolTip.ManualMoveRight.Object")
				p2.addWidget(button)
				
				comboBoxListModel = DefaultListModel.new()
				comboBoxListModel.setAutoSelect(false)
				comboBoxListModel.add("10")
				comboBoxListModel.add("1")
				comboBoxListModel.add("0.1")
				comboBoxListModel.add("0.01")
				comboBoxListModel.add("0.001")
				
				comboBox = ComboBox.new(comboBoxListModel, DefaultComboBoxEditor.new(\
					SessionSettings.get().getModelManualMovementStep(), 4))
				comboBox.setToolTip("@UI.ToolTip.ManualMoveStepWidth.Object")
				p2.addWidget(comboBox)
			end))
			
			// manual rotation
			label = Label.new("@UI.ManualRotate.Label", "Label.FormCaption.SingleLine")
			label.setToolTip("@UI.ToolTip.ManualRotate.Object")
			p.addWidget(label)
			
			p.addWidget(Panel.new(CentrizedBoxLayout.new(LayoutAxis.x, 2), block Panel p2
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					rotateObject(Vector.new(1, 0, 0))
				end))
				button.setDesignerSelector("Button.RotationXPositive")
				button.setToolTip("@UI.ToolTip.ManualRotateXPos.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					rotateObject(Vector.new(-1, 0, 0))
				end))
				button.setDesignerSelector("Button.RotationXNegative")
				button.setToolTip("@UI.ToolTip.ManualRotateXNeg.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					rotateObject(Vector.new(0, 1, 0))
				end))
				button.setDesignerSelector("Button.RotationYPositive")
				button.setToolTip("@UI.ToolTip.ManualRotateYPos.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					rotateObject(Vector.new(0, -1, 0))
				end))
				button.setDesignerSelector("Button.RotationYNegative")
				button.setToolTip("@UI.ToolTip.ManualRotateYNeg.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					rotateObject(Vector.new(0, 0, 1))
				end))
				button.setDesignerSelector("Button.RotationZPositive")
				button.setToolTip("@UI.ToolTip.ManualRotateZPos.Object")
				p2.addWidget(button)
				
				button = Button.new(" ", BlockActionListener.new(block ActionEvent event
					rotateObject(Vector.new(0, 0, -1))
				end))
				button.setDesignerSelector("Button.RotationZNegative")
				button.setToolTip("@UI.ToolTip.ManualRotateZNeg.Object")
				p2.addWidget(button)
				
				comboBoxListModel = DefaultListModel.new()
				comboBoxListModel.setAutoSelect(false)
				comboBoxListModel.add("90")
				comboBoxListModel.add("45")
				comboBoxListModel.add("30")
				comboBoxListModel.add("15")
				comboBoxListModel.add("10")
				comboBoxListModel.add("5")
				comboBoxListModel.add("1")
				comboBoxListModel.add("0.1")
				
				comboBox = ComboBox.new(comboBoxListModel, DefaultComboBoxEditor.new(\
					SessionSettings.get().getModelManualRotationStep(), 4))
				comboBox.setToolTip("@UI.ToolTip.ManualRotateStepWidth.Object")
				p2.addWidget(comboBox)
			end))
		end))
		
		pPanelBehaviors = Panel.new(FlowLayout.new(LayoutAxis.y, 2))
		addWidget(pPanelBehaviors)
	end
end
