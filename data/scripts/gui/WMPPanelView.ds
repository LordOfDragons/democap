/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2022, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Gui

pin Democap.Behaviors

pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Events
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * View settings panel.
 */
class WMPPanelView extends CollapsibleGroup
	class LanguagePackRenderer extends DefaultListElementRenderer
		func new(): end
		
		func void updateRenderer(ListBox listBox, Widget renderer, Object element, bool selected, bool focused)
			var String text
			if element != null
				text = (element cast LanguagePack).getName().toUTF8()
				
			else
				text = String.new('X', 20)
			end
			super.updateRenderer(listBox, renderer, text, selected, focused)
		end
	end
	
	
	protected var String pPrefixUIOptions
	
	/** Create panel. */
	func new(WMPanelSettings panelSettings) super("@UI.ViewSettings", "uipanelsettings#view_settings",\
	CollapsibleGroup.persistModel(prefixUIOptions(panelSettings), true))
		pPrefixUIOptions = prefixUIOptions(panelSettings)
		
		getPanelContent().runWhileBlockingLayout(block
			createContent(getPanelContent())
		end)
	end
	
	/** Dispose of widget. */
	func void dispose()
		super.dispose()
	end
	
	/** UI options prefix. */
	static func String prefixUIOptions(WMPanelSettings panelSettings)
		return panelSettings.getPrefixUIOptions() + "/WMPPanelView"
	end
	
	
	
	/** Create panel content. */
	protected func void createContent(Panel content)
		var SessionSettings sessionSettings = SessionSettings.get()
		var CheckBox checkBox
		var ComboBox comboBox
		var Slider slider
		var Label label
		
		content.setLayout(FormLayout.new(true, 5, 2))
		
		// interaction bubble size
		content.addWidget(Label.new("@UI.BubbleSize.Label", "Label.FormCaption.SingleLine"))
		
		slider = Slider.new(Orientation.horizontal, sessionSettings.getModelInteractionBubbleSize())
		slider.setShowTicks(true)
		slider.setToolTip("@UI.ToolTip.BubbleSize")
		content.addWidget(slider)
		
		content.addWidget(Label.new("", "Label.FormCaption.SingleLine"))
		content.addWidget(Panel.new(GridLayout.new(2, 0), block Panel p
			// camera enable GI
			checkBox = CheckBox.new(sessionSettings.getModelCameraEnableGI(), "@UI.EnableGI")
			checkBox.setToolTip("@UI.ToolTip.EnableGI")
			p.addWidget(checkBox)
			
			// full bright
			checkBox = CheckBox.new(sessionSettings.getModelFullBright(), "@UI.Fullbright")
			checkBox.setToolTip("@UI.ToolTip.Fullbright")
			p.addWidget(checkBox)
		end))
		
		// language
		label = Label.new("@UI.Language.Label")
		label.setToolTip("@UI.ToolTip.Language")
		content.addWidget(label)
		
		comboBox = ComboBox.new(SessionSettings.get().getModelLanguage(), LanguagePackRenderer.new())
		comboBox.setToolTip("@UI.ToolTip.Language")
		content.addWidget(comboBox)
	end
end
