/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Actions

pin Democap.Behaviors
pin Democap.Elements

pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Default BaseVRActor action. Used when the character is spawned in the world but no
 * particular action is running. This is the default action characters spawn with.
 */
class AADefault extends AABase
	/** Factory for loading actor actions. */
	public class Factory implements PersistencyFactory, ECBehaviorActorAIAction.ActionFactory
		public static fixed var String name = "AADefault"
		
		public func new()
		end
		
		public func Persistable readObjectFromFile(PersistencyEnvironment env, FileReader reader)
			return AADefault.new()
		end
		
		public static func void registerFactory(Persistency persistency)
			persistency.addFactory(name, Factory.new())
		end
		
		public func BaseActorAction createAction(ECBehaviorActorAIAction.Instance instance)
			return AADefault.new()
		end
	end
	
	
	
	protected var ECBAControlDesktop.Instance controlDesktop
	protected var ECBAInteractHandle.Instance interactHandle
	protected var ECBAGrab.Instance grabRightHand
	protected var ECBAGrab.Instance grabLeftHand
	protected var ECBASelectable.Instance actorSelectable
	protected var ECBVRHudInfo.Instance vrHudInfo
	protected var ECBAResetCalibrationWatcher.Instance resetCalibrationWatcher
	
	
	
	/** Create action. */
	public func new()
	end
	
	
	
	/** Init behaviors. */
	protected func void initBehaviors()
		super.initBehaviors()
		
		var MoCapActor actor = getActor() cast MoCapActor
		controlDesktop = actor.controlDesktop
		interactHandle = actor.interactHandle
		grabRightHand = actor.grabRightHand
		grabLeftHand = actor.grabLeftHand
		actorSelectable = actor.actorSelectable
		vrHudInfo = actor.vrHudInfo
		resetCalibrationWatcher = actor.resetCalibrationWatcher
	end
	
	
	
	/** Action has been activated. */
	public func void activate(BehaviorElement actor)
		super.activate(actor)
		
		resetCalibrationWatcher.reset()
		grabRightHand.getGrabber().setEnabled(true)
		grabLeftHand.getGrabber().setEnabled(true)
	end
	
	/** Action has been deactivated. */
	public func void deactivate()
		resetCalibrationWatcher.reset()
		super.deactivate()
	end
	
	
	/** Trigger pulled. */
	public func void triggerPull(bool rightHand)
		resetCalibrationWatcher.triggerPulled(rightHand)
		
		if rightHand
			if grabRightHand.triggerPull()
				
			elif vrRightHandPointAt.hasVRHandPointAtElement()
				pointAtPull(vrRightHandPointAt)
				
			else
				GameApp.getGameApp().getSelectionTracker().clear()
			end
			
		else
			if grabLeftHand.triggerPull()
				
			elif vrLeftHandPointAt.hasVRHandPointAtElement()
				pointAtPull(vrLeftHandPointAt)
				
			else
				GameApp.getGameApp().getSelectionTracker().clear()
			end
		end
	end
	
	/** Trigger released. */
	public func void triggerRelease(bool rightHand)
		resetCalibrationWatcher.triggerRelease(rightHand)
		
		if rightHand
			grabRightHand.triggerRelease()
			pointAtRelease(vrRightHandPointAt)
			
		else
			grabLeftHand.triggerRelease()
			pointAtRelease(vrLeftHandPointAt)
		end
	end
	
	
	
	/** Menu button pressed. */
	public func void menuPress(bool rightHand)
		if resetCalibrationWatcher.menuPress(rightHand)
			return
		end
		
		if rightHand
			if grabRightHand.menuPress()
				return
			end
			
		else
			if grabLeftHand.menuPress()
				return
			end
		end
		
		var BehaviorElement vrmenu = GameApp.getGameApp().getWorldSpawnCharacter().getVRMenu()
		if vrmenu == null
			return
		end
		
		var ECBToggleVisibility.Instance toggleVisibility = ECBToggleVisibility.getInstanceIn(vrmenu)
		if toggleVisibility == null
			return
		end
		
		if toggleVisibility.getVisible()
			toggleVisibility.setVisible(false)
			
		else
			aiAction.setAction(AAShowToggleVisible.new(toggleVisibility,\
				rightHand, AAShowToggleVisible.Button.menu))
		end
	end
	
	/** Menu button released. */
	public func void menuRelease(bool rightHand)
		resetCalibrationWatcher.menuRelease(rightHand)
		
		if rightHand
			grabRightHand.menuRelease()
			
		else
			grabLeftHand.menuRelease()
		end
	end
	
	
	
	/** Secondary button pressed. */
	public func void secondaryPress(bool rightHand)
	end
	
	/** Secondary button released. */
	public func void secondaryRelease(bool rightHand)
	end
	
	
	
	/** Touch track pad. */
	public func void trackPadTouch(bool rightHand)
		if rightHand
			if grabRightHand.trackPadTouch()
				
			else
				vrRightHandPointAt.setEnabled(true)
			end
			
		else
			if grabLeftHand.trackPadTouch()
				
			else
				vrLeftHandPointAt.setEnabled(true)
			end
		end
	end
	
	/** Untouch track pad. */
	public func void trackPadUntouch(bool rightHand)
		if rightHand
			if controlDesktop.getPointAt() == vrRightHandPointAt
				controlDesktop.cancel()
			end
			if interactHandle.getPointAt() == vrRightHandPointAt
				interactHandle.cancel()
			end
			vrRightHandPointAt.setEnabled(false)
			grabRightHand.trackPadUntouch()
			
		else
			if controlDesktop.getPointAt() == vrLeftHandPointAt
				controlDesktop.cancel()
			end
			if interactHandle.getPointAt() == vrLeftHandPointAt
				interactHandle.cancel()
			end
			vrLeftHandPointAt.setEnabled(false)
			grabLeftHand.trackPadUntouch()
		end
	end
	
	
	
	/** Grip grab. */
	public func void gripGrab(bool rightHand)
		if rightHand
			if vrRightHandPointAt.getEnabled()
			elif grabRightHand.grab()
			end
			
		else
			if vrLeftHandPointAt.getEnabled()
			elif grabLeftHand.grab()
			end
		end
	end
	
	/** Grip ungrab. */
	public func void gripUngrab(bool rightHand)
		if rightHand
			grabRightHand.ungrab()
			
		else
			grabLeftHand.ungrab()
		end
	end
	
	
	
	/** Pointing at element. */
	public func void pointAt(ECBehaviorVRHandPointAt.Instance behavior)
		// pulled trigger locks action to be used
		if controlDesktop.getTriggerPulled()
			controlDesktop.pointAt(behavior)
			
		elif interactHandle.getTriggerPulled()
			interactHandle.pointAt(behavior)
			
		// try all actions
		elif controlDesktop.pointAt(behavior)
			
		elif interactHandle.pointAt(behavior)
			
		end
	end
	
	/** Pull trigger on pointed at element. */
	public func void pointAtPull(ECBehaviorVRHandPointAt.Instance behavior)
		if controlDesktop.pointAtPull(behavior)
			
		elif interactHandle.pointAtPull(behavior)
			
		elif actorSelectable.pointAtPull(behavior)
			
		end
	end
	
	/** Release trigger on pointed at element. */
	public func void pointAtRelease(ECBehaviorVRHandPointAt.Instance behavior)
		if controlDesktop.pointAtRelease(behavior)
			
		elif interactHandle.pointAtRelease(behavior)
			
		end
	end
	
	
	
	/** Frame update thinking. */
	public func void playerThink(float elapsed)
		super.playerThink(elapsed)
		
		grabRightHand.updateGrab()
		grabLeftHand.updateGrab()
		
		if vrRightHandPointAt.getEnabled()
			pointAt(vrRightHandPointAt)
		end
		if vrLeftHandPointAt.getEnabled()
			pointAt(vrLeftHandPointAt)
		end
		
		var SessionSettings settings = SessionSettings.get()
		projectToGround.setEnabled(settings.getProjectToGround())
	end
	
	
	
	/** Start calibration. Return true if calibration started. */
	public func bool startCalibrate()
		if ECBCharacterConfiguration.getInstanceIn(actor) == null
			return false
		end
		
		aiAction.setAction(AACalibrate.new())
		return true
	end
	
	/** Start recording. Return true if recording started. */
	public func bool startRecording()
		if ECBCharacterConfiguration.getInstanceIn(actor) == null
			return false
		end
		
		aiAction.setAction(AARecordAnimation.new())
		return true
	end
	
	
	
	/** Name of PersistencyFactory required to load object from file. */
	public func String persistencyFactoryName()
		return Factory.name
	end
end
