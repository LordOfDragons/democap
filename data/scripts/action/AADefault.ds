/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Actions

pin Democap.Behaviors
pin Democap.Elements

pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * Default BaseVRActor action. Used when the character is spawned in the world but no
 * particular action is running. This is the default action characters spawn with.
 */
class AADefault extends AABase
	/** Factory for loading actor actions. */
	class Factory implements PersistencyFactory, ECBehaviorActorAIAction.ActionFactory
		public static fixed var String name = "AADefault"
		
		func new()
		end
		
		func Persistable readObjectFromFile(PersistencyEnvironment env, FileReader reader)
			return AADefault.new()
		end
		
		public static func void registerFactory(Persistency persistency)
			persistency.addFactory(name, Factory.new())
		end
		
		func BaseActorAction createAction(ECBehaviorActorAIAction.Instance instance)
			return AADefault.new()
		end
	end
	
	
	
	protected var ECBAControlDesktop.Instance controlDesktop
	protected var ECBAInteractHandle.Instance interactHandle
	protected var ECBAGrab.Instance grabRightHand, grabLeftHand
	protected var ECBASelectable.Instance actorSelectable
	protected var ECBVRHudInfo.Instance vrHudInfo
	protected var ECBAResetCalibrationWatcher.Instance resetCalibrationWatcher
	
	
	
	/** Create action. */
	func new()
	end
	
	
	
	/** Init behaviors. */
	protected func void initBehaviors()
		super.initBehaviors()
		
		var MoCapActor mcactor = actor cast MoCapActor
		controlDesktop = mcactor.controlDesktop
		interactHandle = mcactor.interactHandle
		grabRightHand = mcactor.grabRightHand
		grabLeftHand = mcactor.grabLeftHand
		actorSelectable = mcactor.actorSelectable
		vrHudInfo = mcactor.vrHudInfo
		resetCalibrationWatcher = mcactor.resetCalibrationWatcher
	end
	
	
	
	/** Action has been activated. */
	func void activate(BehaviorElement actor)
		super.activate(actor)
		
		resetCalibrationWatcher.reset()
		grabRightHand.getGrabber().setEnabled(true)
		grabLeftHand.getGrabber().setEnabled(true)
		
		controllerCapabilitiesChanged()
	end
	
	/** Action has been deactivated. */
	func void deactivate()
		resetCalibrationWatcher.reset()
		super.deactivate()
	end
	
	/** Controller capabilities changed. */
	func void controllerCapabilitiesChanged()
		var TrackControllerCaps caps = GameApp.getGameApp().getTrackControllerCaps()
		vrRightHandPointAt.setEnabled(caps.getHasControllerRight() and not caps.getHasTouchTrackPadRight())
		vrLeftHandPointAt.setEnabled(caps.getHasControllerLeft() and not caps.getHasTouchTrackPadLeft())
	end
	
	
	
	/** Trigger pulled. */
	func void triggerPull(bool rightHand)
		resetCalibrationWatcher.triggerPulled(rightHand)
		
		if isHandFacingUser(rightHand) and isHandInFrontOfUser(rightHand)
			return
		end
		
		if rightHand
			if grabRightHand.triggerPull()
				
			elif vrRightHandPointAt.hasVRHandPointAtElement()
				pointAtPull(vrRightHandPointAt)
				
			else
				GameApp.getGameApp().getSelectionTracker().clear()
			end
			
		else
			if grabLeftHand.triggerPull()
				
			elif vrLeftHandPointAt.hasVRHandPointAtElement()
				pointAtPull(vrLeftHandPointAt)
				
			else
				GameApp.getGameApp().getSelectionTracker().clear()
			end
		end
	end
	
	/** Trigger released. */
	func void triggerRelease(bool rightHand)
		resetCalibrationWatcher.triggerRelease(rightHand)
		
		if rightHand
			grabRightHand.triggerRelease()
			pointAtRelease(vrRightHandPointAt)
			
		else
			grabLeftHand.triggerRelease()
			pointAtRelease(vrLeftHandPointAt)
		end
	end
	
	
	
	/** Menu button pressed. */
	func void menuPress(bool rightHand)
		if resetCalibrationWatcher.menuPress(rightHand)
			return
		end
		
		if rightHand
			if grabRightHand.menuPress()
				return
			end
			
		else
			if grabLeftHand.menuPress()
				return
			end
		end
		
		var BehaviorElement vrmenu = GameApp.getGameApp().getWorldSpawnCharacter().getVRMenu()
		if vrmenu == null
			return
		end
		
		var ECBToggleVisibility.Instance toggleVisibility = ECBToggleVisibility.getInstanceIn(vrmenu)
		if toggleVisibility == null
			return
		end
		
		if toggleVisibility.getVisible()
			toggleVisibility.setVisible(false)
			
		else
			aiAction.setAction(AAShowToggleVisible.new(toggleVisibility,\
				rightHand, AAShowToggleVisible.Button.menu))
		end
	end
	
	/** Menu button released. */
	func void menuRelease(bool rightHand)
		resetCalibrationWatcher.menuRelease(rightHand)
		
		if rightHand
			grabRightHand.menuRelease()
			
		else
			grabLeftHand.menuRelease()
		end
	end
	
	
	
	/** Touch track pad. */
	func void trackPadTouch(bool rightHand)
		if rightHand
			if grabRightHand.trackPadTouch()
				
			else
				vrRightHandPointAt.setEnabled(true)
			end
			
		else
			if grabLeftHand.trackPadTouch()
				
			else
				vrLeftHandPointAt.setEnabled(true)
			end
		end
	end
	
	/** Untouch track pad. */
	func void trackPadUntouch(bool rightHand)
		if rightHand
			if controlDesktop.getPointAtRight() == vrRightHandPointAt
				controlDesktop.cancel(true)
			end
			if interactHandle.getPointAt() == vrRightHandPointAt
				interactHandle.cancel()
			end
			vrRightHandPointAt.setEnabled(false)
			grabRightHand.trackPadUntouch()
			
		else
			if controlDesktop.getPointAtLeft() == vrLeftHandPointAt
				controlDesktop.cancel(false)
			end
			if interactHandle.getPointAt() == vrLeftHandPointAt
				interactHandle.cancel()
			end
			vrLeftHandPointAt.setEnabled(false)
			grabLeftHand.trackPadUntouch()
		end
	end
	
	
	
	/** Grip grab. */
	func void gripGrab(bool rightHand)
		if rightHand
			if vrRightHandPointAt.getEnabled()
			elif grabRightHand.grab()
			end
			
		else
			if vrLeftHandPointAt.getEnabled()
			elif grabLeftHand.grab()
			end
		end
	end
	
	/** Grip ungrab. */
	func void gripUngrab(bool rightHand)
		if rightHand
			grabRightHand.ungrab()
			
		else
			grabLeftHand.ungrab()
		end
	end
	
	
	func void twoFingerPress(bool rightHand, int finger)
		if finger == 3 and isHandFacingUser(rightHand) and isHandInFrontOfUser(rightHand)
			resetCalibrationWatcher.twoFingerPress(rightHand)
			
		else
			super.twoFingerPress(rightHand, finger)
		end
	end
	
	func void twoFingerRelease(bool rightHand, int finger)
		if finger == 3
			resetCalibrationWatcher.twoFingerRelease(rightHand)
			
		else
			super.twoFingerRelease(rightHand, finger)
		end
	end
	
	
	
	/** Player update thinking. */
	func void playerThink(float elapsed)
		super.playerThink(elapsed)
		
		if vrRightHandPointAt.getEnabled()
			pointAt(vrRightHandPointAt)
		end
		if vrLeftHandPointAt.getEnabled()
			pointAt(vrLeftHandPointAt)
		end
		
		var SessionSettings settings = SessionSettings.get()
		projectToGround.setEnabled(settings.getProjectToGround())
		playerInputMove.setCanMove(not settings.getTeleportMovement())
		
		checkBeginTeleport()
	end
	
	/** Think. */
	func void think(float elapsed)
		super.think(elapsed)
		
		grabRightHand.updateGrab(elapsed)
		grabLeftHand.updateGrab(elapsed)
	end
	
	/** Post think. */
	func void postThink(float elapsed)
		super.postThink(elapsed)
		
		grabRightHand.updateGrabPostThink()
		grabLeftHand.updateGrabPostThink()
	end
	
	
	
	/** Start teleporting if required. */
	func void checkBeginTeleport()
		if not SessionSettings.get().getTeleportMovement()
			return
		end
		
		var float moveFB = playerInputMove.getAnalogMoveForwardBackwardSticky()
		var float moveLR = playerInputMove.getAnalogMoveLeftRightSticky()
		var float move = Vector2.new(moveLR, moveFB).getLength()
		if move > 0.5
			aiAction.setAction(AATeleport.new())
		end
	end
	
	/** Pointing at element. */
	func void pointAt(ECBehaviorVRHandPointAt.Instance behavior)
		var bool rightHand = vrRightHandPointAt == behavior
		
		// pulled trigger locks action to be used
		if rightHand and controlDesktop.getTriggerPulledRight()
			controlDesktop.pointAt(behavior, true)
			
		elif not rightHand and controlDesktop.getTriggerPulledLeft()
			controlDesktop.pointAt(behavior, false)
			
		elif interactHandle.getTriggerPulled()
			interactHandle.pointAt(behavior)
			
		// try all actions
		elif controlDesktop.pointAt(behavior, rightHand)
		elif interactHandle.pointAt(behavior)
		end
	end
	
	/** Pull trigger on pointed at element. */
	func void pointAtPull(ECBehaviorVRHandPointAt.Instance behavior)
		var bool rightHand = vrRightHandPointAt == behavior
		
		if controlDesktop.pointAtPull(behavior, rightHand)
		elif interactHandle.pointAtPull(behavior)
		elif actorSelectable.pointAtPull(behavior)
		end
	end
	
	/** Release trigger on pointed at element. */
	func void pointAtRelease(ECBehaviorVRHandPointAt.Instance behavior)
		var bool rightHand = vrRightHandPointAt == behavior
		
		if controlDesktop.pointAtRelease(behavior, rightHand)
		elif interactHandle.pointAtRelease(behavior)
		end
	end
	
	
	
	/** Start calibration. Return true if calibration started. */
	func bool startCalibrate()
		if ECBCharacterConfiguration.getInstanceIn(actor) == null
			return false
		end
		
		aiAction.setAction(AACalibrate.new())
		return true
	end
	
	/** Start recording. Return true if recording started. */
	func bool startRecording()
		if ECBCharacterConfiguration.getInstanceIn(actor) == null
			return false
		end
		
		aiAction.setAction(AARecordAnimation.new())
		return true
	end
	
	
	
	/** Name of PersistencyFactory required to load object from file. */
	func String persistencyFactoryName()
		return Factory.name
	end
end
