/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Actions

pin Democap.Actors
pin Democap.Behaviors
pin Democap.Characters

pin Dragengine.Gui.Layouts
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * BaseVRActor action running while animation is recorded.
 */
class AARecordAnimation extends AABase
	/** Factory for loading actor actions. */
	public class Factory implements PersistencyFactory, ECBehaviorActorAIAction.ActionFactory
		public static fixed var String name = "AARecordAnimation"
		
		public func new()
		end
		
		public func Persistable readObjectFromFile(PersistencyEnvironment env, FileReader reader)
			return AARecordAnimation.new()
		end
		
		public static func void registerFactory(Persistency persistency)
			persistency.addFactory(name, Factory.new())
		end
		
		public func BaseActorAction createAction(ECBehaviorActorAIAction.Instance instance)
			return AARecordAnimation.new()
		end
	end
	
	enum State
		prepare
		recording
		finished
		done
	end
	
	
	
	protected var ECBRecordAnimation.Instance recordAnimation
	protected var ECBVRHudInfo.Instance vrHudInfo
	protected var Panel panelInfo
	protected var RangeModel modelProgress
	protected var State state
	protected var float elapsedTime
	protected var float waitTime
	protected var float waitTimeFinished
	
	
	
	/** Create action. */
	public func new()
		state = State.prepare
		elapsedTime = 0
		waitTime = 3
		waitTimeFinished = 1
	end
	
	
	
	/** Action has been activated. */
	public func void activate(BehaviorElement actor)
		super.activate(actor)
		
		if vrHudInfo != null
			modelProgress = DefaultRangeModel.new(0, 100, 1, 0, 1)
			
			panelInfo = Panel.new(StackLayout.new(), block Panel p
				p.setDesignerSelector("Panel.HudInfo.Recording")
				
				p.addWidget(DisplayRotatingImage.new(Image.new("/content/gui/images/prepare_recording.png")))
				
				p.addWidget(Panel.new(CentrizedBoxLayout.new(LayoutAxis.y, 0.5, 0.735), block Panel p2
					var ProgressBar progressBar = ProgressBar.new(Orientation.horizontal, modelProgress)
					progressBar.setDesignerSelector("ProgressBar.HudInfo.Recording")
					progressBar.setShowPercentage(false)
					p2.addWidget(progressBar)
				end))
			end)
			
			vrHudInfo.addPanel(panelInfo)
		end
	end
	
	/** Deactivate action. */
	public func void deactivate()
		removeHudInfoPanel()
		modelProgress = null
	end
	
	/**
	 * Init behaviors
	 */
	protected func void initBehaviors()
		super.initBehaviors()
		
		recordAnimation = ECBRecordAnimation.getInstanceIn(actor)
		vrHudInfo = ECBVRHudInfo.getInstanceIn(actor)
	end
	
	
	/** Menu button pressed. */
	public func void menuPress(bool rightHand)
		stopRecording()
	end
	
	/** Stop recording. */
	public func void stopRecording()
		select state
		case State.prepare
			removeHudInfoPanel()
			state = State.done
			aiAction.setAction(AADefault.new())
			
		case State.recording
			elapsedTime = 0
			state = State.finished
			
			recordAnimation.stopRecording()
			
			removeHudInfoPanel()
			
			if vrHudInfo != null
				panelInfo = Panel.new(StackLayout.new(), block Panel p
					p.setDesignerSelector("Panel.HudInfo.Recording")
					p.addWidget(DisplayRotatingImage.new(Image.new("/content/gui/images/finished_recording.png")))
				end)
				
				vrHudInfo.addPanel(panelInfo)
			end
		end
	end
	
	
	
	/** Frame update. */
	public func void think(float elapsed)
		super.think(elapsed)
		
		select state
		case State.prepare
			elapsedTime += elapsed
			
			if modelProgress != null
				modelProgress.setValue(DEMath.linearStep(elapsedTime, 0, waitTime, 0, 100) cast int)
			end
			
			if elapsedTime >= waitTime
				removeHudInfoPanel()
				
				if vrHudInfo != null
					panelInfo = Panel.new(StackLayout.new(), block Panel p
						p.setDesignerSelector("Panel.HudInfo.Recording")
						
						p.addWidget(Panel.new(CentrizedBoxLayout.new(LayoutAxis.y, 0.8, 0.3), block Panel p2
							p2.addWidget(DisplayImage.new(Image.new("/content/gui/images/recording.png")))
						end))
					end)
					vrHudInfo.addPanel(panelInfo)
				end
				
				state = State.recording
				recordAnimation.startRecording()
			end
			
		case State.finished
			elapsedTime += elapsed
			
			if elapsedTime >= waitTimeFinished
				removeHudInfoPanel()
				state = State.done
				aiAction.setAction(AADefault.new())
			end
		end
	end
	
	
	
	/** Remove and dispose hud info panel if present. */
	protected func void removeHudInfoPanel()
		if vrHudInfo != null and panelInfo != null
			vrHudInfo.removePanel(panelInfo)
			panelInfo.safeDispose()
			panelInfo = null
		end
	end
	
	
	
	/** Name of PersistencyFactory required to load object from file. */
	public func String persistencyFactoryName()
		return Factory.name
	end
end
