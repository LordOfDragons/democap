/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Characters

pin Dragengine.Utils


/**
 * List of character profiles.
 * 
 * Profiles are stored under "/characters" directory in the user game configure directory
 * as files of type "*.demccp" .
 */
class CharacterProfiles
	var String pDirectory
	var Array pProfiles
	var Array pListeners
	
	
	
	/** Create character list. */
	func new(String directory)
		if directory == null
			throw ENullPointer.new("directory")
		end
		
		pListeners = Array.new()
		pDirectory = directory
		pProfiles = Array.new()
	end
	
	/** Dispose of list. */
	func void dispose()
		pListeners.removeAll()
		removeAll()
	end
	
	
	
	/** Directory to save character profiles to. */
	func String getDirectory()
		return pDirectory
	end
	
	
	
	/** Count of profiles. */
	func int getCount()
		return pProfiles.getCount()
	end
	
	/** Named profile is present. */
	func bool hasNamed(String name)
		return getNamed(name) != null
	end
	
	/** Profile at index. */
	func CharacterProfile getAt(int index)
		return pProfiles.getAt(index) cast CharacterProfile
	end
	
	/** Named profile or null if absent. */
	func CharacterProfile getNamed(String name)
		return pProfiles.find(block CharacterProfile each
			return each.getName().compareNoCase(name) == 0
		end) cast CharacterProfile
	end
	
	/** Add profile. Throws exception if named profile is present. */
	func void add(CharacterProfile profile)
		if hasNamed(profile.getName())
			throw EInvalidParam.new("named profile is present: " + profile.getName())
		end
		pProfiles.add(profile)
		
		pListeners.forEach(block CharacterProfilesListener each
			each.profileAdded(this, profile)
		end)
	end
	
	/** Remove profile. Throws exception if named profile is absent. */
	func void remove(CharacterProfile profile)
		pProfiles.remove(profile)
		
		pListeners.forEach(block CharacterProfilesListener each
			each.profileRemoved(this, profile)
		end)
		
		profile.dispose()
	end
	
	/** Remove all profiles. */
	func void removeAll()
		pProfiles.forEach(block CharacterProfile each
			each.dispose()
		end)
		pProfiles.removeAll()
		
		pListeners.forEach(block CharacterProfilesListener each
			each.allProfilesRemoved(this)
		end)
	end
	
	/**
	 * Visit profiles with block. Block receives as only argument CharacterProfile.
	 */
	func void forEach(Block ablock)
		pProfiles.forEach(ablock)
	end
	
	/**
	 * Collect profiles with block. Block receives as only argument CharacterProfile and
	 * has to return true to include or false to exclude profile.
	 * Returns Array of CharacterProfile.
	 */
	func Array collect(Block ablock)
		return pProfiles.collect(ablock)
	end
	
	/** List of profiles as Array of CharacterProfile. */
	func Array toArray()
		return Array.new(pProfiles)
	end
	
	
	
	/** Load characters from directory (*.dmcc). */
	func void loadAll()
		var GameApp app = GameApp.getGameApp()
		var LoadCharacterProfile loader = app.getLoadCharacterProfile()
		var Console console = app.getConsole()
		
		console.addMessage("Load character profiles...")
		
		FileSystem.searchFiles(pDirectory, true, block String each, FileType type
			if type == FileType.file and FileSystem.pathMatchesPattern(File.new(each).getName(), "*.demccp")
				try
					add(loader.load(each, false))
					
				catch Exception e
					console.addError("Failed loading character profile: " + each, e)
					return true
				end
			end
			return true
		end)
		
		var Project project = app.getProject()
		if project != null
			FileSystem.searchFiles(project.getCharactersDirectory().toString(), true, block String each, FileType type
				if type == FileType.file and FileSystem.pathMatchesPattern(File.new(each).getName(), "*.demccp")
					try
						add(loader.load(each, true))
						
					catch Exception e
						console.addError("Failed loading character profile: " + each, e)
						return true
					end
				end
				return true
			end)
		end
		
		pListeners.forEach(block CharacterProfilesListener each
			each.loadingCharactersFinished(this)
		end)
	end
	
	/** Reload characters from directory (*.dmcc). */
	func void reloadAll()
		removeAll()
		loadAll()
	end
	
	/** Save characters to directory (*.dmcc). */
	func void saveAll()
		var LoadCharacterProfile loader = GameApp.getGameApp().getLoadCharacterProfile()
		
		pProfiles.forEach(block CharacterProfile each
			try
				loader.save(each.getPathProfile(), each)
				
			catch Exception e
				GameApp.getGameApp().getConsole().addError("Failed saving character profile: " + each.getName(), e)
			end
		end)
	end
	
	
	
	/** Add listener. */
	func void addListener(CharacterProfilesListener listener)
		if listener == null
			throw ENullPointer.new("listener")
		end
		pListeners.add(listener)
	end
	
	/** Remove listener. */
	func void removeListener(CharacterProfilesListener listener)
		pListeners.remove(listener)
	end
end
