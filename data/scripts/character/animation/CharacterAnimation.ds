/* 
 * Drag[en]gine Motion Capture
 *
 * Copyright (C) 2021, DragonDreams (info@dragondreams.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */

namespace Democap.Characters

pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.LoadSave
pin Dragengine.XML


/**
 * Character animation
 */
class CharacterAnimation
	private var String pName
	private var String pPath
	private var String pPathMeta
	private var String pPathDevices
	private var String pPathDevicesRig
	private var TimeDate pTimestamp
	private var FileUsageManager pFileUsage
	private var Animation pAnimation
	private var Animation pAnimationDevices
	private var Rig pRigDevices
	private var String pCharacterProfile
	
	
	
	/** Create character animation. */
	public func new(String name)
		setName(name)
		pPath = ""
		pPathMeta = ""
		pPathDevices = ""
		pPathDevicesRig = ""
		pTimestamp = TimeDate.new()
		pFileUsage = FileUsageManager.new()
	end
	
	/** Create copy of character animation. */
	public func new(CharacterAnimation copy)
		pName = copy.pName
		pPath = ""
		pPathMeta = ""
		pPathDevices = ""
		pPathDevicesRig = ""
		pTimestamp = copy.pTimestamp
		pAnimation = copy.pAnimation
		pAnimationDevices = copy.pAnimationDevices
		pRigDevices = copy.pRigDevices
		pCharacterProfile = copy.pCharacterProfile
		
		pFileUsage = FileUsageManager.new()
		pFileUsage.setParent(copy.pFileUsage.getParent())
		
		setPath(copy.pPath)
		setPathMeta(copy.pPathMeta)
		setPathDevices(copy.pPathDevices)
		setPathDevicesRig(copy.pPathDevicesRig)
	end
	
	/** Create character animation from file. */
	public func new(FileReader reader)
		var int i, count
		
		pPath = ""
		pPathMeta = ""
		pPathDevices = ""
		pPathDevicesRig = ""
		pFileUsage = FileUsageManager.new()
		
		select reader.readByte() // version
		case 0
			pName = reader.readString8()
			setPath(reader.readString8())
			setPathMeta(reader.readString8())
			pTimestamp = reader.readTimeDate()
			pFileUsage.readFromFile(reader)
			
		case 1
			pName = reader.readString8()
			setPath(reader.readString8())
			setPathMeta(reader.readString8())
			setPathDevices(reader.readString8())
			setPathDevicesRig(reader.readString8())
			pTimestamp = reader.readTimeDate()
			pFileUsage.readFromFile(reader)
			
		else
			throw EInvalidParam.new("unsupported version")
		end
	end
	
	/** Dispose of list. */
	public func void dispose()
		setPath("")
		setPathMeta("")
		setPathDevices("")
		setPathDevicesRig("")
		
		if pFileUsage != null
			pFileUsage.setParent(null)
		end
	end
	
	
	
	/** Name. */
	public func String getName()
		return pName
	end
	
	/** Set name. */
	public func void setName(String name)
		if name == null
			throw ENullPointer.new("name")
		end
		pName = name
	end
	
	/** Path relative to project or empty string. */
	public func String getPath()
		return pPath
	end
	
	/** Set path relative to project or empty string. */
	public func void setPath(String path)
		if path.equals(pPath)
			return
		end
		
		if not pPath.empty()
			pFileUsage.removeFileUsage(pPath)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPath() old(" + pPath + ")")
			end
		end
		
		pPath = path
		
		if not path.empty()
			pFileUsage.addFileUsage(path)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPath() new(" + path + ")")
			end
		end
	end
	
	/** Meta path relative to project or empty string. */
	public func String getPathMeta()
		return pPathMeta
	end
	
	/** Set path relative to project or empty string. */
	public func void setPathMeta(String path)
		if path.equals(pPathMeta)
			return
		end
		
		if not pPathMeta.empty()
			pFileUsage.removeFileUsage(pPathMeta)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPathMeta() old(" + pPathMeta + ")")
			end
		end
		
		pPathMeta = path
		pCharacterProfile = null
		
		if not path.empty()
			pFileUsage.addFileUsage(path)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPathMeta() new(" + path + ")")
			end
		end
	end
	
	/** Devices path relative to project or empty string. */
	public func String getPathDevices()
		return pPathDevices
	end
	
	/** Set devices path relative to project or empty string. */
	public func void setPathDevices(String path)
		if path.equals(pPathDevices)
			return
		end
		
		if not pPathDevices.empty()
			pFileUsage.removeFileUsage(pPathDevices)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPathDevices() old(" + pPathDevices + ")")
			end
		end
		
		pPathDevices = path
		
		if not path.empty()
			pFileUsage.addFileUsage(path)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPathDevices() new(" + path + ")")
			end
		end
	end
	
	/** Devices rig path relative to project or empty string. */
	public func String getPathDevicesRig()
		return pPathDevicesRig
	end
	
	/** Set devices rig path relative to project or empty string. */
	public func void setPathDevicesRig(String path)
		if path.equals(pPathDevicesRig)
			return
		end
		
		if not pPathDevicesRig.empty()
			pFileUsage.removeFileUsage(pPathDevicesRig)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPathDevicesRig() old(" + pPathDevicesRig + ")")
			end
		end
		
		pPathDevicesRig = path
		
		if not path.empty()
			pFileUsage.addFileUsage(path)
			if FileUsageManager.debugUsage
				pFileUsage.log(GameApp.getGameApp().getConsole(),\
					"CharacterAnimation.setPathDevicesRig() new(" + path + ")")
			end
		end
	end
	
	/** File usage. */
	public func FileUsageManager getFileUsage()
		return pFileUsage
	end
	
	/** Animation or null if not created. */
	public func Animation getAnimation()
		return pAnimation
	end
	
	/** Set animation or null if not created. */
	public func void setAnimation(Animation animation)
		pAnimation = animation
	end
	
	/** Devices animation or null if not created. */
	public func Animation getAnimationDevices()
		return pAnimationDevices
	end
	
	/** Set devices animation or null if not created. */
	public func void setAnimationDevices(Animation animation)
		pAnimationDevices = animation
	end
	
	/** Devices rig or null if not created. */
	public func Rig getRigDevices()
		return pRigDevices
	end
	
	/** Set devices rig or null if not created. */
	public func void setRigDevices(Rig rig)
		pRigDevices = rig
	end
	
	/** Load animations and rigs if present and possible. */
	public func void loadAnimation(Project project)
		if not pPath.empty()
			pAnimation = Animation.new((project.getDataDirectory() + pPath).getPath())
		end
		if not pPathDevices.empty()
			pAnimationDevices = Animation.new((project.getDataDirectory() + pPathDevices).getPath())
		end
		if not pPathDevicesRig.empty()
			pRigDevices = Rig.new((project.getDataDirectory() + pPathDevicesRig).getPath())
		end
	end
	
	/** Clear animation. */
	public func void clear()
		pRigDevices = null
		pAnimationDevices = null
		pAnimation = null
		setPath("")
		setPathMeta("")
		setPathDevices("")
		setPathDevicesRig("")
	end
	
	/** Character profile or empty string if unknown. */
	public func String getCharacterProfile(Project project)
		if pCharacterProfile == null
			pCharacterProfile = ""
			
			if not pPathMeta.empty()
				var EasyXML document = EasyXML.new((project.getDataDirectory() + pPathMeta).getPath())
				if document.hasParseFailed()
					GameApp.getGameApp().getConsole().addMessage(document.getParseLog())
					throw EInvalidParam.new("parsing document failed")
				end
				
				document.getRootElement().forEachTag(block EasyXMLElement tag, String tagName
					if tagName.equals("character")
						tag.forEachTag(block EasyXMLElement tag2, String tagName2
							if tagName2.equals("profileName")
								pCharacterProfile = tag2.getFirstCData()
							end
						end)
					end
				end)
			end
		end
		
		return pCharacterProfile
	end
	
	/** Assign from another character animation. */
	public func void assign(CharacterAnimation characterAnimation)
		pTimestamp = characterAnimation.pTimestamp
		pAnimation = characterAnimation.pAnimation
		pAnimationDevices = characterAnimation.pAnimationDevices
		pRigDevices = characterAnimation.pRigDevices
		pCharacterProfile = characterAnimation.pCharacterProfile
		
		setPath(characterAnimation.pPath)
		setPathDevices(characterAnimation.pPathDevices)
		setPathDevicesRig(characterAnimation.pPathDevicesRig)
		setPathMeta(characterAnimation.pPathMeta)
	end
	
	/** Export character animation. */
	public func void export(Project project, String name)
		var Project.AnimationPath path = project.exportAnimationPath(name, "deanim", "derig")
		var File directory = project.getDataDirectory()
		
		if not pPath.empty()
			FileWriter.new((directory + path.getPath()).getPath()).writeData(\
				FileReader.new((directory + pPath).getPath()))
		end
		
		if not pPathDevices.empty()
			FileWriter.new((directory + path.getPathDevices()).getPath()).writeData(\
				FileReader.new((directory + pPathDevices).getPath()))
		end
		
		if not pPathDevicesRig.empty()
			FileWriter.new((directory + path.getPathDevicesRig()).getPath()).writeData(\
				FileReader.new((directory + pPathDevicesRig).getPath()))
		end
		
		var EasyXML document = EasyXML.new((directory + pPathMeta).getPath())
		if document.hasParseFailed()
			GameApp.getGameApp().getConsole().addMessage(document.getParseLog())
			throw EInvalidParam.new("parsing document failed")
		end
		
		document.getRootElement().forEachTag(block EasyXMLElement tag, String tagName
			if tagName.equals("capturedAnimation")
				tag.forEachTag(block EasyXMLElement tag2, String tagName2
					if tagName2.equals("pathAnimation")
						tag2.getElementAt(0).setCDataContent(path.getPath())
					end
				end)
				
			elif tagName.equals("capturedAnimationDevices")
				tag.forEachTag(block EasyXMLElement tag2, String tagName2
					if tagName2.equals("pathAnimation")
						tag2.getElementAt(0).setCDataContent(path.getPathDevices())
						
					elif tagName2.equals("pathRig")
						tag2.getElementAt(0).setCDataContent(path.getPathDevicesRig())
					end
				end)
			end
		end)
		
		document.writeToFile(FileWriter.new((directory + path.getPathMeta()).getPath()), false)
	end
	
	
	
	/** Write to file. */
	public func void writeToFile(FileWriter writer)
		writer.writeByte(1) // version
		
		writer.writeString8(pName)
		writer.writeString8(pPath)
		writer.writeString8(pPathMeta)
		writer.writeString8(pPathDevices)
		writer.writeString8(pPathDevicesRig)
		writer.writeTimeDate(pTimestamp)
		pFileUsage.writeToFile(writer)
	end
end
